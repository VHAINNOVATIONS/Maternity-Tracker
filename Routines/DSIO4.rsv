Routine DSIO4 saved using VFDXTRS routine on May 20, 2015 09:21
DSIO4^INT^63692,31310^May 20, 2015@08:41
DSIO4 ;DSS/TFF - DSIO PREGNANCY X-REF AND FILE SUPPORT;06/28/2013 15:19
 ;;1.0;DSIO 1.0;;Feb 06, 2015;Build 1
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
 ; ------------------------------- UTILITIES ----------------------------------
PG(DFN) ; GET CURRENT PREGNANCY
 N SCR,OUT,ERR
 S SCR="I $P(^(0),U,3)=DFN,$P(^(0),U,4)=""C"""
 D LIST^DIC(19641.13,,"@","BP",1,,,"P",SCR,,"OUT","ERR")
 Q $G(OUT("DILIST",1,0))
 ;
EDDC(DFN) ; GET EDD FOR COMPUTED FIELD
 Q $$FMTE^XLFDT($$GET1^DIQ(19641.03,$$GET1^DIQ(19641.13,$$PG^DSIO4($G(DFN))_",",.06,"I"),.01,"I"),"5Z")
 ;
WVPG(DFN) ; GET WV PATIENT CURRENT PREGNANCY
 N SCR,OUT,ERR,CT,WV
 S SCR="I $P(^(0),U,2)=1,'$P(^(0),U,4)"
 D LIST^DIC(790.16,","_DFN_",","@;3I","P",,,,,SCR,,"OUT","ERR")
 Q:+$G(OUT("DILIST",0))=1 $G(OUT("DILIST",1,0))
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,2)<1
 .S:$P(@CT,U,2) WV($P(@CT,U,2))=+@CT
 Q WV($O(WV(""),-1))
 ;
WVLMP(DFN) ; GET WV LAST MENSTRUAL PERIOD
 N SCR,OUT,ERR,CT,WV
 S SCR="I $P(^(0),U,2)"
 D LIST^DIC(790.15,","_DFN_",","@;2I","P",,,,,SCR,,"OUT","ERR")
 Q:+$G(OUT("DILIST",0))=1 $G(OUT("DILIST",1,0))
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,2)<1
 .S:$P(@CT,U,2) WV($P(@CT,U,2))=+@CT
 Q WV($O(WV(""),-1))
 ;
PSTAT(DFN) ; CURRENTLY PREGNANT
 I $$PG(DFN) Q "YES"
 Q "NO"
 ;
LACT(DFN) ; CURRENTLY LACTATING
 N FLG,SCR,OUT,ERR
 ; Because no one knows the state I'm in...
 I '$$VFILE^DILFD(790.17) D  Q $G(FLG,"NO")
 .S SCR="I '$P(^(0),U,2)"
 .D LIST^DIC(19641.04,","_DFN_",","@","BP",1,,,,SCR,,"OUT","ERR")
 .I $G(OUT("DILIST",1,0)) S FLG="YES"
 ; IF LACTATION STATUS = YES AND NO END DATE
 S SCR="I $P(^(0),U,2)=1,'$P(^(0),U,3)"
 D LIST^DIC(790.17,","_DFN_",","@","BP",1,,,,SCR,,"OUT","ERR")
 I $G(OUT("DILIST",1,0)) Q "YES"
 Q "NO"
 ;
LACE(DFN) ; CURRENTLY LACTATING ENTRY
 N FLG,SCR,OUT,ERR
 ; Because no one knows the state I'm in...
 I '$$VFILE^DILFD(790.17) D  Q $G(FLG)
 .S SCR="I '$P(^(0),U,2)"
 .D LIST^DIC(19641.04,","_DFN_",","@","BP",1,,,,SCR,,"OUT","ERR")
 .S FLG=+$G(OUT("DILIST",1,0))
 S SCR="I $P(^(0),U,2)=1,'$P(^(0),U,3)"
 D LIST^DIC(790.17,","_DFN_",","@","BP",1,,,,SCR,,"OUT","ERR")
 I $G(OUT("DILIST",1,0)) Q +$G(OUT("DILIST",1,0))
 Q ""
 ;
PGL(DFN) ; GET LAST PREGNANCY HISTORY ENTRY
 N SCR,OUT,ERR S SCR="I $P(^(0),U,3)=DFN"
 D LIST^DIC(19641.13,,"@","BP",1,,,"P",SCR,,"OUT","ERR")
 Q $G(OUT("DILIST",1,0))
 ;
PGE(DFN) ; GET LAST PREGNANCY HISTORY ENTRY WITH AN END
 N SCR,OUT,ERR S SCR="I $P(^(0),U,3)=DFN,$P(^(0),U,7)'="""""
 D LIST^DIC(19641.13,,"@","BP",1,,,"P",SCR,,"OUT","ERR")
 Q $G(OUT("DILIST",1,0))
 ;
PGEL(DFN) ; GET LAST PREGNANCY HISTORY ENTRY WITH A LIVE BIRTH
 ; PREGNANCY MUST HAVE AN OBSERVATION USING LOINC 75092-7
 N SCR,OUT,ERR,PN,OBS,POBS,FLG
 S SCR="I $P(^(0),U,3)=DFN,$P(^(0),U,7)'="""""
 ; GET PREGNANCIES WITH AN END DATE WITH OLDEST FIRST (B FLAG DOESN'T WORK)
 D LIST^DIC(19641.13,,"@","P",,,,"P",SCR,,"OUT","ERR")
 I $D(OUT) S PN="" F  S PN=$O(OUT("DILIST",PN),-1) Q:PN=""!(PN=0)  D  Q:$D(FLG)
 .; LOOK AT MOST RECENT PREGNANCIES FIRST AND GET OBSERVATIONS WITH THAT
 .; PREGNANCY AND A CODY SYSTEM OF LOINC WITH A CODE OF 75092-7
 .S SCR="I $P(^(0),U,4)="_OUT("DILIST",PN,0)_",$P(^(0),U,7)=""LNC"""
 .K OBS D LIST^DIC(19641.12,,"@;.06E","P",,,,"P",SCR,,"OBS","ERR")
 .I $D(OBS) K POBS S POBS=$NA(OBS) F  S POBS=$Q(@POBS) Q:POBS=""  D  Q:$D(FLG)
 ..I $P(@POBS,U,2)="75092-7" S FLG=OUT("DILIST",PN,0)
 Q $G(FLG)
 ;
FC(DFN,PIEN) ; GET FETUS COUNT FOR A PREGNANCY
 N RET,CT,OUT,FLG Q:'$G(DFN) ""
 D OBG^DSIO10(.RET,DFN,,$G(PIEN),,,,"PREGNANCY",,"FetusBabyCount")
 S CT=$NA(@RET) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,2)'="DSIO OBG"  D  Q:$D(FLG)
 .I $QS(CT,3)=1 S OUT=+@CT,FLG=1
 Q $G(OUT)
 ;
TRACK(DFN,FL) ; GET TRACKING STATUS OF PATIENT
 N TR S TR=$O(^DSIO(19641.2,"S",DFN,""))
 Q:$D(FL) $S(TR="T":1,TR="F":2,1:0)
 Q $S(TR="T":"YES",TR="F":"FLAGGED",1:"NO")
 ;
 ;=============================================================================
 ;                       X - R E F  S U P P O R T
 ;=============================================================================
 ;
 ; DSIO PREGNANCY HISTORY
 ;  - IF C ALL OTHER ENTRIES WILL BE HISTORICAL (ONLY ONE C)
 ;    EXCEPT - IF ANOTHER ENTRY HAS A FUTURE EDD AND NO PREGNANCY END THEN
 ;    THE ENTRY WILL NOT BE CREATED.
 ;  - IF EDD IS FUTURE AND THE ENTRY IS CURRENT THEN WV PATIENT .13 IS
 ;    CHANGED TO YES AND .14 IS SET WITH EDD
 ;  - IF PREGNANCY END (NO FUTURE DATES) AND NOT A HISTORICAL ENTRY THEN THE
 ;    STATUS IS CHANGED TO H AND WV PATIENT .13 IS NO AND .14 IS DELETED
 ;
CURRENT(IEN) ; SHOULD THIS RECORD BE CURRENT?
 ; IS THERE AN ENTRY WITH A FUTURE EDD AND NO PREGNANCY END?
 ; INPUT TRANSFORM FOR .04 STATUS
 N DFN,FROM,SCR,OUT,ERR,CT,FLG S FLG=1
 S DFN=$$GET1^DIQ(19641.13,IEN_",",.03,"I")
 S FROM(1)=DFN,SCR="I $P(^(0),U,3)=DFN"
 D LIST^DIC(19641.13,,"@;.06I;.07I","P",,".FROM",,"P",SCR,,"OUT","ERR")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:FLG=0
 .Q:$QS(CT,2)<1
 .I $$GET1^DIQ(19641.03,$P(@CT,U,2)_",",.01,"I")>=DT&($P(@CT,U,3)="") S FLG=0
 Q FLG
 ;
HIS(DFN,IEN) ; KILL HISTORICAL EDD X-REF
 ; NEW 'ASTATUS'
 I $D(^DSIO(19641.13,"EDD",DFN,+$O(^DSIO(19641.13,"EDD",DFN,"")),IEN)) D
 .K ^DSIO(19641.13,"EDD",DFN)
 Q
 ;
STATUS(DFN,IEN) ; UPDATE PREGNANCY HISTORY STATUSES TO H IF CURRENT IS C
 ; NEW 'ASTATUS'
 ;Q:$D(DSIOSILENT)  N DSIOSILENT,DLAYGO S DSIOSILENT=1
 Q:$$GET1^DIQ(19641.13,$G(IEN)_",",.04,"I")'="C"
 N FROM,SCR,OUT,ERR,CT,IPT,WVIEN,EDD,RET
 S FROM(1)=DFN,SCR="I $P(^(0),U,3)=DFN,$P(^(0),U,4)=""C"""
 D LIST^DIC(19641.13,,"@","P",,".FROM",,"P",SCR,,"OUT","ERR")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  K IPT D
 .Q:$QS(CT,2)<1
 .I @CT'=IEN D
 ..S DLAYGO=19641.13
 ..S IPT(19641.13,@CT_",",.04)="H" D UPDATE^DIE(,"IPT") K IPT
 ..D HIS(DFN,@CT)
 ..S WVIEN=$$GET1^DIQ(19641.13,@CT_",",99.1,"I")
 ..S DLAYGO=790
 ..S IPT(790.16,WVIEN_","_DFN_",",2)=0 D UPDATE^DIE(,"IPT") K IPT
 Q
 ;
EDD(EDD,IEN) ; UPDATE WV,DSIO PATIENT FILES WHEN EDD IS FUTURE
 ; NEW 'EDD' (NOT ON HISTORICAL ENTRIES)
 ; CREATE AN "EDD" X-REF - ONLY ONE PER PATIENT
 I '$G(DFN) S DFN=$$GET1^DIQ(19641.13,IEN_",",.03,"I") Q:'DFN
 K ^DSIO(19641.13,"EDD",DFN)
 I EDD'="@" D
 .S ^DSIO(19641.13,"EDD",DFN,EDD,DA)="",EDD=$$GET1^DIQ(19641.03,EDD_",",.01,"I")
 Q:$D(DSIOSILENT)  N DSIOSILENT,DLAYGO S DSIOSILENT=1
 Q:$$GET1^DIQ(19641.13,IEN_",",.04,"I")="H"
 N WVIEN,IPT,DLAYGO
 S WVIEN=$$GET1^DIQ(19641.13,IEN_",",99.1,"I")
 S DLAYGO=790
 S IPT(790.16,WVIEN_","_DFN_",",2)=1,IPT(790.16,WVIEN_","_DFN_",",3)=EDD
 D UPDATE^DIE(,"IPT") K IPT
 D STATUS(DFN,IEN)
 Q
 ;
DELV(DATE,DFN,IEN) ; UPDATE WV,DSIO PATIENT FILES WHEN PREGNANCY END IS SET
 Q:$D(DSIOSILENT)  N DSIOSILENT S DSIOSILENT=1
 Q:+$G(DATE)=0!($$GET1^DIQ(19641.13,IEN_",",.04,"I")="H")
 I DATE'>$$NOW^XLFDT D
 .N DLAYGO,IPT,WVIEN
 .S DLAYGO=19641.13,IPT(19641.13,IEN_",",.04)="H" D UPDATE^DIE(,"IPT") K IPT
 .K ^DSIO(19641.13,"EDD",DFN)
 .S DLAYGO=790,WVIEN=$$GET1^DIQ(19641.13,IEN_",",99.1,"I")
 .S IPT(790.16,WVIEN_","_DFN_",",2)=0,IPT(790.16,WVIEN_","_DFN_",",3)=""
 .S IPT(790.16,WVIEN_","_DFN_",",4)=DATE
 .D UPDATE^DIE(,"IPT") K IPT D PTRK(DFN,0)
 Q
 ;
 ; -------------------------- WV PATIENT - (NEW) ------------------------------
 ;
WV(DFN,WVIEN) ; WOMEN'S HEALTH ENTRY POINT
 ; UPDATE THE CURRENT PREGNANCY WITH THE NEW EDD FROM WV PREGNANCY LOG
 ; OR IF THERE IS NO CURRENT PREGNANCY CREATE A NEW ONE
 ;
 ; PREGNANCY LOG IS TRIGGERED ON FIELD AND THIS NEW STYLE IS SET
 ; TO TRIGGER ON RECORD AND THE CREATION OF THE LOG
 ;
 ; PREG(RET,DFN,IEN,STRT,TYP,FOF,EDD,END,OBP,PFAC)
 ;
 Q:'$G(WVIEN)!('$G(DFN))
 N DLAYGO,PREG,EDD,END
 S DLAYGO=19641.13,CIEN=$$PG(DFN)
 S PREG=$$GET1^DIQ(790.16,WVIEN_","_DFN_",",2,"I")
 S EDD=$$GET1^DIQ(790.16,WVIEN_","_DFN_",",3,"I")
 S END=$$GET1^DIQ(790.16,WVIEN_","_DFN_",",4,"I") K:'END END
 I PREG=1,'CIEN D PREG^DSIO15(.RET,DFN,,,"C",,EDD),PTRK(DFN,PREG)
 E  I PREG=1,CIEN,EDD D PREG^DSIO15(.RET,DFN,CIEN,,,,EDD)
 E  I PREG=0,CIEN D PREG^DSIO15(.RET,DFN,CIEN,,"H",,,$G(END,DT)),PTRK(DFN,PREG)
 I $G(RET) K IPT S IPT(19641.13,RET_",",99.1)=WVIEN D UPDATE^DIE(,"IPT")
 Q
 ;
PTRK(DFN,PREG) ; TRACK PREGNANCY
 N RES,RET
 S RES="WV PATIENT FILE(790) PREGNANCY STATUS = "_$S(PREG:"YES",1:"NO")
 D REC^DSIO1(.RET,DFN,2,RES,,"TRIGGER")
 Q
 ;
 ; -------- WV PATIENT TO DSIO MENSTRUAL HISTORY AND DSIO PATIENT -------------
 ;
MH(DFN,WVIEN) ; UPDATE DSIO WITH LMP
 ;
 ; STARTING LOCATION = 790.15,2
 ; DSIO MENSTRUAL HISTORY (LINK BACK AT FIELD 99.1)
 ; DSIO PATIENT : 1.2
 ;
 Q:$D(DSIOSILENT)!('$G(WVIEN))!('$G(DFN))
 N DATE,FLG,IPT,IEN,DLAYGO,DSIOSILENT
 S DSIOSILENT=1,DLAYGO=19641.01
 S DATE=$$GET1^DIQ(790.15,WVIEN_","_DFN_",",2,"I") Q:'DATE
 Q:$D(^DSIO(19641.01,"C",DFN,DATE))
 S IPT(19641.01,"+1,",.01)=DATE,IPT(19641.01,"+1,",.02)=DFN
 D UPDATE^DIE("U","IPT","IEN") K IPT S DLAYGO=19641
 I $D(IEN(1)) S IPT(19641,DFN_",",1.2)=IEN(1) D FILE^DIE(,"IPT")
 Q
 ;
MH1(DFN,DATE) ; FROM DSIO PATIENT TO WV PATIENT
 Q:$D(DSIOSILENT)!('$G(DATE))
 N DSIOSILENT,DLAYGO,IPT,WVIEN S DSIOSILENT=1
 S DLAYGO=790
 S IPT(790.15,"?+1,"_DFN_",",.01)=$$NOW^XLFDT
 S IPT(790.15,"?+1,"_DFN_",",2)=$$GET1^DIQ(19641.01,DATE_",",.01,"I")
 S IPT(790.15,"?+1,"_DFN_",",3)=DUZ
 D UPDATE^DIE(,"IPT","WVIEN") Q:'$G(WVIEN(1))
 S DLAYGO=19641.01
 S IPT(19641.01,DATE_",",99.1)=WVIEN(1) D UPDATE^DIE(,"IPT") K IPT
 Q
