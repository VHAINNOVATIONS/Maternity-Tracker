Routine DSIO03 saved using VFDXTRS routine on Oct 09, 2015 10:03
DSIO03^INT^63826,55268^Oct 01, 2015@15:21
DSIO03 ;DSS/TFF - DSIO OBSERVATION PUSH;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**2**;Feb 06, 2015;Build 18
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
CREATE(RET,NA,CD,SY,DL) ; Create Observation PUSH
 ;
 ; NA = NAME
 ; CD = CODE
 ; SY = CODE SYSTEM
 ; DL = 0:FALSE, 1:TRUE, 2:BOTH
 ; *For delete observation and transaction
 ;
 N FDA,IEN S RET=0
 Q:$G(NA)=""!($G(CD)="")
 S FDA(19641.123,"?+1,",.01)=NA
 S FDA(19641.123,"?+1,",.02)=CD
 S FDA(19641.123,"?+1,",.03)=$G(SY,"OTHER")
 S FDA(19641.123,"?+1,",.04)=$S(+$G(DL)<3&(+$G(DL)>0):DL,1:0)
 D UPDATE^DIE(,"FDA","IEN") S:$G(IEN(1)) RET=IEN(1)
 Q
 ;
 ; ----------------------------------------------------------------------------
 ;
LAS ; LACTATION STATUS (CODE: Lactating)
 ;
 ; DSIO PATIENT #19641
 ;   3  LACTATION HISTORY (Multiple-19641.04), [3;0]
 ;         .01  LACTATION DATE (MD), [0;1]
 ;         .02  END DATE (D), [0;2]
 ;
 Q:$G(X)=""!('$G(DFN))
 N FLE,IPT,IENS S FLE=19641.04
 I X="YES" D
 . Q:$$LACT^DSIO4(DFN)="YES"
 . S IPT(FLE,"?+1,"_DFN_",",.01)=$$NOW^XLFDT
 I X="NO" D
 . Q:$$LACT^DSIO4(DFN)="NO"
 . S IENS=$$LACE^DSIO4(DFN)_","_DFN_","
 . S:'IENS IENS="?+1,"_DFN_",",IPT(FLE,IENS,.01)=$$NOW^XLFDT
 . S IPT(FLE,IENS,.02)=$$NOW^XLFDT
 D:$D(IPT) UPDATE^DIE(,"IPT")
 Q
 ;
PATIENT(FLD) ; Update Patient Element
 Q:$G(X)=""!('$G(DFN))
 N DIE,DA,DR
 S DIE="^DSIO(19641,",DA=DFN,DR=FLD_"///"_X D ^DIE
 Q
