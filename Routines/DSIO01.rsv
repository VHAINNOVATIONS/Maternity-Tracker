DSIO01 ;DSS/TFF - DSIO OCNT PUSH SUPPORT;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**1**;Feb 06, 2015;Build 19
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ; FASTVIT^ORQQVI
 ;
 ;
 ;
 ; PUSH DEFINED VARIABLES
 ; ----------------------
 ;   DFN = PATIENT IEN
 ;   ELE = DSIO NOTE ELEMENT IEN
 ;  DIEN = DSIO OCNT DIALOG IEN
 ; FNAME = INTERFACE NAME
 ;  FIEN = DSIO OCNT CONFIGURATION IEN
 ;  CIEN = DSIO OCNT CONFIGURATION CONTROL MULTIPLE IEN
 ;  PUSH = IEN FOR EITHER DSIO REPORT ITEMS OR DSIO REMINDER ELEMENTS
 ;  RIEN = REMINDER DIALOG IEN (PARENT)
 ; RIENS = REMINDER DIALOG IEN
 ;
 Q
 ;
FLOW ; PARSE FLOWSHEET AND PUSH VALUES
 ;
 ; SHARED DIALOG GLOBAL: ^DSIO(19641.4941,DFN,2,DIEN,
 ;
 N LOC,CT,COLS,RATE,DATE,RDT,RET,FCT,PRT,BIEN,OBJ,OBJECT,NAR,ORT
 S LOC=$NA(^TMP($J,"DSIO FLOW")) K @LOC
 D GETS^DIQ(19641.45,$G(ELE)_",",1,"E",LOC) K @LOC@(19641.45,+$G(ELE)_",",1,"E")
 S CT=$NA(@LOC) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO FLOW")  D
 . ; SET THE DATE FOR THE ROW
 . I +@CT=0,$P($P(@CT,U),",",2)'=0 S DATE($P($P(@CT,U),",",2))=$P(@CT,U,2)
 . I $P($P(@CT,U),",",2)=0,$P(@CT,U,2)["Rate" D
 . . ; GET THE COLUMN AND THE RATE COUNT
 . . S COLS=$G(COLS)_+@CT_"|"_$P($P(@CT,U,2)," ",2)_U
 . Q:$P($P(@CT,U),",",2)=0
 . ; IF THIS COLUMN IS UNDER THE RATE HEADER THEN THE SECOND PIECE IS RATE VAL
 . S RATE=$$COL(+@CT) I RATE S FCT=$G(FCT)+1 D
 . . S RDT=DATE(+$P($P(@CT,U),",",2))
 . . Q:$$OBS(RDT,FCT,249043002)  ; HAS THIS BEEN OBSERVED ALREADY?
 . . ; SCT 249043002 = Fetal heart rate
 . . K PRT D PREG^DSIO15(.PRT,$$PG^DSIO4(DFN),DFN,,,,,,"+",,,1) Q:+PRT<1
 . . S BIEN=+$P(PRT,U,2) Q:'BIEN
 . . S (OBJ(0),OBJECT($O(OBJECT(""),-1)+1))="FB."_BIEN
 . . S OBJ(1)="PG."_$$PG^DSIO4(DFN)
 . . K ORT D OBS^DSIO10(.ORT,,DFN,RDT,.OBJ,,,"FETAL HEART RATE","SCT^^249043002","^^"_RATE,,,,,1)
 . S NAR(0)="Fetus/Baby Count",OBJECT(0)="PG."_$$PG^DSIO4(DFN)
 . K ORT D:$G(FCT) OBS^DSIO10(.ORT,,DFN,$$NOW^XLFDT,.OBJECT,,,"PREGNANCY","OTHER^^FetusBabyCount","^^"_FCT,,,,.NAR,1)
 K @LOC
 Q
 ;
COL(CK) ; ARE WE LOOKING FOR THIS COLUMN?
 Q:$G(COLS)="" 0
 N P,FLG S FLG=0 F P=1:1:$L(COLS,U) I +$P(COLS,U,P)=CK S FLG=$P($P(COLS,U,P),"|",2) Q
 Q FLG
 ;
OBS(DATE,FCT,CODE) ; HAS THIS OBSERVATION BEEN CREATED ALREADY?
 Q:DATE="" 1
 N PREG,BIEN,OBJ,RET
 S PREG=$$PG^DSIO4(DFN) Q:'PREG 0
 S BIEN=$O(^DSIO(19641.112,"C",DFN,PREG,FCT,"")) Q:'BIEN 0
 S OBJ(0)="PG."_PREG,OBJ(1)="FB."_BIEN
 K RET D OBG^DSIO10(.RET,,DFN,.OBJ,,,,,,"SCT",CODE) Q:+$G(@RET@(0))<1 0
 Q 1
 ;
EDD(TYP,OVR) ; CREATE EDD OBSERVATIONS
 ;
 ; Piece 1 is type date
 ; Piece 2 is "IsFinal"
 ; Piece 3 is Gestational Age in days
 ; Piece 4 is EDD
 ;
 ; OVR = IF SET THEN UPDATE EVEN IF FALSE (USED FOR DUAL PURPOSED LMP UPDATE)
 ;
 ; OBS(RET,DFN,IEN,PIEN,BIEN,DATE,CAT,SYS,CODE,DESC,VAL)
 ; OBS(.RET,DFN,,,,$$NOW^XLFDT,"EDD")
 ;
 ; RUN ROUTINE WILL BE ON THE EDD COLUMN ELEMENTS AND THEY WILL CREATE
 ; OBSERVATIONS AND NOT PUSH - ONLY AN OBSERVATION OF "ISFINAL" WILL
 ; PUSH AND "DSIO OBSERVATION PUSH" WILL HANDLE THAT
 ;
 ; DFN,@VAL(1),ELE(19641.45 IEN),OUT(LIST OF CONTROLS FOR THE NOTE)
 ;
 Q:'$D(@VAL)  Q:@VAL@($O(@VAL@("")))=""
 N RET,CAT,SYS,CODE,DES,DY,ISFIN,GESD,GESW,EDD,VALUE,LMPQ,OBJECT
 ; *** LMP
 I TYP="LMP" S SYS="LOINC",CODE="8665-2",DES(0)="Last Menstrual Period"
 ; *** CONCEPTION
 I TYP="ECD" S SYS="LOINC",CODE="33067-0",DES(0)="Estimated Conception Date"
 ; *** ULTRASOUND
 I TYP="ULTRA" S SYS="LOINC",CODE="11884-4",DES(0)="Ultrasound"
 ; *** EMBRYO
 I TYP="EMBRYO" S DY=$$VAL("SPNTRANSFERDAY") Q:DY=""  D
 . S SYS="OTHER",CODE="EmbryoTransfer",DES(0)="Embryo Transfer ("_$G(DY)_"-days)"
 ; *** OTHER
 I TYP="OTHER" S DES(0)=$$VAL("LBLOTHER") Q:DES(0)=""!(DES(0)="Enter Other Criteria")  D
 . S SYS="OTHER",CODE="OtherEdd",DES(0)=$$VAL("LBLOTHER")
 ; *** UNKNOWN
 I TYP="UNKNOWN" Q:$$VAL("CKFINALEDD"_TYP)'="TRUE"  D
 . S SYS="LOINC",CODE="11778-8",DES(0)="Unknown",EDD=$$VAL("DTEDD"_TYP)
 ; *** VALUE
 S ISFIN=$$VAL("CKFINALEDD"_TYP)
 S GESD=$$VAL("EDTDAY"_TYP) I GESD="" S GESD=$$VAL("SPNDAY"_TYP)
 S GESW=$$VAL("EDTWEEK"_TYP) I GESW="" S GESW=$$VAL("SPNWEEK"_TYP)
 S GESD=GESD+(GESW*7)
 S:'$D(EDD) EDD=$$VAL("EDTEDD"_TYP)
 S VALUE=@VAL@($O(@VAL@("")))_"|"_$S(ISFIN'="":"TRUE",1:"FALSE")_"|"_$S(+GESD>0:GESD,1:"")
 S VALUE=VALUE_"|"_$S($G(EDD):EDD,1:"")
 I TYP="LMP" D
 . S LMPQ=$$VAL("LMPQ"),VALUE=VALUE_"|"_LMPQ
 . I '$P(VALUE,"|",3),'$P(VALUE,"|",4)
 . I $G(OVR) S $P(VALUE,"|",2)="TRUE"
 S OBJECT(0)="PG."_$$PG^DSIO4(DFN)
 D OBS^DSIO10(.RET,,DFN,$$NOW^XLFDT,.OBJECT,,,$S($D(LMPQ):"LMP",1:"EDD"),SYS_"^^"_CODE,"^^"_VALUE,,,,.DES,1)
 I TYP="LMP",$D(LMPQ) D OBSLMP
 Q
 ;
EDDCAL(TYP) ; EDD OBSERVATION CALCULATOR ENTRY
 Q:'$P($G(X),"|")
 N TVAL,EDD,IPT,RT,LMPI
 S TVAL=$$DT^DSIO2($P(X,"|")) ; EDC,LMP,ULTRASOUND,EMBRYO,OTHER,UNKNOWN DATE VALUE
 I $$UP^XLFSTR($P(X,"|",2))="TRUE"!($$UP^XLFSTR($P(X,"|",2))="YES") D
 . S EDD=$P(X,"|",4) I EDD="" S EDD=$$CAL(TYP,TVAL,$P(X,"|",3))
 . D OBEFLE
 I TYP="LMP",$$UP^XLFSTR($P(X,"|",2))="TRUE" D
 . S IPT(19641.01,"?+1,",.01)=TVAL
 . S IPT(19641.01,"?+1,",.02)=DFN
 . S IPT(19641.01,"?+1,",.03)=DUZ
 . S IPT(19641.01,"?+1,",.04)=$$NOW^XLFDT
 . S IPT(19641.01,"?+1,",.05)=$P(X,"|",5)
 . D UPDATE^DIE(,"IPT","LMPI") K IPT I $G(LMPI(1)) D
 . . S IPT(19641,$G(DFN)_",",1.2)=LMPI(1) D UPDATE^DIE(,"IPT") K IPT
 I TYP="EDC" D PREG^DSIO15(.RT,$$PG^DSIO4(DFN),DFN,TVAL,,,,,,,,1)
 Q
 ;
CAL(TYP,VAL,GES) ; EDD CALCULATOR
 N RET
 I TYP="LMP" S RET=$$FMADD^XLFDT(VAL,280)
 I TYP="ECD" S RET=$$FMADD^XLFDT(VAL,266)
 I TYP="ULT" S RET=$$FMADD^XLFDT(VAL,(280-GES))
 I TYP="EMB" S RET=$$FMADD^XLFDT(VAL,262)
 I TYP="OTH" S RET=$$FMADD^XLFDT(VAL,(280-GES))
 I TYP="UNK" S RET=VAL
 Q $G(RET)
 ;
OBEFLE ; FILE EDD OBSERVATION
 N IPT,EIEN,PG,RT Q:'$G(EDD)
 I +$G(PIEN)>0 S PG=PIEN,EIEN=$$GET1^DIQ(19641.13,PG_",",.06,"I")
 E  S PG=$$PG^DSIO4(DFN),EIEN=$$GET1^DIQ(19641.13,PG_",",.06,"I")
 S IPT(19641.03,EIEN_",",1.1)=$P($$GET1^DIQ(19641.123,IEN_",",.01),"EDD BY ",2)
 S IPT(19641.03,EIEN_",",1.2)="YES"        ; IS FINAL
 S IPT(19641.03,EIEN_",",2.2)="`"_OIEN     ; OBSERVATION
 D FILE^DIE("E","IPT") K IPT
 D PREG^DSIO15(.RT,PG,DFN,"^^"_EDD,,,,,,,,1)
 Q
 ;
OBSLMP ; OBSERVATION FOR RECORDING ADDITIONAL DATA ELEMENTS FOR LMP
 N IPT,LIEN,MYES,MNO,AYES,ANO,DYES,DNO,CYES,CNO,NVAL,CONIEN
 S LIEN=$$GET1^DIQ(19641,DFN_",",1.2,"I") Q:LIEN=""
 S MYES=$$VAL("CKMENSESYES"),MNO=$$VAL("CKMENSESNO")
 S IPT(19641.01,LIEN_",",1.1)=$S($G(MYES)'="":MYES,$G(MNO)'="":MNO,1:"")
 S IPT(19641.01,LIEN_",",1.2)=$$VAL("EDTFREQUENCY")
 S AYES=$$VAL("CKAMOUNTYES"),ANO=$$VAL("CKAMOUNTNO")
 S IPT(19641.01,LIEN_",",1.3)=$S($G(AYES)'="":AYES,$G(ANO)'="":ANO,1:"")
 S DYES=$$VAL("CKDURATIONYES"),DNO=$$VAL("CKDURATIONNO")
 S IPT(19641.01,LIEN_",",1.4)=$S($G(DYES)'="":DYES,$G(DNO)'="":DNO,1:"")
 S CYES=$$VAL("CKCONTRACEPTIONYES"),CNO=$$VAL("CKCONTRACEPTIONNO")
 S IPT(19641.01,LIEN_",",1.5)=$S($G(CYES)'="":CYES,$G(CNO)'="":CNO,1:"")
 S IPT(19641.01,LIEN_",",1.6)=$$DT^DSIO2($$VAL("EDTHCG")) ; hCG+
 D FILE^DIE("E","IPT") K IPT
 D VALWP("MEMLMP",19641.01,2,LIEN) ; COMMENTS
 Q
 ;
OBLAC ; OBSERVATION PUSH LACTATION STATUS
 Q:$G(X)=""!('$G(DFN))
 N FLE,IPT,IENS
 ; Because no one knows the state I'm in...
 S FLE=790.17 I '$$VFILE^DILFD(790.17) S FLE=19641.04
 I X="YES" D
 . Q:$$LACT^DSIO4(DFN)="YES"
 . S IPT(FLE,"?+1,"_DFN_",",.01)=$$NOW^XLFDT
 . S:FLE=790.17 IPT(FLE,"?+1,"_DFN_",",2)=1
 . S IPT(FLE,"?+1,"_DFN_",",4)=DUZ
 I X="NO" D
 . Q:$$LACT^DSIO4(DFN)="NO"
 . S IENS=$$LACE^DSIO4(DFN)_","_DFN_","
 . I +IENS<1 S IENS="?+1,"_DFN_",",IPT(FLE,IENS,.01)=$$NOW^XLFDT
 . S:FLE=790.17 IPT(FLE,IENS,2)=0
 . S IPT(FLE,IENS,$S(FLE=790.17:3,1:.02))=$$NOW^XLFDT
 . S IPT(FLE,IENS,4)=DUZ
 D:$D(IPT) UPDATE^DIE(,"IPT")
 Q
 ;
FAM ; PROCESS FAMILY HISTORY FOR DSIO PERSON
 ;
 ; FFSAVE(RET,DFN,IEN,NAME,DOB,ADDR,PHONE,EDU,SEX,REL,STAT,PROB)
 ;
 ; VARIABLES DEFINED: DFN, DIEN = DIALOG IEN, CIEN = CONTROL IEN,
 ;                    ELE = DATA ELEMENT IEN
 N LOC,CT,NAME,DOB,ADDR,PHONE,EDU,SEX,REL,STAT,PROB,RET
 S LOC=$NA(^TMP($J,"DSIO FAM")) K @LOC
 D GETS^DIQ(19641.45,$G(ELE)_",",1,"E",LOC) K @LOC@(19641.45,+$G(ELE)_",",1,"E")
 S CT=$NA(@LOC) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO FAM")  D
 . I $$FAMP(@CT,"RELATIONSHIP") D
 . . I $D(REL) D FAMS
 . . S REL=$$FAMR(@CT),SEX=$$CR(REL)
 . I $$FAMP(@CT,"NAME") S NAME=$$FAMR(@CT)
 . I $$FAMP(@CT,"STATUS") S STAT=$$FAMR(@CT)
 . I $$FAMP(@CT,"DATE OF BIRTH") S DOB=$$DT^DSIO2($$FAMR(@CT))
 . I $$FAMP(@CT,"YEARS OF EDUCATION") S EDU=$$FAMR(@CT)
 . I $$FAMP(@CT,"SEX") S SEX=$$FAMR(@CT)
 . ; BUILD ARRAYS ADDR,PHONE,PROB
 . I $$FAMP(@CT,"STREET LINE 1") S ADDR($O(ADDR(""),-1)+1)="1^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"STREET LINE 2") S ADDR($O(ADDR(""),-1)+1)="2^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"STREET LINE 3") S ADDR($O(ADDR(""),-1)+1)="3^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"CITY") S ADDR($O(ADDR(""),-1)+1)="CITY^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"STATE") S ADDR($O(ADDR(""),-1)+1)="STATE^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"ZIP") S ADDR($O(ADDR(""),-1)+1)="ZIP^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"PHONE (HOME)") S PHONE($O(PHONE(""),-1)+1)="H^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"PHONE (CELL)") S PHONE($O(PHONE(""),-1)+1)="MC^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"PHONE (WORK)") S PHONE($O(PHONE(""),-1)+1)="WP^"_$$FAMR(@CT)
 . I $$FAMP(@CT,"DIAGNOSIS") S PROB($O(PROB(""),-1)+1)=$$FAMR(@CT)
 D:$D(REL) FAMS
 K @LOC
 Q
 ;
FAMS ; SUBMIT TO FFSAVE
 N IEN I $G(NAME)="",$G(REL)'="" D
 . S IEN=$O(^DSIO(19641.11,"D",DFN,REL,"")) S:IEN<1 NAME="<LAST>,<FIRST>"
 D SAVE^DSIO9(.RET,$G(IEN),DFN,$G(NAME),$G(DOB),.ADDR,.PHONE,$G(EDU),SEX,$G(REL),$G(STAT),.PROB)
 K NAME,DOB,ADDR,PHONE,EDU,SEX,REL,STAT,PROB
 Q
 ;
FAMP(ND,NAME) ; DOES THE NAME VALUE EXIST ON THIS LINE
 Q $P($$UP^XLFSTR($$TRIM^XLFSTR(ND)),":")=NAME
 ;
FAMR(ND) ; RETRUN THE VALUE
 Q $$UP^XLFSTR($$TRIM^XLFSTR($P(ND,":",2)))
 ;
CR(REL) ; DETERMINE GENDER BY RELATIONSHIP
 I REL="OTHER" Q ""
 I "FATHER,BROTHER,SON,UNCLE,NEPHEW"[REL Q "M"
 I "MOTHER,SISTER,DAUGHTER,AUNT,NIECE"[REL Q "F"
 Q ""
 ;
PHIST ; PREGNANCY HISTORY PUSH
 ;
 ; PREGNANCY:
 ; PREG(RET,IEN,DFN,DATES,TYP,FOF,OBP,FAP,BAB,POST,COM,AB)
 ;
 ; FACILITY:
 ; SENT(RET,IEN,NAME,TYP,ACT,PCON,ADDR,PHONE,AB)
 ;
 ; OBSERVATION:
 ;  OBS(RET,IEN,DFN,DATES,OBJECT,REF,REL,CAT,CODE,VAL,VALCD,NEG,QUAL,NAR,AB)
 ;
 N LOC,CT,PHGES,PHLOL,PHTOD,TMP,PHEPS,PHPOD,PHFAC,PHPTL,PHCOM,PHPOST,PRT
 N BSTR,P,ORT,BAY,BABY,OBJECT,IPT,PHBAB,PHNAR,FCT,ORT
 S LOC=$NA(^TMP($J,"DSIO PHIST")) K @LOC
 D GETS^DIQ(19641.45,$G(ELE)_",",1,"E",LOC) K @LOC@(19641.45,+$G(ELE)_",",1,"E")
 S CT=$NA(@LOC) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO PHIST")  D
 . S PHGES=$P(@CT,U,18)                                      ; GESTATIONAL AGE AT DELIVERY
 . S PHLOL=$P(@CT,U,19)                                      ; LENGTH OF LABOR
 . S PHTOD=$P(@CT,U,20)                                      ; TYPE OF DELIVERY
 . S TMP=$$UP^XLFSTR($E($P(@CT,U,21),1))
 . S PHEPS=$S(TMP="Y":1,TMP="N":0,1:"")                      ; EPIDERAL/SPINAL
 . S PHPOD=$P(@CT,U,22)                                      ; PLACE OF DELIVERY
 . I PHPOD'="" D SENT^DSIO1(.PHFAC,,PHPOD,"F",,,,,1) S PHFAC=$S(PHFAC:PHFAC="NVA."_+PHFAC,1:"")
 . S TMP=$$UP^XLFSTR($E($P(@CT,U,23),1))
 . S PHPTL=$S(TMP="Y":1,TMP="N":0,1:"")                      ; PRETERM LABOR
 . S PHCOM=$P(@CT,U,25)                                      ; COMMENTS/COMPLICATIONS
 . S PHPOST=PHGES_U_PHLOL_U_PHTOD_U_PHEPS_U_PHPTL
 . S BSTR=$P(@CT,U,17) F P=1:1:$L(BSTR,"|") D
 . . S BAY=$P(BSTR,"|",P) S BABY($O(BABY(""),-1)+1)=BAY      ; SAVE FOR LATER
 . . S PHBAB=$S($D(PHBAB):PHBAB_",",1:"")_+BAY               ; BABY STRING FOR PREGNANCY "1,2,3"
 . K PRT D PREG^DSIO15(.PRT,,DFN,U_$P(@CT,U,10)_U,,,,$G(PHFAC),$G(PHBAB),PHPOST,PHCOM,1) Q:+PRT<1
 . S OBJECT($O(OBJECT(""),-1)+1)="PG."_PRT
 . S P=0 F  S P=$O(BABY(P)) Q:'P  D
 . . S BIEN=$O(^DSIO(19641.112,"C",DFN,PRT,+BABY(P),"")) Q:'BIEN
 . . S OBJECT($O(OBJECT(""),-1)+1)="FB."_BIEN
 . . S IPT(19641.112,BIEN_",",.05)=$P(BABY(P),";",3)
 . . S IPT(19641.112,BIEN_",",.06)=$P(BABY(P),";",4)
 . . S IPT(19641.112,BIEN_",",.07)=$P(BABY(P),";",5)
 . . D UPDATE^DIE("E","IPT") K IPT S FCT=$G(FCT)+1
 . S PHNAR(0)="Fetus/Baby Count"
 . D:$G(FCT) OBS^DSIO10(.ORT,,DFN,$$NOW^XLFDT,.OBJECT,,,"PREGNANCY","OTHER^^FetusBabyCount","^^"_FCT,,,,.PHNAR,1)
 K @LOC
 Q
 ;
DELV(TYP) ; PUSH DELIVERY INFORMATION FOR CURRENT PREGNANCY
 N PREG,OBJECT,VALUE,RT,IPT,CT,PRT,BIEN,FCT,NAR,ORT
 S PREG=$$PG^DSIO4(DFN) Q:'PREG
 S OBJECT(0)="PG."_PREG,VALUE=@VAL@($O(@VAL@("")))
 I $G(TYP)="DLY" D  Q
 . D PREG^DSIO15(.RT,PREG,DFN,U_VALUE_U,,,,,,,,1)
 I $G(TYP)="AGE" D  Q
 . S IPT(19641.13,PREG_",",3.1)=VALUE D FILE^DIE(,"IPT")
 I $G(TYP)="BAY" D  Q
 . ; ^^NUMBER^GENDER(M,F,U)^WEIGHT(LB|OZ|G)^^DEMISE^APGAR^NICU^COMMENTS
 . S CT=$NA(@VAL) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,2)'="SETDATA VALUE"  D
 . . K PRT D PREG^DSIO15(.PRT,PREG,DFN,,,,,,$P(@CT,U,3),,,1) Q:+PRT<1
 . . S BIEN=+$P(PRT,U,2) Q:'BIEN
 . . S OBJECT($O(OBJECT(""),-1)+1)="FB."_BIEN
 . . S IPT(19641.112,BIEN_",",.05)=$P(@CT,U,4)               ; GENDER
 . . S IPT(19641.112,BIEN_",",.06)=$$WEIGHT($P(@CT,U,5))     ; BIRTH WEIGHT
 . . D UPDATE^DIE("E","IPT") K IPT S FCT=$G(FCT)+1
 . S NAR(0)="Fetus/Baby Count"
 . D:$G(FCT) OBS^DSIO10(.ORT,,DFN,$$NOW^XLFDT,.OBJECT,,,"PREGNANCY","OTHER^^FetusBabyCount","^^"_FCT,,,,.NAR,1)
 Q
 ;
WEIGHT(STR) ; Rebuild Weight String
 N FAT
 I $P(STR,"|") S FAT=$P(STR,"|")_" lbs"
 I $P(STR,"|",2) S FAT=$S($D(FAT):FAT_" ",1:"")_$P(STR,"|",2)_" ozs"
 I $P(STR,"|",3) S FAT=$G(FAT)_" ("_$P(STR,"|",3)_" g)"
 Q $G(FAT)
 ;
CHIEF ; CHIEF COMPLAINT REASON FOR VISIT
 N VALUE,OBJECT,NAR
 S VALUE=$$TRIM^XLFSTR($P(@VAL@($O(@VAL@(""))),"-",2,99))
 S OBJECT(0)="PG."_$$PG^DSIO4(DFN)
 S NAR(0)="Chief Complaint"
 D OBS^DSIO10(.RET,,DFN,$$NOW^XLFDT,.OBJECT,,,"Chief Complaint","LNC^^10154-3","^^"_VALUE,,,,.NAR,1)
 Q
 ;
 ; ------------------------------ UTILITIES -----------------------------------
 ;
VAL(CTR) ; GET VALUE BY CONTROL NAME
 Q $$VAL^DSIO02(CTR)
 ;
CIEN(CTR) ; GET CONTROL IEN
 Q $$CIEN^DSIO02(CTR)
 ;
VALWP(CTR,FLE,FLD,XIEN) ; TRANSFER A WORD PROCESSING FIELD
 D VALWP^DSIO02(CTR,FLE,FLD,XIEN)
 Q
