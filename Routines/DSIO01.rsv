Routine DSIO01 saved using VFDXTRS routine on Oct 16, 2015 14:49
DSIO01^INT^63840,58892^Oct 15, 2015@16:21
DSIO01 ;DSS/TFF - DSIO OCNT PUSH SUPPORT;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**1,2**;Feb 06, 2015;Build 18
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
 ; ----------------------------------------------------------------------------
 ; * CONTROL(DDCSC), RECORD(DDCSR), 
 ;   SIEN(DESTINATION IEN), DESTINATION FILE(DDCSFLE),
 ;   DFN, INTERFACE/FORM(DDCSFRM), ELEMENT IEN(DDCSE), ELEMENT NAME(DDCSEN)
 ; ----------------------------------------------------------------------------
 ;
DELV ; DSIO DDCS REPORT ITEM - NURSE POSTPARTUM - DELIVERY
 N PREG,IEN,OUT,BCT,CT,LN,PG,BY,FDA,BW,OBJ,DES,OB,NAM
 S PREG=$$PG^DSIO4($G(DFN)) Q:'PREG
 S IEN=$$IEN^DSIO62("LSTDELIVERY") Q:'IEN  D GETS^DSIO62(.OUT,IEN)
 ; *** PREGNANCY
 ; Update Pregnancy with Babies, and the Babies with their data
 S (BCT,CT)=0 F  S CT=$O(OUT(CT)) Q:'CT  S LN=OUT(CT) D
 . I $P(LN,U,2)="L" S BCT=BCT+1 D
 . . I '$P(LN,U,3) K PG D PREG^DSIO15(.PG,PREG,DFN,,,,,,"+",,,,1) Q:'$P(PG,U,2)  S $P(LN,U,3)=+$P(PG,U,2)
 . . S BY=$P(LN,U,3)
 . . S FDA(19641.112,BY_",",.05)=$P(LN,U,6)         ; GENDER
 . . S BW=$P(LN,U,7)                                ; BIRTH WEIGHT
 . . S $P(BW,"|")=$P(BW,"|")_"LBS "
 . . S $P(BW,"|",2)=$P(BW,"|",2)_"OZS "
 . . S $P(BW,"|",3)="("_$P(BW,"|",3)_"G)"
 . . S FDA(19641.112,BY_",",.06)=$TR(BW,"|")
 . . S FDA(19641.112,BY_",",.07)=+$P(LN,U,8)        ; STILLBORN
 . . S FDA(19641.112,BY_",",.08)=+$P(LN,U,9)        ; APGAR1
 . . S FDA(19641.112,BY_",",.09)=+$P(LN,U,10)       ; APGAR2
 . . S FDA(19641.112,BY_",",.011)=$P(LN,U,11)       ; STATUS
 . . S FDA(19641.112,BY_",",.012)=+$P(LN,U,12)      ; NICU
 . . D UPDATE^DIE(,"FDA") K FDA
 . . K ^TMP("DSIO01",$J)
 . . D WP^DIE(19641.112,BY_",",1,"K","^TMP(""DSIO01"",$J)")
 . . K ^TMP("DSIO01",$J)
 . I $P(LN,U,2)="C",$G(BY) D                        ; COMMENTS
 . . K ^TMP("DSIO01",$J) S ^TMP("DSIO01",$J,1)=$P(LN,U,4,99)
 . . D WP^DIE(19641.112,BY_",",1,"A","^TMP(""DSIO01"",$J)")
 . . K ^TMP("DSIO01",$J)
 ; *** OBSERVATION
 ; Create an observation of number of babies for this pregnancy
 I BCT D
 . S DES(0)="Fetus/Baby Count",OBJ(0)="PG."_PREG
 . D OBS^DSIO10(.OB,,DFN,$$NOW^XLFDT,.OBJ,,,"PREGNANCY","OTHER^^FetusBabyCount","^^"_BCT,,,,.DES,1)
 ; *** PREGNANCY
 ; Update Pregnancy with Delivery Date, and Delivery and Complication Notes
 S CT=0 F NAM="MEMODELIVERYNOTES","MEMODELIVERYMETHODCOMPLICATIONS" D DC(NAM,CT) S CT=1
 K PG D PREG^DSIO15(.PG,PREG,DFN,U_$$DT^DSIO2($P($$GET1^DSIO62($$IEN^DSIO62("DTDELIVERY")),U,2))_U,,,,,,,,,1)
 Q
 ;
DC(NAM,I) ; Continue
 N OUT,CT
 K ^TMP("DSIO01",$J)
 D GETS^DSIO62(.OUT,$$IEN^DSIO62(NAM)) Q:'$D(OUT)
 S ^TMP("DSIO01",$J,1)=" -- "_$S('I:"Delivery Notes",1:"Complications/Anomalies")_" "_$$FMTE^XLFDT($$NOW^XLFDT,"5Z")
 S CT=0 F  S CT=$O(OUT(CT)) Q:'CT  D
 . S ^TMP("DSIO01",$J,(CT+1))=$P(OUT(CT),U,2,99)
 D WP^DIE(19641.13,PREG_",",4,"A","^TMP(""DSIO01"",$J)")
 K ^TMP("DSIO01",$J)
 Q
 ;
 ; ----------------------------------------------------------------------------
 ;
HIST ; DSIO DDCS REPORT ITEM - PREGNANCY HISTORY
 ;
 ;  2 : ID
 ;  3 : IEN
 ;  4 : DATE RECORDED
 ;  5 : EDC
 ;  6 : DFN|PATIENT
 ;  7 : STATUS
 ;  8 : FOF|(IEN OR IDENTIFIER)
 ;  9 : EDD
 ; 10 : END
 ; 11 : OB IEN|OB
 ; 12 : FACILITY IEN|FACILITY
 ; 13 : UPDATED BY IEN|NAME
 ; 14 : GESTATIONAL AGE AT DELIVERY
 ; 15 : LENGTH OF DELIVERY
 ; 16 : TYPE OF DELIVERY
 ; 17 : EPIDERAL/SPINAL
 ; 18 : PRETERM LABOR
 ; 19 : BIRTH TYPE
 ; 20 : IEN;BABY#;SEX;WEIGHT;STILLBORN|
 ; 21 : COMPLICATION
 ;
 N IEN,OUT,CT,LN,PG,I,DATES,FA,FAC,STR
 S IEN=$$IEN^DSIO62("PREGLISTVIEW") Q:'IEN  D GETS^DSIO62(.OUT,IEN) Q:'$D(OUT)
 ; *** PREGNANCY
 ; Update Pregnancies and Babies
 S CT=1 F  S CT=$O(OUT(CT)) Q:'CT  S LN=OUT(CT) D
 . K PG,DATES,FA,FAC,STR
 . I $P(LN,U,2)="L" D
 . . I $P(LN,U,3)["-" D DELETE^DSIO15(+$P(LN,U,3)) Q
 . . I $P(LN,U,3)["+" D PREG^DSIO15(.PG,,DFN,,"H",,,,,,,,1) Q:'PG  D UIEN(+PG)
 . . F I=5,10,9 S DATES=$S($D(DATES):DATES_U,1:"")_$$DT^DSIO2($P(LN,U,I))
 . . I $P($P(LN,U,12),"|",2)'="" D  K:$G(FAC)'?1"NVA.".N FAC
 . . . S FAC=$$UP^XLFSTR($P($P(LN,U,12),"|",2))
 . . . I $D(^DSIO(19641.1,"F",FAC)) D  Q
 . . . . S FAC="NVA."_$O(^DSIO(19641.1,"F",FAC,""))
 . . . D SENT^DSIO1(.FA,,FAC,"F",,,,,1) I FA S FAC="NVA."_+FA
 . . K PG D PREG^DSIO15(.PG,$P(LN,U,3),DFN,DATES,"H",,,$G(FAC),,$P(LN,U,14,18)_U_$P(LN,U,21),,,1)
 . . ; Add/Update Babies
 . . F I=1:1:$L($P(LN,U,20),"|") S STR=$P($P(LN,U,20),"|",I) D BABY($P(LN,U,3),STR)
 . . K ^TMP("DSIO01",$J)
 . . D WP^DIE(19641.13,$P(LN,U,3)_",",4,"K","^TMP(""DSIO01"",$J)")
 . . K ^TMP("DSIO01",$J)
 . I $P(LN,U,2)="C" D                              ; COMMENTS
 . . K ^TMP("DSIO01",$J) S ^TMP("DSIO01",$J,1)=$P(LN,U,4,99)
 . . D WP^DIE(19641.13,$P(LN,U,3)_",",4,"A","^TMP(""DSIO01"",$J)")
 . . K ^TMP("DSIO01",$J)
 Q
 ;
UIEN(IEN) ; Update IEN
 N OLD,CT
 S OLD=$P(LN,U,3),$P(LN,U,3)=IEN
 S CT=1 F  S CT=$O(OUT(CT)) Q:'CT  D
 . I $P(OUT(CT),U,3)=OLD S $P(OUT(CT),U,3)=IEN
 Q
 ;
BABY(PREG,LN) ; Update Baby
 N PG,FDA
 I $P(LN,";")["+" D PREG^DSIO15(.PG,PREG,DFN,,,,,,"+",,,,1) Q:'$P(PG,U,2)  S $P(LN,";")=+$P(PG,U,2)
 Q:'LN
 S FDA(19641.112,+LN_",",.05)=$P(LN,";",3)        ; GENDER
 S FDA(19641.112,+LN_",",.06)=$P(LN,";",4)        ; BIRTH WEIGHT
 S FDA(19641.112,+LN_",",.07)=+$P(LN,";",5)       ; STILLBORN
 D UPDATE^DIE(,"FDA")
 Q
 ;
 ; ----------------------------------------------------------------------------
 ;
FAM ; DSIO DDCS REPORT ITEM - FAMILY HISTORY
 ;
 ;  2 : IEN
 ;  3 : NAME
 ;  4 : SEX
 ;  5 : DATE OF BIRTH
 ;  6 : YEARS OF EDUCATION
 ;  7 : STREET ADDRESS 1
 ;  8 : STREET ADDRESS 2
 ;  9 : STREET ADDRESS 3
 ; 10 : CITY
 ; 11 : STATE
 ; 12 : ZIP
 ; 13 : PHONE HOME
 ; 14 : PHONE WORK
 ; 15 : PHONE CELL
 ; 16 : PHONE FAX
 ; 17 : PATIENT NAME
 ; 18 : DFN
 ; 19 : RELATIONSHIP
 ; 20 : STATUS
 ; 21 : DIAGNOSIS|
 ; 22 : COMMENTS
 ;
 N IEN,OUT,CT,LN,PR,DATES,I,ID,ADDR,PHONE,PROB,FDA
 S IEN=$$IEN^DSIO62("LVPERSONLIST") Q:'IEN  D GETS^DSIO62(.OUT,IEN) Q:'$D(OUT)
 S CT=1 F  S CT=$O(OUT(CT)) Q:'CT  S LN=OUT(CT) D
 . K PR,DATES,ADDR,PHONE,PROB
 . I $P(LN,U,2)["-"&($P(LN,U,2)'["+") D SAVE^DSIO9(.PR,+$P(LN,U,2),,"@") Q
 . I $P(LN,U,2)["+" D SAVE^DSIO9(.PR,,DFN,$P(LN,U,3),,,,,,,,,1) Q:'PR  S $P(LN,U,2)=+PR
 . S DATES=$$DT^DSIO2($P(LN,U,5))
 . ; *** ADDRESS
 . S I=7 F ID=1,2,3,"CITY","STATE","ZIP" S ADDR(I)=ID_U_$P(LN,U,I),I=I+1
 . ; *** PHONE NUMBERS
 . S I=13 F ID="H","WP","MC" S PHONE(I)=ID_U_$P(LN,U,I),I=I+1
 . ; *** DIAGNOSIS
 . F I=1:1:$L($P(LN,U,21),"|") S PROB(I)=$P($P(LN,U,21),"|",I)
 . K PR D SAVE^DSIO9(.PR,+$P(LN,U,2),DFN,$P(LN,U,3),DATES,.ADDR,.PHONE,$P(LN,U,6),$$G($P(LN,U,19)),$P(LN,U,19),$P(LN,U,20),.PROB,1)
 . S FDA(19641.11,+$P(LN,U,2)_",",4)=$P(LN,U,22)
 . D UPDATE^DIE(,"FDA") K FDA
 Q
 ;
G(REL) ; Determine Gender by Relationship
 S REL=$$UP^XLFSTR($G(REL))
 I "^FATHER^BROTHER^SON^UNCLE^NEPHEW^"[(U_REL_U) Q "M"
 I "^MOTHER^SISTER^DAUGHTER^AUNT^NIECE^"[(U_REL_U) Q "F"
 Q ""
