Routine DSIO01 saved using VFDXTRS routine on Aug 03, 2016 11:34
DSIO01^INT^64127,45285^Jul 28, 2016@12:34
DSIO01 ;DSS/TFF - DSIO OCNT PUSH SUPPORT;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**1,2,5**;Feb 06, 2015;Build 1
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
 ; ----------------------------------------------------------------------------
 ; * CONTROL(DDCSC), RECORD(DDCSR), 
 ;   SIEN(DESTINATION IEN), DESTINATION FILE(DDCSFLE),
 ;   DFN, INTERFACE/FORM(DDCSFRM), ELEMENT IEN(DDCSE), ELEMENT NAME(DDCSEN)
 ; ----------------------------------------------------------------------------
 ;
DELV ; NURSE POSTPARTUM - DELIVERY
 N PREG,OBJ,IEN,OUT,BCT,CT,LN,PG,BY,FDA,STATUS,BC,IFC,IFCF,FAC,FA,STR
 S PREG=$$PG^DSIO4($G(DFN)) Q:'PREG  S OBJ(0)="PG."_PREG
 D GETS^DSIO62(.OUT,$$IEN^DSIO62("LSTDELIVERY")) Q:'$D(OUT)
 ; *** PREGNANCY
 ; Update Pregnancy with Babies, and the Babies with their data
 S (BCT,CT)=0 F  S CT=$O(OUT(CT)) Q:'CT  S LN=OUT(CT) D
 . I $P(LN,U,2)="L" S BCT=BCT+1 D
 . . I '$P(LN,U,3) K PG D PREG^DSIO15(.PG,PREG,DFN,,,,,,"+",,,,1) Q:'$P(PG,U,2)  S $P(LN,U,3)=+$P(PG,U,2)
 . . S BY=$P(LN,U,3),OBJ(1)="FB."_BY
 . . ; *** OBSERVATIONS ---------------------------------------------------------------------------
 . . ;     BABY LEVEL
 . . D O^DSIO03(DFN,.OBJ,,"BabyDetails","46098-0","LOINC","Sex",$$SEX($P(LN,U,6)))                      ; OBS GENDER
 . . D O^DSIO03(DFN,.OBJ,,"BabyDetails","9272-6","LOINC","One Minute APGAR",$P(LN,U,9))                 ; OBS APGAR1 (1 MINUTE)
 . . D O^DSIO03(DFN,.OBJ,,"BabyDetails","9274-2","LOINC","Five Minute APGAR",$P(LN,U,10))               ; OBS APGAR2 (5 MINUTE)
 . . D O^DSIO03(DFN,.OBJ,,"BabyDetails","AdmittedToIcu","OTHER","",$S($P(LN,U,12):"True",1:"False"))    ; OBS NICU
 . . D O^DSIO03(DFN,.OBJ,,"BabyDetails","8339-4","LOINC","Birth Weight",$P($P(LN,U,7),"."))             ; OBS BIRTH WEIGHT IN GRAMS
 . . D UPDATE^DIE(,"FDA") K FDA
 . I $P(LN,U,2)="C",$G(BY) S BC(BY,$O(BC(BY,""),-1)+1)=$P(LN,U,4,99)                                    ; COMPLICATIONS/ANOMALIES
 ; *** OBSERVATIONS -------------------------------------------------------------------------------
 ;     PREGNANCY LEVEL
 K OBJ(1)
 ;     OBS BABY COUNT
 D O^DSIO03(DFN,.OBJ,,"Pregnancy","FetusBabyCount","OTHER","Fetus/Baby Count",BCT)
 ;     OBS BIRTH DATE OF FETUS
 D O^DSIO03(DFN,.OBJ,,"Outcome","75092-7","LOINC","Birth Date of Fetus",$$FMTE^XLFDT($$EGET1^DSIO62("DTDELIVERY"),"5ZD"))
 ;     OBS GESTATIONAL AGE
 D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","412726003","SNOMED-CT","Gestational Age at Birth",$$G1^DSIO4($$EGET1^DSIO62("SPNGAWEEKS")_"W"_$$EGET1^DSIO62("SPNGADAYS")_"D"))
 ;     OBS DAYS IN HOSPITAL
 D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","DaysInHospital","OTHER",,$$EGET1^DSIO62("EDTDELIVERYAT"))
 ;     OBS CESAREAN INCISION
 S STR=$$EGET1^DSIO62("RGINCISION") I STR'="" D
 . D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","11466000","SNOMED-CT","Cesarean Delivery",STR)
 ;     OBS DELIVERY TYPE
 D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","CesareanDelivery","OTHER",,$S($$TGET1^DSIO62("CKDELIVERYMETHODC"):"True",1:"False"))
 D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","NormalSpontaneousVaginalDelivery","OTHER",,$S($$TGET1^DSIO62("CKDELIVERYMETHODV"):"True",1:"False"))
 S STR=$S('$$TGET1^DSIO62("CKDELIVERYMETHODC")&('$$TGET1^DSIO62("CKDELIVERYMETHODV")):"True",1:"False")
 D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","OtherDelivery","OTHER",,STR)
 ;     OBS OBSTETRIC DELIVERY METHOD
 D D1($S($$TGET1^DSIO62("CKDELIVERYMETHODC"):"Cesarean Delivery",$$TGET1^DSIO62("CKDELIVERYMETHODV"):"Normal Spontaneous Vaginal Delivery (NSVD)",1:"Other"))
 ;     OBS PREGNANCY OUTCOME
 S STR=$$OT^DSIO03($$EGET1^DSIO62("CBOUTCOME"))
 I STR="PregnancyTermination" D
 . D O^DSIO03(DFN,.OBJ,,"PregnancyTerminationOutcome","Indication","OTHER",,"Unknown")
 D O^DSIO03(DFN,.OBJ,,"Outcome","OutcomeType","OTHER","Pregnancy Outcome",STR)
 ;     OBS FailedForcepVacuumDelivery
 F IFC="EDCPRIMARYFOR","CBREASONSCPRIMARY","EDREASONSCOTHPRIMARY","CBREASONSCSECONDARY","EDREASONSCOTHSECONDARY" D  Q:$D(IFCF)
 . I $TR($$UP^XLFSTR($$EGET1^DSIO62(IFC))," ")["FAILEDFORCEP" S IFCF=1
 I $D(IFCF),$$TGET1^DSIO62("CKVAGVACUUM") D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","FailedForcepVacuumDelivery","OTHER",,"True")
 ;     OBS PREGANCY NOTES
 K OUT D TXT^DSIO62(.OUT,$$IEN^DSIO62("MEDELIVERYNOTES")) D:$D(OUT)
 . D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","Notes","OTHER",,,,.OUT)
 ;     OBS BABY NOTES (NEWBORN DELIVERY)
 I $D(BC) S BY=0 F  S BY=$O(BC(BY)) Q:'BY  D
 . K OUT S CT=0 F  S CT=$O(BC(BY,CT)) Q:'CT  S OUT(CT+1)=BC(BY,CT)
 . S OBJ(1)="FB."_BY
 . D O^DSIO03(DFN,.OBJ,,"BabyDetails","57075-4","LOINC",,,,.OUT)
 ; *** FACILITY
 S FAC=$$UP^XLFSTR($$EGET1^DSIO62("CBDELIVERYPLACE")) I FAC'="" D  K:$G(FAC)'?1"NVA.".N FAC
 . I $D(^DSIO(19641.1,"F",FAC)) S FAC="NVA."_$O(^DSIO(19641.1,"F",FAC,"")) Q
 . D SENT^DSIO1(.FA,,FAC,"F",,,,,1) I FA S FAC="NVA."_+FA
 ; *** PREGNANCY - Close it out
 ; Update Pregnancy with DELIVERY DATE, LOCATION, ANESTHESIA, LENGTH OF LABOR
 K PG D PREG^DSIO15(.PG,PREG,DFN,U_$$DT^DSIO2($$EGET1^DSIO62("DTDELIVERY"))_U,,,,$G(FAC),,$$D2,,,1)
 ;     OBS TOTAL PREGNANCIES, STILLBIRTHS, PRETERM, LIVING, TERM BIRTHS
 D CALC
 Q
 ;
D1(DF) ; Obstetric Delivery Method
 N IT,V,LS
 F IT="CKVAGSVD","CKVAGVACUUM","CKVAGFORCEPS","CKVAGEPISIOTOMY","CKVAGLACERATIONS","CKVAGVBAC" D
 . Q:'$$TGET1^DSIO62(IT)
 . S V=$$EGET1^DSIO62(IT) Q:$$UP^XLFSTR(V)["VAGINAL DELIVERY"&(DF["Vaginal Delivery")
 . S LS=$S($D(LS):LS_","_V,1:V)
 D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","57071-3","LOINC","Obstetric Delivery Method",$S($D(LS):LS_","_DF,1:DF))
 I $$TGET1^DSIO62("CKVAGVACUUM"),$$TGET1^DSIO62("CKVAGFORCEPS") D
 . D O^DSIO03(DFN,.OBJ,,"DeliveryDetails","ForcepVacuumDelivery","OTHER",,"True")
 Q
 ;
D2() ; Get POST = ADDITIONAL DETAILS (CARET DELIMITED STRING)
 Q U_$$EGET1^DSIO62("SPNLABORLENGTH")_"^^"_$$EGET1^DSIO62("CBANESTHESIA")
 ;
 ; COMMON ---------------------------------------------------------------------
 ;
SEX(IN) ; Translate Sex Code
 S IN=$E($$UP^XLFSTR(IN),1)
 S IN=$S(IN="M":"Male",IN="F":"Female",IN="U":"Unknown",1:"")
 Q IN
 ;
CALC ; Calculated information
 ;     OBS TOTAL PREGNANCIES (CALCULATE)
 D O^DSIO03(DFN,,,"Pregnancy History","11996-6","LOINC","Total Pregnancies (Including Current)",$$TOTAL^DSIO4(DFN),,,,-1)
 ;     OBS STILLBIRTHS       (CALCULATE)
 D O^DSIO03(DFN,,,"Pregnancy History","57062-2","LOINC","Stillbirths",$$STILL^DSIO4(DFN),,,,-1)
 ;     OBS PRETERM           (CALCULATE)
 D O^DSIO03(DFN,,,"Pregnancy History","11637-6","LOINC","Preterm Births (Live & Stillborn)",$$PRETM^DSIO4(DFN),,,,-1)
 ;     OBS LIVING            (CALCULATE)
 D O^DSIO03(DFN,,,"Pregnancy History","11638-4","LOINC","Living Children",$$LIVIG^DSIO4(DFN),,,,-1)
 ;     OBS TERM BIRTHS       (CALCULATE)
 D O^DSIO03(DFN,,,"Pregnancy History","11639-2","LOINC","Term Births (Live & Stillborn)",$$FULLT^DSIO4(DFN),,,,-1)
 ;     OBS GRAVIDA & PARA SUMMARY
 D O^DSIO03(DFN,,,"Pregnancy History","GravidaParaSummary","OTHER","Gravida & Para Summary",$$GP^DSIO4(DFN),,,,-1)
 Q
