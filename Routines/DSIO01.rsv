Routine DSIO01 saved using VFDXTRS routine on May 20, 2015 09:21
DSIO01^INT^63692,31237^May 20, 2015@08:40
DSIO01 ;DSS/TFF - DSIO OCNT PUSH SUPPORT;06/28/2013 15:19
 ;;1.0;DSIO 1.0;;Feb 06, 2015;Build 23
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ; FASTVIT^ORQQVI
 ;
 ;
 ;
 ; PUSH DEFINED VARIABLES
 ; ----------------------
 ;   DFN = PATIENT IEN
 ;   ELE = DSIO NOTE ELEMENT IEN
 ;  DIEN = DSIO OCNT DIALOG IEN
 ; FNAME = INTERFACE NAME
 ;  FIEN = DSIO OCNT CONFIGURATION IEN
 ;  CIEN = DSIO OCNT CONFIGURATION CONTROL MULTIPLE IEN
 ;  PUSH = IEN FOR EITHER DSIO REPORT ITEMS OR DSIO REMINDER ELEMENTS
 ;  RIEN = REMINDER DIALOG IEN (PARENT)
 ; RIENS = REMINDER DIALOG IEN
 ;
 Q
 ;
FLOW ; PARSE FLOWSHEET AND PUSH VALUES
 ;
 ; SHARED DIALOG GLOBAL: ^DSIO(19641.4941,DFN,2,DIEN,
 ;
 N LOC,CT,COLS,RATE,DATE,RDT,RET,FCT
 S LOC=$NA(^TMP($J,"DSIO FLOW")) K @LOC
 D GETS^DIQ(19641.45,$G(ELE)_",",1,"E",LOC) K @LOC@(19641.45,+$G(ELE)_",",1,"E")
 S CT=$NA(@LOC) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO FLOW")  D
 .; SET THE DATE FOR THE ROW
 .I +@CT=0,$P($P(@CT,U),",",2)'=0 S DATE($P($P(@CT,U),",",2))=$P(@CT,U,2)
 .I $P($P(@CT,U),",",2)=0,$P(@CT,U,2)["Rate" D
 ..; GET THE COLUMN AND THE RATE COUNT
 ..S COLS=$G(COLS)_+@CT_"|"_$P($P(@CT,U,2)," ",2)_U
 .Q:$P($P(@CT,U),",",2)=0
 .; IF THIS COLUMN IS UNDER THE RATE HEADER THEN THE SECOND PIECE IS RATE VAL
 .S RATE=$$COL(+@CT) I RATE S FCT=$G(FCT)+1 D
 ..S RDT=DATE(+$P($P(@CT,U),",",2))
 ..Q:$$OBS(RDT,249043002)  ; HAS THIS BEEN OBSERVED ALREADY?
 ..; SCT 249043002 = Fetal heart rate
 ..D OBS^DSIO10(.RET,DFN,,$$PG^DSIO4(DFN),RATE,RDT,,"SCT",249043002,"Fetal heart rate",$P(@CT,U,2))
 I $G(FCT) D
 .D OBS^DSIO10(.RET,DFN,$$FC^DSIO4(DFN,$$PG^DSIO4(DFN)),$$PG^DSIO4(DFN),,$$NOW^XLFDT,"PREGNANCY","OTHER","FetusBabyCount","Fetus/Baby Count",FCT)
 K @LOC
 Q
 ;
COL(CK) ; ARE WE LOOKING FOR THIS COLUMN?
 Q:$G(COLS)="" 0
 N P,FLG S FLG=0 F P=1:1:$L(COLS,U) I +$P(COLS,U,P)=CK S FLG=$P($P(COLS,U,P),"|",2) Q
 Q FLG
 ;
OBS(DATE,CODE) ; HAS THIS OBSERVATION BEEN CREATED ALREADY?
 ; OBG(RET,DFN,IEN,PIEN,BIEN,FDT,TDT,CAT,SORT)
 N RET,CT,FLG Q:DATE="" 1
 K RET D OBG^DSIO10(.RET,DFN,,$$PG^DSIO4(DFN),RATE)
 S CT=$NA(RET) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO OBG")  D  Q:$D(FLG)
 .I $P(@CT,U,2)=DATE,$P(@CT,U,10)=CODE S FLG=1
 Q +$G(FLG)
 ;
EDD(TYP,OVR) ; CREATE EDD OBSERVATIONS
 ;
 ; Piece 1 is type date
 ; Piece 2 is "IsFinal"
 ; Piece 3 is Gestational Age in days
 ; Piece 4 is EDD
 ;
 ; OVR = IF SET THEN UPDATE EVEN IF FALSE (USED FOR DUAL PURPOSED LMP UPDATE)
 ;
 ; OBS(RET,DFN,IEN,PIEN,BIEN,DATE,CAT,SYS,CODE,DESC,VAL)
 ; OBS(.RET,DFN,,,,$$NOW^XLFDT,"EDD")
 ;
 ; RUN ROUTINE WILL BE ON THE EDD COLUMN ELEMENTS AND THEY WILL CREATE
 ; OBSERVATIONS AND NOT PUSH - ONLY AN OBSERVATION OF "ISFINAL" WILL
 ; PUSH AND "DSIO OBSERVATION PUSH" WILL HANDLE THAT
 ;
 ; DFN,@VAL(1),ELE(19641.45 IEN),OUT(LIST OF CONTROLS FOR THE NOTE)
 ;
 Q:'$D(@VAL)  Q:@VAL@($O(@VAL@("")))=""
 N RET,CAT,SYS,CODE,DES,DY,ISFIN,GESD,GESW,EDD,VALUE,LMPQ
 ; LMP
 I TYP="LMP" S SYS="LOINC",CODE="8665-2",DES="Last Menstrual Period"
 ; CONCEPTION
 I TYP="ECD" S SYS="LOINC",CODE="33067-0",DES="Estimated Conception Date"
 ; ULTRASOUND
 I TYP="ULTRA" S SYS="LOINC",CODE="11884-4",DES="Ultrasound"
 ; EMBRYO
 I TYP="EMBRYO" S DY=$$VAL("SPNTRANSFERDAY") Q:DY=""  D
 .S SYS="OTHER",CODE="EmbryoTransfer",DES="Embryo Transfer ("_$G(DY)_"-days)"
 ; OTHER
 I TYP="OTHER" S DES=$$VAL("LBLOTHER") Q:DES=""!(DES="Enter Other Criteria")  D
 .S SYS="OTHER",CODE="OtherEdd",DES=$$VAL("LBLOTHER")
 ; UNKNOWN
 I TYP="UNKNOWN" Q:$$VAL("CKFINALEDD"_TYP)'="TRUE"  D
 .S SYS="LOINC",CODE="11778-8",DES="Unknown",EDD=$$VAL("DTEDD"_TYP)
 ; VALUE
 S ISFIN=$$VAL("CKFINALEDD"_TYP)
 S GESD=$$VAL("EDTDAY"_TYP) I GESD="" S GESD=$$VAL("SPNDAY"_TYP)
 S GESW=$$VAL("EDTWEEK"_TYP) I GESW="" S GESW=$$VAL("SPNWEEK"_TYP)
 S GESD=GESD+(GESW*7)
 S:'$D(EDD) EDD=$$VAL("EDTEDD"_TYP)
 S VALUE=@VAL@($O(@VAL@("")))_"|"_$S(ISFIN'="":"TRUE",1:"FALSE")_"|"_$S(+GESD>0:GESD,1:"")
 S VALUE=VALUE_"|"_$S($G(EDD):EDD,1:"")
 I TYP="LMP" D
 .S LMPQ=$$VAL("LMPQ"),VALUE=VALUE_"|"_LMPQ
 .I '$P(VALUE,"|",3),'$P(VALUE,"|",4)
 .I $G(OVR) S $P(VALUE,"|",2)="TRUE"
 D OBS^DSIO10(.RET,DFN,,$$PG^DSIO4(DFN),,$$NOW^XLFDT,$S($D(LMPQ):"LMP",1:"EDD"),SYS,CODE,DES,VALUE)
 D REC^DSIO6(ELE,"ROU")
 I TYP="LMP",$D(LMPQ) D OBSLMP
 Q
 ;
EDDCAL(TYP) ; EDD OBSERVATION CALCULATOR ENTRY
 Q:'$P($G(X),"|")
 N TVAL,EDD,IPT,RT,LMPI
 S TVAL=$$DT^DSIO2($P(X,"|")) ; ECD,LMP,ULTRASOUND,EMBRYO,OTHER,UNKNOWN DATE VALUE
 I $$UP^XLFSTR($P(X,"|",2))="TRUE"!($$UP^XLFSTR($P(X,"|",2))="YES") D
 .S EDD=$P(X,"|",4) I EDD="" S EDD=$$CAL(TYP,TVAL,$P(X,"|",3))
 .D OBEFLE
 I TYP="LMP",$$UP^XLFSTR($P(X,"|",2))="TRUE" D
 .S IPT(19641.01,"?+1,",.01)=TVAL
 .S IPT(19641.01,"?+1,",.02)=DFN
 .S IPT(19641.01,"?+1,",.03)=DUZ
 .S IPT(19641.01,"?+1,",.04)=$$NOW^XLFDT
 .S IPT(19641.01,"?+1,",.05)=$P(X,"|",5)
 .D UPDATE^DIE(,"IPT","LMPI") K IPT I $G(LMPI(1)) D
 ..S IPT(19641,$G(DFN)_",",1.2)=LMPI(1) D UPDATE^DIE(,"IPT") K IPT
 I TYP="EDC" D PREG^DSIO10(.RT,DFN,$$PG^DSIO4(DFN),TVAL)
 Q
 ;
CAL(TYP,VAL,GES) ; EDD CALCULATOR
 N RET
 I TYP="LMP" S RET=$$FMADD^XLFDT(VAL,280)
 I TYP="ECD" S RET=$$FMADD^XLFDT(VAL,266)
 I TYP="ULT" S RET=$$FMADD^XLFDT(VAL,(280-GES))
 I TYP="EMB" S RET=$$FMADD^XLFDT(VAL,262)
 I TYP="OTH" S RET=$$FMADD^XLFDT(VAL,(280-GES))
 I TYP="UNK" S RET=VAL
 Q $G(RET)
 ;
OBEFLE ; FILE EDD OBSERVATION
 N IPT,EIEN,PG,RT Q:'$G(EDD)
 I +$G(PIEN)>0 S PG=PIEN,EIEN=$$GET1^DIQ(19641.13,PG_",",.06,"I")
 E  S PG=$$PG^DSIO4(DFN),EIEN=$$GET1^DIQ(19641.13,PG_",",.06,"I")
 S IPT(19641.03,EIEN_",",1.1)=$P($$GET1^DIQ(19641.123,IEN_",",.01),"EDD BY ",2)
 S IPT(19641.03,EIEN_",",1.2)="YES"    ; IS FINAL
 S IPT(19641.03,EIEN_",",2.2)="`"_OIEN ; OBSERVATION
 D FILE^DIE("E","IPT") K IPT
 D PREG^DSIO10(.RT,DFN,PG,,,,EDD)
 Q
 ;
OBSLMP ; OBSERVATION FOR RECORDING ADDITIONAL DATA ELEMENTS FOR LMP
 N IPT,LIEN,MYES,MNO,AYES,ANO,DYES,DNO,CYES,CNO,NVAL,CONIEN
 ;S IPT(19641,$G(DFN)_",",1.2)=X D UPDATE^DIE("E","IPT") K IPT
 S LIEN=$$GET1^DIQ(19641,DFN_",",1.2,"I") Q:LIEN=""
 S MYES=$$VAL("CKMENSESYES"),MNO=$$VAL("CKMENSESNO")
 S IPT(19641.01,LIEN_",",1.1)=$S($G(MYES)'="":MYES,$G(MNO)'="":MNO,1:"")
 S IPT(19641.01,LIEN_",",1.2)=$$VAL("EDTFREQUENCY")
 S AYES=$$VAL("CKAMOUNTYES"),ANO=$$VAL("CKAMOUNTNO")
 S IPT(19641.01,LIEN_",",1.3)=$S($G(AYES)'="":AYES,$G(ANO)'="":ANO,1:"")
 S DYES=$$VAL("CKDURATIONYES"),DNO=$$VAL("CKDURATIONNO")
 S IPT(19641.01,LIEN_",",1.4)=$S($G(DYES)'="":DYES,$G(DNO)'="":DNO,1:"")
 S CYES=$$VAL("CKCONTRACEPTIONYES"),CNO=$$VAL("CKCONTRACEPTIONNO")
 S IPT(19641.01,LIEN_",",1.5)=$S($G(CYES)'="":CYES,$G(CNO)'="":CNO,1:"")
 S IPT(19641.01,LIEN_",",1.6)=$$DT^DSIO2($$VAL("EDTHCG")) ; hCG+
 D FILE^DIE("E","IPT") K IPT
 D VALWP("MEMLMP",19641.01,2,LIEN) ; COMMENTS
 Q
 ;
OBLAC ; OBSERVATION PUSH LACTATION STATUS
 Q:$G(X)=""!('$G(DFN))
 N FLE,IPT,IENS
 ; Because no one knows the state I'm in...
 S FLE=790.17 I '$$VFILE^DILFD(790.17) S FLE=19641.04
 I X="YES" D
 .Q:$$LACT^DSIO4(DFN)="YES"
 .S IPT(FLE,"?+1,"_DFN_",",.01)=$$NOW^XLFDT
 .S:FLE=790.17 IPT(FLE,"?+1,"_DFN_",",2)=1
 .S IPT(FLE,"?+1,"_DFN_",",4)=DUZ
 I X="NO" D
 .Q:$$LACT^DSIO4(DFN)="NO"
 .S IENS=$$LACE^DSIO4(DFN)_","_DFN_","
 .I +IENS<1 S IENS="?+1,"_DFN_",",IPT(FLE,IENS,.01)=$$NOW^XLFDT
 .S:FLE=790.17 IPT(FLE,IENS,2)=0
 .S IPT(FLE,IENS,$S(FLE=790.17:3,1:.02))=$$NOW^XLFDT
 .S IPT(FLE,IENS,4)=DUZ
 D:$D(IPT) UPDATE^DIE(,"IPT")
 Q
 ;
FAM ; PROCESS FAMILY HISTORY FOR DSIO PERSON
 ;
 ; FFSAVE(RET,DFN,IEN,NAME,DOB,ADDR,PHONE,EDU,SEX,REL,STAT,PROB)
 ;
 ; VARIABLES DEFINED: DFN, DIEN = DIALOG IEN, CIEN = CONTROL IEN,
 ;                    ELE = DATA ELEMENT IEN
 N LOC,CT,NAME,DOB,ADDR,PHONE,EDU,SEX,REL,STAT,PROB,RET
 S LOC=$NA(^TMP($J,"DSIO FAM")) K @LOC
 D GETS^DIQ(19641.45,$G(ELE)_",",1,"E",LOC) K @LOC@(19641.45,+$G(ELE)_",",1,"E")
 S CT=$NA(@LOC) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO FAM")  D
 .I $$FAMP(@CT,"RELATIONSHIP") D
 ..I $D(REL) D FAMS
 ..S REL=$$FAMR(@CT),SEX=$$CR(REL)
 .I $$FAMP(@CT,"NAME") S NAME=$$FAMR(@CT)
 .I $$FAMP(@CT,"STATUS") S STAT=$$FAMR(@CT)
 .I $$FAMP(@CT,"DATE OF BIRTH") S DOB=$$DT^DSIO2($$FAMR(@CT))
 .I $$FAMP(@CT,"YEARS OF EDUCATION") S EDU=$$FAMR(@CT)
 .I $$FAMP(@CT,"SEX") S SEX=$$FAMR(@CT)
 .; BUILD ARRAYS ADDR,PHONE,PROB
 .I $$FAMP(@CT,"STREET LINE 1") S ADDR($O(ADDR(""),-1)+1)="1^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"STREET LINE 2") S ADDR($O(ADDR(""),-1)+1)="2^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"STREET LINE 3") S ADDR($O(ADDR(""),-1)+1)="3^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"CITY") S ADDR($O(ADDR(""),-1)+1)="CITY^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"STATE") S ADDR($O(ADDR(""),-1)+1)="STATE^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"ZIP") S ADDR($O(ADDR(""),-1)+1)="ZIP^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"PHONE (HOME)") S PHONE($O(PHONE(""),-1)+1)="H^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"PHONE (CELL)") S PHONE($O(PHONE(""),-1)+1)="MC^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"PHONE (WORK)") S PHONE($O(PHONE(""),-1)+1)="WP^"_$$FAMR(@CT)
 .I $$FAMP(@CT,"DIAGNOSIS") S PROB($O(PROB(""),-1)+1)=$$FAMR(@CT)
 D:$D(REL) FAMS
 Q
 ;
FAMS ; SUBMIT TO FFSAVE
 N IEN I $G(NAME)="",$G(REL)'="" D
 .S IEN=$O(^DSIO(19641.11,"D",DFN,REL,"")) S:IEN<1 NAME="<LAST>,<FIRST>"
 D FFSAVE^DSIO9(.RET,DFN,$G(IEN),$G(NAME),$G(DOB),.ADDR,.PHONE,$G(EDU),SEX,$G(REL),$G(STAT),.PROB)
 K NAME,DOB,ADDR,PHONE,EDU,SEX,REL,STAT,PROB
 Q
 ;
FAMP(ND,NAME) ; DOES THE NAME VALUE EXIST ON THIS LINE
 Q $P($$UP^XLFSTR($$TRIM^XLFSTR(ND)),":")=NAME
 ;
FAMR(ND) ; RETRUN THE VALUE
 Q $$UP^XLFSTR($$TRIM^XLFSTR($P(ND,":",2)))
 ;
CR(REL) ; DETERMINE GENDER BY RELATIONSHIP
 I REL="OTHER" Q ""
 I "FATHER,BROTHER,SON,UNCLE,NEPHEW"[REL Q "M"
 I "MOTHER,SISTER,DAUGHTER,AUNT,NIECE"[REL Q "F"
 Q ""
 ;
PHIST ; PREGNANCY HISTORY PUSH
 ;
 ; SHIFT ONE TO THE LEFT - INDEX VALUE USED FOR LISTVIEW
 ; IEN^STRT^PATIENT^DFN^TYP^FOF^FOF IEN^EDD^END^OBP^OBP IEN^PFAC^PFAC IEN^
 ; UPDATE BY^UPDATE BY IEN^BABY;IEN;GENDER;WEIGTH;STILLBORN|^
 ; GESTATIONAL AGE AT DELIVERY^LENGTH OF LABOR^TYPE OF DELIVERY^
 ; EPIDERAL/SPINAL^PLACE OF DELIVERY^PRETERM LABOR^BIRTH TYPE^COMMENTS
 ;
 ; PREG(RET,DFN,IEN,STRT,TYP,FOF,EDD,END,OBP,PFAC)
 ;
 ; OBS(RET,DFN,IEN,PIEN,BIEN,DATE,CAT,SYS,CODE,DESC,VAL)
 ;
 N LOC,CT,PRT,IPT,TMP,FCT,BSTR,P,ORT,BAY,BIEN
 S LOC=$NA(^TMP($J,"DSIO PHIST")) K @LOC
 D GETS^DIQ(19641.45,$G(ELE)_",",1,"E",LOC) K @LOC@(19641.45,+$G(ELE)_",",1,"E")
 S CT=$NA(@LOC) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO PHIST")  D
 .K PRT D PREG^DSIO10(.PRT,DFN,$P(@CT,U,2),,,,,$P(@CT,U,10)) Q:'PRT
 .S IPT(19641.13,PRT_",",3.1)=$P(@CT,U,18) ; GESTATIONAL AGE AT DELIVERY
 .S IPT(19641.13,PRT_",",3.2)=$P(@CT,U,19) ; LENGTH OF LABOR
 .S IPT(19641.13,PRT_",",3.3)=$P(@CT,U,20) ; TYPE OF DELIVERY
 .S TMP=$$UP^XLFSTR($E($P(@CT,U,21),1))
 .S IPT(19641.13,PRT_",",3.4)=$S(TMP="Y":1,TMP="N":0,1:"") ; EPIDERAL/SPINAL
 .S IPT(19641.13,PRT_",",3.5)=$P(@CT,U,22) ; PLACE OF DELIVERY
 .S TMP=$$UP^XLFSTR($E($P(@CT,U,23),1))
 .S IPT(19641.13,PRT_",",3.6)=$S(TMP="Y":1,TMP="N":0,1:"") ; PRETERM LABOR
 .S IPT(19641.13,PRT_",",4)=$P(@CT,U,25)   ; COMMENTS/COMPLICATIONS
 .D UPDATE^DIE(,"IPT") K IPT
 .K FCT S BSTR=$P(@CT,U,17) F P=1:1:$L(BSTR,"|") S FCT=$G(FCT)+1 D
 ..K ORT S BAY=$P(BSTR,"|",P)
 ..D OBS^DSIO10(.ORT,DFN,,PRT,+BAY,$$NOW^XLFDT) Q:'ORT
 ..; FIND BABY BY KEY: PATIENT,PREGNANCY,NUMBER
 ..S BIEN=$O(^DSIO(19641.112,"C",DFN,PRT,+BAY,""))_","
 ..S IPT(19641.112,BIEN,.05)=$P(BAY,";",3)
 ..S IPT(19641.112,BIEN,.06)=$P(BAY,";",4)
 ..S IPT(19641.112,BIEN,.07)=$P(BAY,";",5)
 ..D UPDATE^DIE("E","IPT") K IPT
 .I $G(FCT) D
 ..D OBS^DSIO10(.RET,DFN,$$FC^DSIO4(DFN,$$PG^DSIO4(DFN)),$$PG^DSIO4(DFN),,$$NOW^XLFDT,"PREGNANCY","OTHER","FetusBabyCount","Fetus/Baby Count",FCT)
 Q
 ;
DELV(TYP) ; PUSH DELIVERY INFORMATION FOR CURRENT PREGNANCY
 N PIEN,VALUE,RT,IPT,ERR,CT,ORT,BIEN
 Q:'$$PG^DSIO4(DFN)
 S PIEN=$$PG^DSIO4(DFN)
 S VALUE=@VAL@($O(@VAL@("")))
 I $G(TYP)="DLY" D  Q
 .D PREG^DSIO10(.RT,DFN,PIEN,,,,,VALUE)
 I $G(TYP)="AGE" D  Q
 .S IPT(19641.13,PIEN_",",3.1)=VALUE D FILE^DIE(,"IPT")
 I $G(TYP)="BAY" D  Q
 .;^IEN^NUMBER^GENDER(M,F,U)^WEIGHT(LB|OZ|G)^^DEMISE^APGAR^NICU^COMMENTS
 .S CT=$NA(@VAL) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,2)'="SETDATA VALUE"  D
 ..D OBS^DSIO10(.ORT,DFN,,PIEN,$P(@CT,U,3),$$NOW^XLFDT) Q:+ORT=-1
 ..S BIEN=$O(^DSIO(19641.112,"C",DFN,PIEN,$P(@CT,U,3),""))_","
 ..S IPT(19641.112,BIEN,.05)=$P(@CT,U,4)     ; GENDER
 ..S IPT(19641.112,BIEN,.06)=$P(@CT,U,5)     ; BIRTH WEIGHT
 ..D UPDATE^DIE("E","IPT") K IPT
 ..S FCT=$G(FCT)+1
 .;S FCT=$$VAL("RBSINGLETON") I '$G(FCT) S FCT=$$VAL("EDTNUMBABIES")
 .D:$G(FCT) OBS^DSIO10(.RET,DFN,$$FC^DSIO4(DFN,PIEN),PIEN,,$$NOW^XLFDT,"PREGNANCY","OTHER","FetusBabyCount","Fetus/Baby Count",FCT)
 Q
 ;
CHIEF ; CHIEF COMPLAINT REASON FOR VISIT
 N VALUE S VALUE=$$TRIM^XLFSTR($P(@VAL@($O(@VAL@(""))),"-",2,99))
 D OBS^DSIO10(.RET,DFN,,$$PG^DSIO4(DFN),,$$NOW^XLFDT,"Chief Complaint","LNC","10154-3","Chief Complaint",VALUE)
 Q
 ;
 ; ----------------------- RUN ROUTINE FOR CONFIGURATION ----------------------
 ;
VIT(VITAL) ; GET VITAL FOR CONTROL
 ; DFN, IEN = DIALOG IEN
 N OUT,CT S @RET@(0)=-1 Q:'$G(DFN)
 D FASTVIT^ORQQVI(.OUT,DFN)
 N CT S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$G(@RET@(0))'=-1
 .I $P(@CT,U,2)=VITAL S @RET@(0)=$P(@CT,U,3) Q
 Q
 ;
GETOBS(CAT,CD) ; GET OBSERVATIONS
 D OBG^DSIO10(.RET,DFN,,,,,,$G(CAT),,$G(CD))
 K:$$UP^XLFSTR($G(@RET@(0)))["NOTHING FOUND" @RET
 S @RET@(0)="IEN^DATE^PATIENT^DFN^BABY^BABY IEN^PREGNANCY^PREGNANCY IEN^CATEGORY^"
 S @RET@(0)=@RET@(0)_"CODE^SYSTEM^UPDATED BY^UPDATEDBY IEN^DESCRIPTION^VALUE"
 Q
 ;
PERSON ; GET LIST OF PEOPLE ASSOCIATED WITH THE PATIENT - DSIO PERSON
 N OUT,I,CT,FLG
 D PERSON^DSIO9(.OUT,DFN,,1)
 S @RET@(0)="IEN^NAME^SEX^DOB^EDUCATION^STREET1^STREET2^STREET3^CITY^STATE^ZIP^PHONE HOME^"
 S @RET@(0)=@RET@(0)_"PHONE CELL^PHONE WORK^PATIENT^DFN^RELATIONSHIP^STATUS^DIAGNOSIS"
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$D(FLG)
 .I $$UP^XLFSTR(@CT)["NOTHING FOUND" S FLG=1 Q
 .I @CT[U S @RET@(I)=@CT D
 ..S $P(@RET@(I),U,10)=$$GET1^DIQ(19641.11,+@RET@(I)_",",1.5),I=I+1
 Q
 ;
PREG ; GET LIST OF HISTORICAL PREGNANCIES
 N OUT,I,CT,FLG,J,FLD,STR,T,STR1
 D PREGG^DSIO10(.OUT,DFN)
 S @RET@(0)="IEN^STRT^PATIENT^DFN^TYP^FOF^FOF IEN^EDD^END^OBP^OBP IEN^PFAC^PFAC IEN^"
 S @RET@(0)=@RET@(0)_"UPDATE BY^UPDATE BY IEN^BABY;IEN;GENDER;WEIGTH;STILLBORN|^"
 S @RET@(0)=@RET@(0)_"GESTATIONAL AGE AT DELIVERY^LENGTH OF LABOR^TYPE OF DELIVERY^"
 S @RET@(0)=@RET@(0)_"ANESTHESIA^PLACE OF DELIVERY^PRETERM LABOR^BIRTH TYPE^COMMENTS"
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$D(FLG)
 .I $$UP^XLFSTR(@CT)["NOTHING FOUND" S FLG=1 Q
 .Q:$P(@CT,U,5)="C"
 .S @RET@(I)=@CT
 .S FLD=3.1 F J=17:1:22 S $P(@RET@(I),U,J)=$$GET1^DIQ(19641.13,+@CT_",",FLD),FLD=FLD+.1
 .S $P(@RET@(I),U,23)=$$GET1^DIQ(19641.13,+@CT_",",999.1)
 .S $P(@RET@(I),U,24)=$$GET1^DIQ(19641.13,+@CT_",",4)
 .; UPDATE THE BABIES (BABY;IEN;GENDER;WEIGTH;STILLBORN|)
 .S STR=$P(@RET@(I),U,16) F J=1:1:$L(STR,"|") S STR1=$P(STR,"|",J) D  S $P(STR,"|",J)=STR1
 ..S FLD=.05 F T=3:1:5 D  S FLD=FLD+.01
 ...S $P(STR1,";",T)=$$GET1^DIQ(19641.112,$P(STR1,";",2)_",",FLD,$S(T=3:"I",1:""))
 .S $P(@RET@(I),U,16)=STR,I=I+1
 Q
 ;
BAY ; GET BABIES FOR CURENT PREGNANCY
 Q:'$$PG^DSIO4(DFN)
 N OUT,CT,I,BIEN,BW,BW1,L,BW2
 S @RET@(0)="IEN^NUMBER^GENDER^WEIGHT^STILLBORN^DEMISE^APGAR^NICU^COMMENTS"
 D LIST^DIC(19641.132,","_$$PG^DSIO4(DFN)_",","@;.01I","P",,,,,,,"OUT")
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,2)<1
 .S BIEN=$P(@CT,U,2)
 .S @RET@(I)=BIEN_U_$$GET1^DIQ(19641.112,BIEN_",",.01)_U_$$GET1^DIQ(19641.112,BIEN_",",.05,"I")
 .S BW=$$UP^XLFSTR($$GET1^DIQ(19641.112,BIEN_",",.06)) I BW'?.N1"|".N1"|".N D
 ..S BW1=$$TRIM^XLFSTR($P(BW,"LB"))_"|"_$$TRIM^XLFSTR($P(BW,"OZS"))_"|"_$$TRIM^XLFSTR($P(BW,"GRAMS"))
 ..F L=$L(BW1):-1:1 D
 ...I $E(BW1,L)="|" S BW2=$E(BW1,L)_$G(BW2) K FLG Q
 ...I '$D(FLG),$E(BW1,L)?.N S BW2=$E(BW1,L)_$G(BW2) Q
 ...S FLG=1
 .S @RET@(I)=@RET@(I)_U_$G(BW2,BW)_U_$$GET1^DIQ(19641.112,BIEN_",",.07,"I")_"^^^^"
 .S I=I+1
 Q
 ;
PAT(FLD) ; GET PATIENT INFORMATION
 N L,R S L(0)=$G(FLD),RET=$NA(^TMP($J,"OCNT PAT")) K @RET S @RET@(0)=""
 D INFO^DSIO1(.R,$G(DFN),.L) S @RET@(0)=$P(R(0),U,2)
 Q
 ;
MH(FLD) ; GET CURRENT MENSTRUAL HISTORY FIELD
 ;
 ; THE FOLLOWING ARE CHECKBOXES AND ONLY ONE SHOULD BE RETURNED WITH A VALUE
 ; 1.1, 1.3, 1.4 (N(ORMAL)/A(BNORMAL))
 ; 1.5(Y(ES)/N(O))
 ;
 N IEN S RET=$NA(^TMP($J,"OCNT MH")) K @RET
 S IEN=$$GET1^DIQ(19641,DFN_",",1.2,"I") Q:+IEN<1
 I $$GET1^DID(19641.01,FLD,,"TYPE")="WORD-PROCESSING" D  Q
 .K @RET D GETS^DIQ(19641.01,IEN_",",2,"E",RET)
 .K @RET@(19641.01,IEN_",",2,"E")
 I $$GET1^DID(19641.01,FLD,,"TYPE")="DATE/TIME" D  Q
 .S @RET@(0)=$$FMTE^XLFDT($$GET1^DIQ(19641.01,IEN_",",FLD,"I"),"5Z")
 S @RET@(0)=$$GET1^DIQ(19641.01,IEN_",",FLD)
 I CONTROL="CKMENSESYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKMENSESNO",@RET@(0)="NORMAL" S @RET@(0)=""
 I CONTROL="CKAMOUNTYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKAMOUNTNO",@RET@(0)="NORMAL" S @RET@(0)=""
 I CONTROL="CKDURATIONYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKDURATIONNO",@RET@(0)="NORMAL" S @RET@(0)=""
 I CONTROL="CKCONTRACEPTIONYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKCONTRACEPTIONNO",@RET@(0)="NORMAL" S @RET@(0)=""
 Q
 ;
EDU ; GET EDUCATION
 N OUT,I,CT
 D LIST^DIC(19641.8,,"@;.01","P",,,,,,,"OUT") Q:'$D(OUT)
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,2)<1
 .S @RET@(I)=$P(@CT,U,2),I=I+1
 Q
 ;
 ; ----------------------------------------------------------------------------
 ;
VAL(CTR) ; GET VALUE BY CONTROL NAME
 Q $$VAL^DSIO02(CTR)
 ;
CIEN(CTR) ; GET CONTROL IEN
 Q $$CIEN^DSIO02(CTR)
 ;
VALWP(CTR,FLE,FLD,XIEN) ; TRANSFER A WORD PROCESSING FIELD
 D VALWP^DSIO02(CTR,FLE,FLD,XIEN)
 Q
