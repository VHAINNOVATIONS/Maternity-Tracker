Routine DSIO6 saved using VFDXTRS routine on May 20, 2015 17:01
DSIO6^INT^63692,55777^May 20, 2015@15:29
DSIO6 ;DSS/TFF - DSIO DISCREET DATA - DDCS;06/28/2013 15:19
 ;;1.0;DSIO 1.0;;Feb 06, 2015;Build 23
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
 ;=============================================================================
 ;                          D I S C R E E T  D A T A
 ;=============================================================================
 ;
 ; DSIO TIU DISCREET CONTROL (FILE TO PUSH DISCREET DATA ELEMENTS
 ; ELSEWHERE) (19641.4)
 ;
 ; - FIRST BY TIU DOCUMENT DEFINITION (8925.1) THEN BY DATA ELEMENT
 ;   - A DOCUMENT DEFINITION CAN CONTROL MANY DATA ELEMENTS
 ;
 ; - DSIO TIU DISCREET DATA (FILE THAT STORES THE DISCREET DATA) (19641.41)
 ;   - FIRST BY PATIENT - THEN BY NOTE (8925)
 ;
 ; DIALOGS CAN SAVE DISCREET DATA UNDER THE TIU NOTE IEN
 ; WITHIN THE DSIO NOTE DATA FILE
 ; 
 ; DIALOG = SOURCE
 ;  R - REMINDER DIALOG
 ;  C - OCNT
 ;  O - OTHER (RPC)
 ;  D - DIALOG (OCNT DIALOG)
 ;
 ; FROM : R### = REMINDER DIALOG AND ITS IEN
 ;      : Cxxx = OCNT AND ITS NAME
 ;      : Oxxx = OTHER AND THE NOTE TITLE - THIS RESTRICTS TO ONE NOTE
 ;      : D### = IEN OF AN OCNT DIALOG
 ;
STORED(RET,NIEN,DATA,FROM) ; RPC: DSIO DDCS STORE (DIRECT)
 N DIEN S RET=-1
 I $E($G(FROM),1)="R" S DIEN=$E(FROM,2,99),FROM="R"
 E  I $E($G(FROM),1)="C" S DIEN=$E(FROM,2,99),FROM="C"
 E  I $E($G(FROM),1)="O" S DIEN="",FROM="O"
 E  I $E($G(FROM),1)="D" S DIEN=$E(FROM,2,99),FROM="D"
 I $D(DIEN) D SETDATA S:$P(RET,U,2)="" RET=1
 Q
 ;
STORET(RET,NIEN,DATA,FROM) ; RPC: DSIO DDCS STORE (TASK)
 ; SET AS A TASK AND RETURN THE TASK ID
 ;
 N DIEN,ZTRTN,ZTDESC,ZTDTH,ZTIO,ZTUCI,ZTCPU,ZTPRI,ZTSAVE,ZTKIL,ZTSYNC,ZTSK
 S ZTRTN="SETDATA^DSIO6",ZTDESC="DSIO DISCREET DATA STORAGE"
 S ZTDTH=$$FMTH^XLFDT($$NOW^XLFDT),ZTIO="DSIO DDCS",ZTPRI=10
 S ZTSAVE("NIEN")="",ZTSAVE("DATA(")=""
 I $E($G(FROM),1)="R" S ZTSAVE("DIEN")=$E(FROM,2,99),ZTSAVE("FROM")="R"
 E  I $E($G(FROM),1)="C" S ZTSAVE("DIEN")=$E(FROM,2,99),ZTSAVE("FROM")="C"
 E  I $E($G(FROM),1)="O" S ZTSAVE("DIEN")="",ZTSAVE("FROM")="O"
 E  I $E($G(FROM),1)="D" S ZTSAVE("DIEN")=$E(FROM,2,99),ZTSAVE("FROM")="D"
 E  S RET=-1 Q
 D ^%ZTLOAD S RET=$G(ZTSK,-1)
 Q
 ;
SETDATA ; SAVE DATA AS DISCREET ELEMENTS FOR A TIU NOTE
 ; INPUT VARIABLES REQUIRED (NIEN,DATA,FROM,DIEN)
 ;
 ; EXAMPLE ARRAY:
 ; --------------
 ; (S,M,WP)^CONTROL^LABEL 1^INDEX(OPTIONAL ONLY FOR M)^VALUE
 ;  - M AND WP, AND HAVE REPEATED ENTRIES THAT WILL BE APPLIED TO ONE ENTRY
 ;
 ; IN THE CASE OF A "WP" OR A "M" THE NOTE TEXT ONLY HAS TO BE APPLIED TO ONE
 ; ENTRY PER CONTROL ENTRIES - THEY WILL BE PROCESSED AND CREATED AS A SINGLE
 ; ENTRY TO DSIO NOTE DATA.
 ;
 ; REMINDER DIALOG - FROM = R
 ; --------------------------
 ; (S,M,WP)^CLASS|IENS^CAPTION^INDEX(OPTIONAL ONLY FOR M)^VALUE
 ;
 N DFN,IEN,TITLE,TIEN,DLAYGO,DIC,DA,X,Y,SUB,CT,VP,FLG,INFAC,NM,CTR,IPT,TYP,DONE
 N FLE,IEN1,SHAR,RIEN,RIENS,CK,LINK,TMP,ID,CI,I,DONE,ERR
 N DSIOSILENT S DSIOSILENT=1 ; DON'T RUN OUR X-REFS
 ; R = REMINDER           THE IEN IS PASSED
 ; C = OCNT               THE NAME OF THE FILE(DLL) IS PASSED
 ; O = OTHER              THE TITLE WILL BE USED
 ; D = DIALOG             THE DIEN IS PASSED
 ; GET THE DSIO NOTE CONTROL IEN - IS IT TO BE CONTROLLED?
 S DFN=$$GET1^DIQ(8925,NIEN_",",.02,"I") I +DFN<1 S RET="-1^Invalid patient." Q
 S TIEN=$$TITLE^DSIO3(.RET,NIEN),TITLE=$$GET1^DIQ(8925.1,TIEN_",",.01)
 I FROM="R" D
 .S IEN=$$FIND1^DIC(19641.4,,,$$GET1^DIQ(801.41,DIEN_",",.01))
 .S FLE=19641.411,IEN1="?+1,",DIEN=TITLE,INFAC="O.`"_TIEN
 E  S IEN=$$FIND1^DIC(19641.4,,,TITLE)
 I FROM="C"!(FROM="O")!(FROM="E") D
 .S FLE=19641.411,IEN1="?+1,"
 .S:DIEN="" DIEN=TITLE,INFAC="O.`"_TIEN
 I FROM="D" Q:'$$ACTION(DIEN,NIEN,"SAVE")  S INFAC="D.`"_DIEN D
 .S FLE=19641.412,IEN1=DIEN_",",SHAR=$$GET1^DIQ(19641.49,DIEN_",",.04,"I")
 S:'$D(INFAC) INFAC=FROM_"."_DIEN
 I IEN=0 S RET="-1^Title or Reminder not controlled." Q
 ; CREATE OR FIND THE DSIO NOTE DATA ENTRY
 S DLAYGO=19641.41,DIC="^DSIO(19641.41,",DIC(0)="XL",X="`"_NIEN D ^DIC
 I +Y<1 S RET="-1^Entry does not exist." Q
 I FROM="D" K DIC,DA,X,Y D
 .S DA(1)=NIEN,DIC="^DSIO(19641.41,"_DA(1)_",2,"
 .S DIC(0)="XL",X="`"_DIEN,DINUM=DIEN D ^DIC S:+Y>0 SUB(1)=+Y
 .I SHAR K DIC,DA,X,Y D
 ..S IPT(FLE,IEN1_NIEN_",",.02)=1 D FILE^DIE(,"IPT")
 ..S DLAYGO=19641.4941,DIC="^DSIO(19641.4941,",DIC(0)="XL",X="`"_DFN D ^DIC
 ..K DIC,DA,X,Y S DA(1)=DFN,DIC="^DSIO(19641.4941,"_DA(1)_",2,"
 ..S DIC(0)="XL",X="`"_DIEN,DINUM=DIEN D ^DIC S:+Y>0 SUB(1)=+Y
 E  I FROM'="D" S IPT(FLE,IEN1_NIEN_",",.01)=DIEN D
 .D UPDATE^DIE("E","IPT",$S(IEN1'?.N:"SUB",1:"")) K IPT
 .I '$D(SUB),IEN1'["+" S SUB(1)=$P(IEN1,",")
 I $G(SUB(1))="" S RET="-1^Interface failed to create multiple." Q
 S IPT(19641.41,NIEN_",",.02)=IEN D FILE^DIE(,"IPT")
 S CT="" F  S CT=$O(DATA(CT)) Q:CT=""  D
 .Q:$D(DONE(CT))
 .; FIND EXISTING ENTRY TO ADD OR EDIT
 .; UNIQUENESS KEY = "C" - NOTE(NIEN),INTERFACE(INFAC),CONTROL(CTR),FIELD(NM)
 .S NM=$$UP^XLFSTR($$TRIM^XLFSTR($P(DATA(CT),U,3))) Q:NM=""
 .S CTR=$$UP^XLFSTR($P($P(DATA(CT),U,2),"|")) Q:CTR=""
 .Q:FROM="D"&($$GET1^DIQ(19641.491,$$FIND1^DIC(19641.491,","_DIEN_",","X",CTR)_","_DIEN_",",.04,"I"))  ; DO NOT SAVE CONTROL
 .S VP="S VP=$S(INFAC[""D"":DIEN_"";DSIO(""_19641.49,INFAC[""C"":"
 .S VP=VP_"$$FIND1^DIC(19641.48,,,DIEN)_"";DSIO(""_19641.48,1:"
 .S VP=VP_"TIEN_"";TIU(""_8925.1)" X VP
 .S FLG=$O(^DSIO(19641.45,"C",NIEN,VP_",",CTR,NM,""))
 .I $G(FLG)="" S FLG="+1"
 .S IPT(19641.45,FLG_",",.01)=NM
 .S:$P(DATA(CT),U,4)'="" IPT(19641.45,FLG_",",.02)=1
 .S IPT(19641.45,FLG_",",2.1)="`"_NIEN
 .S IPT(19641.45,FLG_",",2.2)=FROM
 .S IPT(19641.45,FLG_",",2.3)=$$NOW^XLFDT
 .S IPT(19641.45,FLG_",",2.4)=CTR
 .S (TYP,IPT(19641.45,FLG_",",2.5))=$P(DATA(CT),U)
 .S IPT(19641.45,FLG_",",2.6)=INFAC
 .I FROM="R" D
 ..S RIEN=$P($P($P(DATA(CT),U,2),"|",2),",")
 ..S RIENS=$$RIENS(RIEN,$P($P($P(DATA(CT),U,2),"|",2),",",2))
 ..I +$G(RIENS)>0 S IPT(19641.45,FLG_",",3.1)="`"_RIENS
 ..I +$G(RIEN)>0 S IPT(19641.45,FLG_",",3.2)="`"_RIEN
 .S DLAYGO=19641.45 K CK D UPDATE^DIE("EK","IPT",$S(FLG["+":"CK",1:"")) K IPT
 .I '$D(CK),FLG'["+" S CK(1)=FLG
 .I $G(CK(1))'="" S LINK(CK(1))="" D
 ..S TMP=$NA(^TMP($J,"SETDATA VALUE")) K @TMP
 ..; SET UP THE VALUE GLOBAL (PIECE 5 AND BEYOND = VALUE)
 ..I TYP="S" S @TMP@(1)=$P(DATA(CT),U,5,9999)
 ..I TYP="M" D
 ...S ID=$P(DATA(CT),U,2) Q:ID=""
 ...S CI=1,I=CT-1 F  S I=$O(DATA(I)) Q:'I  D
 ....I $P(DATA(I),U,2)=ID&($P(DATA(I),U)="M") D
 .....S @TMP@(CI)=$S($P(DATA(I),U,4)="":0,1:$P(DATA(I),U,4))_U_$P(DATA(I),U,5,9999)
 .....S CI=CI+1,DONE(I)="" ; JUMPING AHEAD - SET DONE
 ..I TYP="WP" D
 ...S ID=$P(DATA(CT),U,2) Q:ID=""
 ...S CI=1,I=CT-1 F  S I=$O(DATA(I)) Q:'I  D
 ....I $P(DATA(I),U,2)=ID&($P(DATA(I),U)="WP") D
 .....S @TMP@(CI)=$P(DATA(I),U,5,9999)
 .....S CI=CI+1,DONE(I)="" ; JUMPING AHEAD - SET DONE
 ..; SAVE THE VALUE GLOBAL
 ..I $D(@TMP) D WP^DIE(19641.45,CK(1)_",",1,"K",TMP) K @TMP
 ; ADD DSIO NOTE ELEMENTS TO THE DSIO NOTE DATA MULTIPLE
 I $D(LINK) S CT="" F  S CT=$O(LINK(CT)) Q:CT=""  D
 .I FROM="D",SHAR D  Q
 ..S DLAYGO=19641.4941
 ..S IPT(19641.494121,"?+1,"_SUB(1)_","_DFN_",",.01)=CT D
 ...D UPDATE^DIE(,"IPT") K IPT
 .S DLAYGO=FLE,IPT(FLE_1,"?+1,"_SUB(1)_","_NIEN_",",.01)=CT
 .D UPDATE^DIE(,"IPT") K IPT
 ; PUSH SHARED DIALOGS NOW OR IF 
 I FROM="D",$$ACTION(DIEN,NIEN,"SUBMIT") D DPUSH(DFN,DIEN)
 Q
 ;
RIENS(IEN,SEQ) ; GET REMINDER DIALOG BASED OFF OF ITS SEQUENCE TO A
 ;               REMINDER DIALOG
 N IENS
 S IENS=$$FIND1^DIC(801.412,","_IEN_",","X",SEQ) Q:IENS="" ""
 Q $$GET1^DIQ(801.412,IENS_","_IEN_",",2,"I")
 ;
 ; ---------------------------- DDCS - PUSH AND HISTORY -----------------------
 ;
TRIG ; CHECK NOTE DATA FOR STATUS UPDATE AND ACT
 N IEN,IPT
 S IEN=0 F  S IEN=$O(^DSIO(19641.41,"PUSH",IEN)) Q:'IEN  D
 .I '$D(^TIU(8925,IEN))!($$GET1^DIQ(8925,IEN_",",.05)="DELETED") D  Q
 ..D DELETE^DSIO0(IEN)
 .I $$GET1^DIQ(8925,IEN_",",.05)="COMPLETED" D
 ..D PUSH(IEN)
 ..S IPT(19641.41,IEN_",",.03)=$$NOW^XLFDT D FILE^DIE(,"IPT")
 Q
 ;
PUSH(NIEN) ; PUSH DATA OUT TO VISTA
 ;
 ; THIS SHOULD BE CALLED BY AN X-REF WHEN A NOTE IS SIGNED NOT WHEN IT IS
 ; CREATED OR EDITED.
 ;
 ;  NIEN - DSIO NOTE DATA ENTRY
 ;  IENS - THE SUBENTRY OF THE CONTROL IN DSIO NOTE CONTROL
 ;  RIEN - THE IEN;FILE (VARIABLE POINTER) OF THE CONTROL
 ; RIENS - THE SUB REMINDER DIALOG IF ONE
 ;
 ; START - CHECK DSIO NOTE CONTROL (19641.4) FOR THE EXISTENCE OF THE
 ;         CONTROL AND THAT IS IT NOT INACTIVE
 ;       - GO TO DSIO REPORT ITEMS (OCNT OR OCNT DIALOGS) (19641.401)
 ;         OR DSIO REMINDER ELEMENTS (REMINDER DIALOGS) (19641.402)
 ;         AND CONFIRM THAT IT IS NOT INACTIVE
 ;       * REPORT ITEMS ARE LIKE A FUNCTION WITH THEIR COMPONENT THAT
 ;         PREFORMS THIS FUNCTION SET IN DSIO NOTE CONTROL WHILE
 ;         A REMINDER ELEMENT IS SET WITHIN ITS OWN FILE
 ;   END - IF A FILE AND FIELD IS SET THEN PUSH THE DATA THERE
 ;
 N DFN,IEN,OUT,CT,CTR,FNAME,FIEN,CIEN,PUSH,FPLE,PFLD,VAL,DIEN
 N IENS,RIEN,RIENS,DONE,ROUT,RCT,VAL,FLG,INPUT,ROU,RET,IPT,ELE
 S DFN=$$GET1^DIQ(8925,NIEN_",",.02,"I") Q:+DFN<1
 Q:'$$FIND1^DIC(19641.41,,"A",NIEN)
 S IEN=$$GET1^DIQ(19641.41,NIEN_",",.02,"I")
 D GETS^DIQ(19641.41,NIEN_",","**","I","OUT") Q:'$D(OUT)
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .; PUSH SHARED DIALOGS
 .I $QS(CT,1)=19641.412,$QS(CT,3)=.02,@CT D DPUSH(DFN,+$QS(CT,2))
 .Q:$L($QS(CT,1))<10
 .;Q:$$GET1^DIQ(19641.45,@CT_",",4.1,"I")'=""  ; ALREADY PUSHED
 .K ELE,CTR,FNAME,FIEN,CIEN,PUSH,PFLE,PFLD,INPUT,ROU
 .S ELE=@CT,CTR=$$UP^XLFSTR($$GET1^DIQ(19641.45,ELE_",",2.4))
 .; REPORT ITEM
 .I $QS(CT,1)=19641.4111,'$$GET1^DIQ(19641.45,ELE_",",3.1,"I") D  Q
 ..S FNAME=$$GET1^DIQ(19641.411,$P($QS(CT,2),",",2)_","_NIEN_",",.01)
 ..S FIEN=$$FIND1^DIC(19641.48,,"X",FNAME)
 ..S CIEN=$$FIND1^DIC(19641.481,","_FIEN_",","X",CTR) Q:CIEN<1
 ..S PUSH=$$GET1^DIQ(19641.481,CIEN_","_FIEN_",",.03,"I") Q:PUSH<1
 ..D REPORT
 .; DIALOG
 .I $QS(CT,1)=19641.4121 D  Q
 ..S DIEN=$P($QS(CT,2),",",2)
 ..S CIEN=$$FIND1^DIC(19641.491,","_DIEN_",","X",CTR) Q:CIEN<1
 ..S PUSH=$$GET1^DIQ(19641.491,CIEN_","_DIEN_",",.03,"I") Q:PUSH<1
 ..D REPORT
 .Q:$QS(CT,1)'=19641.4111&('$$GET1^DIQ(19641.45,ELE_",",3.1,"I"))
 .; REMINDER ELEMENT
 .S RIEN=$$GET1^DIQ(19641.45,ELE_",",3.2,"I")
 .S RIENS=$$GET1^DIQ(19641.45,ELE_",",3.1,"I")
 .S IENS=+$O(^DSIO(19641.4,IEN,1,"B",RIEN,"")) Q:IENS<1
 .Q:$$GET1^DIQ(19641.42,IENS_","_IEN_",",.02,"I")  ; INACTIVE
 .; CHECK TO SEE IF THIS ELEMENT IS INACTIVATED FOR ALL USE
 .Q:$$GET1^DIQ(19641.402,RIEN_",",.02,"I")
 .; DEFINE THE PUSH FILE AND FIELD
 .Q:$D(DONE(RIEN))  S DONE(RIEN)=""
 .K ROUT D GETS^DIQ(19641.402,RIEN_",","1*","I","ROUT") Q:'$D(ROUT)
 .S RCT=$NA(ROUT) F  S RCT=$Q(@RCT) Q:RCT=""  D
 ..; WITH INFO FROM THE CONTAINER SEE IF THERE IS A DSIO NOTE ELEMENT
 ..; ENTRY FOR THE SUBCOMPONENT
 ..I $QS(RCT,3)=.01 S FLG=@RCT Q
 ..Q:'$D(FLG)
 ..I $QS(RCT,3)=.02&(@RCT) K FLG Q  ; INACTIVE
 ..Q:$QS(RCT,3)=.02
 ..I $QS(RCT,3)=.03 S PFLE=@RCT Q
 ..I $QS(RCT,3)=.04 S PFLD=@RCT Q
 ..I $QS(RCT,3)=1 S INPUT=@RCT Q
 ..I $QS(RCT,3)=2 S ROU=@RCT
 ..Q:$G(PFLE)=""&($G(PFLD)="")&($G(ROU)="")
 ..D PVAL(ELE) I ROU'="",PFLE="",PFLD="" X ROU K FLG D REC(ELE,"ROU") Q
 ..Q:PFLE=""&(PFLD="")
 ..D HISTORY^DSIO98(.RET,DFN_";DPT(",PFLE,PFLD,NIEN_";TIU(8925,",VAL)
 ..K FLG D REC(ELE,RET)
 .K PFLE,PFLD,INPUT,ROU,FLG
 Q
 ;
REPORT ; PUSH REPORT ITEM
 Q:$$GET1^DIQ(19641.401,PUSH_",",.02,"I")  ; INACTIVE
 S PFLE=$$GET1^DIQ(19641.401,PUSH_",",.03,"I")
 S PFLD=$$GET1^DIQ(19641.401,PUSH_",",.04,"I")
 S INPUT=$$GET1^DIQ(19641.401,PUSH_",",1)
 D PVAL(ELE) I PFLE="",PFLD="" D  Q
 .S ROU=$$GET1^DIQ(19641.401,PUSH_",",2) I ROU'="" X ROU D REC(ELE,"ROU")
 D HISTORY^DSIO98(.RET,DFN_";DPT(",PFLE,PFLD,NIEN_";TIU(8925,",VAL) D REC(ELE,RET)
 Q
 ;
REC(ELE,HIS) ; RECORD PUSH
 N IPT I HIS="ROU" S IPT(19641.45,ELE_",",4.1)=$$NOW^XLFDT
 E  I +$G(HIS)>0 S IPT(19641.45,ELE_",",4.1)=HIS
 D FILE^DIE(,"IPT") K IPT
 Q
 ;
PVAL(IEN) ; GET CAPTURED VALUE FROM DSIO NOTE ELEMENT (19641.45)
 N ND,ND1,X S VAL=$NA(^TMP($J,"SETDATA VALUE")) K @VAL
 D GETS^DIQ(19641.45,IEN_",",1,"E",VAL)
 K @VAL@(19641.45,IEN_",",1,"E")
 S ND=$NA(@VAL@(19641.45,IEN_",",1)) M ND1=@ND K @VAL M @VAL=ND1
 ; INPUT TRANSFORM
 I $G(INPUT)'="",$O(@VAL@(""),-1)=1 S X=@VAL@(1) X INPUT S @VAL@(1)=X
 Q
 ;
DPUSH(DFN,DIEN) ; PUSH SHARED DIALOG DATA
 N OUT,CT,ELE,CTR,CIEN,PUSH,PFLE,PFLD,NIEN,INPUT,ROU,VAL
 D GETS^DIQ(19641.49412,DIEN_","_DFN_",","**","I","OUT") Q:'$D(OUT)
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,1)=19641.49412
 .;Q:$$GET1^DIQ(19641.45,@CT_",",4.1,"I")'=""  ; ALREADY PUSHED
 .K ELE,CTR,CIEN,PUSH,NIEN,PFLE,PFLD,INPUT,ROU
 .S ELE=@CT,CTR=$$UP^XLFSTR($$GET1^DIQ(19641.45,ELE_",",2.4))
 .S CIEN=$$FIND1^DIC(19641.491,","_DIEN_",","X",CTR) Q:CIEN<1
 .S PUSH=$$GET1^DIQ(19641.491,CIEN_","_DIEN_",",.03,"I") Q:PUSH<1
 .S NIEN=19641.49_":"_DIEN D REPORT
 Q
 ;
 ; ----------------------------------------------------------------------------
 ;
GETS(ARY,IEN) ; APPEND DISCREET DATA TO A NOTE ARRAY <NOT USED 08/14/14>
 N OUT,I,FLD,PROC,APP,CT
 S I=$O(@ARY@(""),-1)+1 Q:+$G(IEN)<1!($G(ARY)="")
 S @ARY@(I)="----------------- DISCREET DATA FIELDS -----------------",I=I+1
 D GETS^DIQ(19641.41,IEN_",","1*","E","OUT")
 I $D(OUT) S FLD=$NA(OUT) F  S FLD=$Q(@FLD) Q:FLD=""  D
 .Q:$QS(FLD,3)'=.01  Q:$D(PROC($$UP^XLFSTR(@FLD)))
 .S PROC($$UP^XLFSTR(@FLD))="" K APP D GETDATA(.APP,IEN,@FLD) Q:$G(APP(0))=-1
 .S CT=$NA(APP) F  S CT=$Q(@CT) Q:CT=""  D
 ..S @ARY@(I)="TIME: "_$P(@CT,U)_" "_@FLD_" = "_$P(@CT,U,2),I=I+1
 I @ARY@($O(@ARY@(""),-1))="----------------- DISCREET DATA FIELDS -----------------" D
 .K @ARY@($O(@ARY@(""),-1))
 Q
 ;
GETDATA(RET,IEN,FLD) ; RPC: DSIO GET NOTE ELEMENT
 ;
 ; GET DATA SAVED AS DISCREET ELEMENTS LINKED TO A TIU NOTE
 ;
 ; RETURN: COMPONENT ^ DATE ^ VALUE
 ;
 S RET(0)="0^Nothing found."
 I +$G(IEN)<1 S RET(0)="-1^TIU DOCUMENT IEN is required." Q
 I $G(FLD)="" D GETDATA1(.RET,IEN) Q
 D GETFLD(.RET,IEN,FLD)
 Q
 ;
GETDATA1(RET,IEN) ; GET ALL NOTE ELEMENTS
 N LIST,I,FLD,PROC,APP,CT S RET(0)="0^Nothing found."
 D GETS^DIQ(19641.41,IEN_",","1*","E","LIST")
 I $D(LIST) S I=0,FLD=$NA(LIST) F  S FLD=$Q(@FLD) Q:FLD=""  D
 .Q:$QS(FLD,1)=19641.411
 .Q:$QS(FLD,3)'=.01  Q:$D(PROC($$UP^XLFSTR(@FLD)))
 .S PROC($$UP^XLFSTR(@FLD))=""
 .K APP D GETFLD(.APP,IEN,@FLD) Q:$P($G(APP(0)),U)=-1
 .S CT=$NA(APP) F  S CT=$Q(@CT) Q:CT=""  D
 ..S RET(I)=@CT,I=I+1
 Q
 ;
GETFLD(RET,IEN,FLD) ; GET A SINGLE NOTE ELEMENT
 ;
 ; 0 : <COMPONENT>^COMPONENT NAME^DATE
 ; # : VALUE
 ;
 N I,OUT,CT,EIEN,DATE,DTIV,DTE,DATA,M,MCT
 S I=1,FLD=$$UP^XLFSTR($G(FLD)),RET(0)=-1
 D GETS^DIQ(19641.41,IEN_",","1*","IE","OUT")
 I $D(OUT) S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .I $$UP^XLFSTR(@CT)=FLD D
 ..S EIEN=$G(OUT($QS(CT,1),$QS(CT,2),$QS(CT,3),"I"))
 ..S DATE=$$GET1^DIQ(19641.45,EIEN_",",2.3,"I") Q:DATE=""
 ..S DTIV=9999999-DATE,DTE=$$FMTE^XLFDT(DATE,"5Z")
 ..D GETS^DIQ(19641.45,EIEN_",","1",,"M") I $D(M) D  K M
 ...S DATA(DTIV,0)="<COMPONENT>"_U_FLD_U_DTE
 ...S MCT=$NA(M) F  S MCT=$Q(@MCT) Q:MCT=""  D
 ....S:$QL(MCT)=4 DATA(DTIV,I)=@MCT,I=I+1
 I $D(DATA) K RET M RET=DATA
 Q
 ;
TRACK(RET,IENSTR) ; RPC: DSIO IS IT CONTROLLED?
 ;
 ; IS THIS A DISCREET DATA COLLECTING NOTE?
 ; ONLY LOOKING AT REMINDER DIALOGS
 ;
 S RET=0 I $D(^DSIO(19641.4,"B",$G(IENSTR))) D
 .I '$$GET1^DIQ(19641.4,$O(^DSIO(19641.4,"B",IENSTR,""))_",",.02,"I") S RET=1
 Q
 ;
 ; ---------------------------------- UTILITY ---------------------------------
 ;
ACTION(DIEN,NIEN,FUN,IEN) ; DETERMINE IF THE DIALOG IS ALLOWED TO PROCEED WITH
 ;                          THE RESTORE OR SAVE FUNCTIONALITY
 ;
 ; FUN = RESTORE,SAVE,SUBMIT
 ; TO BE CALLED AS Q:'$$ACTION
 ;
 N RET,TIEN,TITLE,OUT,CT,FLG,ACT
 S TIEN=$$TITLE^DSIO3(.RET,NIEN),TITLE=$$GET1^DIQ(8925.1,TIEN_",",.01)
 D GETS^DIQ(19641.49,$G(DIEN)_",","2*","E","OUT") Q:'$D(OUT) 1
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$D(FLG)
 .I $QS(CT,3)=.01 S ACT=@CT Q
 .I FUN="RESTORE" D
 ..I ACT="DNRT" D  Q
 ...I TITLE=@CT S FLG=0 I $G(IEN),NIEN=IEN K FLG
 ..I ACT["DNR" S FLG=0
 .I FUN="SAVE",ACT["DNS" S FLG=0 Q
 .I FUN="SUBMIT",ACT="SN" S FLG=1
 Q $S(FUN="SUBMIT":+$G(FLG),1:+$G(FLG,1))
