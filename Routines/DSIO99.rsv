Routine DSIO99 saved using VFDXTRS routine on May 20, 2015 09:21
DSIO99^INT^63692,31238^May 20, 2015@08:40
DSIO99 ;DSS/TFF - DSIO IMPORT/EXPORT TIU TITLES;06/28/2013 15:19
 ;;1.0;DSIO 1.0;;Feb 06, 2015;Build 23
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ; $$MIXED^TIULS
 ;
 ;
 ;
 ; CLINICAL DOCUMENTS (CL)
 ;  +PROGRESS NOTES (CL)
 ;   +MATERNITY TRACKER (DC) - OR OTHER DOCUMENT CLASS
 ;    TITLE <- SELECT AT THIS LEVEL
 ;
 D UPDATE,CONTROL,FIX
 ;
 Q
 ;
UPDATE ; UPDATE FILES WITH CORRECT NOTE POINTERS
 ; ADD THE FILES WITH DATA, IF THE TITLE DOESN'T EXIST THEN ADD IT,
 ; THEN CORRECT THE POINTER, IF THE TITLE WASN'T BROUGHT IN THEN DELETE
 ; THE FILE ENTRY
 ;
 N I,STR,TITLE,FNAME,IEN
 ; DSIO NOTE CONTROL (19641.4)
 F I=1:1 S STR=$P($T(TITLE+I),";;",2) Q:STR=""  D
 .S TITLE=$P(STR,U),FNAME=$P(STR,U,3)
 .S IEN=$$FIND1^DIC(8925.1,,"X",TITLE) ; THIS SYS TITLE IEN
 .I 'IEN D IMPORT(FNAME) D  Q:'IEN     ; BRING IN THIS TITLE FROM XML AND SET IEN
 ..S IEN=$$FIND1^DIC(8925.1,,"X",TITLE)
 .S IPT(19641.4,"?+1,",.01)=IEN_";TIU(8925.1,"
 .D UPDATE^DIE(,"IPT")
 S DIK="^DSIO(19641.4," D IXALL2^DIK   ; REINDEX DELETE
 S DIK="^DSIO(19641.4," D IXALL^DIK    ; REINDEX SET
 D TITLEC
 Q
 ;
CONTROL ; CORRECT NOTE CONTROL TITLE REFERENCES
 N NAM,IEN
 F NAM="oCNT_OBFollowUp.dll" D
 .S IEN=$$FIND1^DIC(19641.48,,"X",NAM)
 .D:IEN CONTROL1(IEN)
 Q
 ;
FIX ; CORRECT THE OCNT CONFIGURATION "GET DATA FROM"
 S ^DSIO(19641.48,3,1,29,5,0)="^19641.4815IP^2^2"
 S ^DSIO(19641.48,3,1,29,5,1,0)="1^MEMOPRENATAL^"_$$FIND1^DIC(8925.1,,"X","OB H&P INITIAL")_"^^1"
 S ^DSIO(19641.48,3,1,29,5,2,0)="3^MEMOPRENATAL^"_$$FIND1^DIC(8925.1,,"X","OB FOLLOWUP NOTE")_"^^0"
 S ^DSIO(19641.48,3,1,29,5,"AB",1,1)=""
 S ^DSIO(19641.48,3,1,29,5,"AB",3,2)=""
 S ^DSIO(19641.48,3,1,29,5,"B",1,1)=""
 S ^DSIO(19641.48,3,1,29,5,"B",3,2)=""
 S ^DSIO(19641.48,3,1,29,5,"P",0,2)=""
 S ^DSIO(19641.48,3,1,29,5,"P",1,1)=""
 S ^DSIO(19641.48,3,1,31,5,0)="^19641.4815IP^2^2"
 S ^DSIO(19641.48,3,1,31,5,1,0)="1^CKLSTPROBLEMS^"_$$FIND1^DIC(8925.1,,"X","OB H&P INITIAL")_"^^1"
 S ^DSIO(19641.48,3,1,31,5,2,0)="3^CKLSTPROBLEMS^"_$$FIND1^DIC(8925.1,,"X","OB FOLLOWUP NOTE")_"^^0"
 S ^DSIO(19641.48,3,1,31,5,"AB",1,1)=""
 S ^DSIO(19641.48,3,1,31,5,"AB",3,2)=""
 S ^DSIO(19641.48,3,1,31,5,"B",1,1)=""
 S ^DSIO(19641.48,3,1,31,5,"B",3,2)=""
 S ^DSIO(19641.48,3,1,31,5,"P",0,2)=""
 S ^DSIO(19641.48,3,1,31,5,"P",1,1)=""
 S ^DSIO(19641.48,3,1,32,5,0)="^19641.4815IP^2^2"
 S ^DSIO(19641.48,3,1,32,5,1,0)="1^CKLSTEDUCATION^"_$$FIND1^DIC(8925.1,,"X","OB H&P INITIAL")_"^^1"
 S ^DSIO(19641.48,3,1,32,5,2,0)="3^CKLSTEDUCATION^"_$$FIND1^DIC(8925.1,,"X","OB FOLLOWUP NOTE")_"^^0"
 S ^DSIO(19641.48,3,1,32,5,"AB",1,1)=""
 S ^DSIO(19641.48,3,1,32,5,"AB",3,2)=""
 S ^DSIO(19641.48,3,1,32,5,"B",1,1)=""
 S ^DSIO(19641.48,3,1,32,5,"B",3,2)=""
 S ^DSIO(19641.48,3,1,32,5,"P",0,2)=""
 S ^DSIO(19641.48,3,1,32,5,"P",1,1)=""
 Q
 ;
 ; ----------------------------------------------------------------------------
 ;
CONTROL1(IEN) ;
 N ND,OUT,CT,FLG,IPT,XND
 S ND="$S(FLG=""OCNT_OBHP.DLL"":$$FIND1^DIC(8925.1,,""X"",""OB H&P INITIAL""),"
 S ND=ND_"FLG=""OCNT_OBFOLLOWUP.DLL"":$$FIND1^DIC(8925.1,,""X"",""OB FOLLOWUP NOTE""),1:"""")"
 D GETS^DIQ(19641.48,IEN_",","1*","EN","OUT")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .I $QS(CT,1)=19641.4815 D
 ..I $QS(CT,3)=.01 S FLG=$$UP^XLFSTR(@CT) Q
 ..I $QS(CT,3)=.03,$S(FLG="OCNT_OBHP.DLL"&(@CT="OB H&P INITIAL"):-1,FLG="OCNT_OBFOLLOWUP.DLL"&(@CT="OB FOLLOWUP NOTE"):-1,1:1) D
 ...S XND="S IPT(19641.4815,"""_$QS(CT,2)_""",.03)="_ND X XND
 ...D FILE^DIE(,"IPT") K IPT
 Q
 ;
TITLEC ; ADD TITLE CONFIGURATION
 N I,STR,IPT
 F I=1:1 S STR=$P($T(TITLE+I),";;",2) Q:STR=""  D
 .I '$$FIND1^DIC(8925.1,,"X",$P(STR,U)) D IMPORT($P(STR,U,3))
 .I '$$FIND1^DIC(44,,"X","MATERNITY TRACKER") D ADD44 ; FOR TEST ACCOUNTS
 .S IPT(19641.5,"?+1,",.01)=$P(STR,U)
 .S IPT(19641.5,"?+1,",.02)="MATERNITY TRACKER"
 .S IPT(19641.5,"?+1,",.03)=$P(STR,U,2)
 .D UPDATE^DIE("E","IPT") K IPT
 Q
 ;
ADD44 ; ADD MATERNITY TRACKER AS A HOSIPITAL LOCATION
 ; THIS IS ONLY INTENDED FOR USE WITH DEVELOPMENT
 N IPT
 S IPT(44,"+1,",.01)="MATERNITY TRACKER"
 S (IPT(44,"+1,",2),IPT(44,"+1,",2.1))="CLINIC"
 S IPT(44,"+1,",8)="TELEPHONE CASE MANAGEMENT"
 S IPT(44,"+1,",9)="MEDICINE"
 S IPT(44,"+1,",9.5)="TREATING SPECIALTY"
 D UPDATE^DIE(,"IPT") K IPT
 Q
 ;
TITLE ; DSIO TITLE CONFIGURATION (19641.5)
 ;;DASHBOARD CDA INCOMING^E^DSIO_TIU_TITLE_DASHBOARD_CDA_INCOMING.TXT
 ;;MCC DASHBOARD NOTE^E^DSIO_TIU_TITLE_MCC_DASHBOARD_NOTE.TXT
 ;;MCC PROVIDER CONTACT^T^DSIO_TIU_TITLE_MCC_PROVIDER_CONTACT.TXT
 ;;MD POSTPARTUM FOLLOWUP VISIT^E^DSIO_TIU_TITLE_MD_POSTPARTUM_FOLLOWUP_VISIT.TXT
 ;;OB FOLLOWUP NOTE^E^DSIO_TIU_TITLE_OB_FOLLOWUP_NOTE.TXT
 ;;OB H&P INITIAL^E^DSIO_TIU_TITLE_OB_H&P_INITIAL.TXT
 ;;NURSE POSTPARTUM NOTE^E^DSIO_TIU_TITLE_NURSE_POSTPARTUM_NOTE.TXT
 ;;PHONE CALL #1 (FIRST CONTACT)^T^DSIO_TIU_TITLE_PHONE_CALL_#1_(FIRST_CONTACT).TXT
 ;;PHONE CALL #2 (12 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_#2_(12_WEEKS).TXT
 ;;PHONE CALL #3 (20 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_#3_(20_WEEKS).TXT
 ;;PHONE CALL #4 (28 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_#4_(28_WEEKS).TXT
 ;;PHONE CALL #5 (36 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_#5_(36_WEEKS).TXT
 ;;PHONE CALL #6A (41 WEEKS NOT DELIVERED)^T^DSIO_TIU_TITLE_PHONE_CALL_#6A_(41_WEEKS_NOT_DELIVERED).TXT
 ;;PHONE CALL #6B (41 WEEKS DELIVERED) TOPICS^T^DSIO_TIU_TITLE_PHONE_CALL_#6B_(41_WEEKS_DELIVERED)_TOPICS.TXT
 ;;PHONE CALL #7 (6 WEEKS POSTPARTUM) TOPICS^T^DSIO_TIU_TITLE_PHONE_CALL_#7_(6_WEEKS_POSTPARTUM)_TOPICS.TXT
 ;;PHONE CALL - ADDITIONAL^T^DSIO_TIU_TITLE_PHONE_CALL_-_ADDITIONAL.TXT
 ;;
 Q
 ;
ASK ;
 N DIR,X,Y
 S DIR(0)="Y",DIR("A")="Would you like to import TIU Titles"
 S DIR("B")="YES" D ^DIR I +Y>0 D IMPORT
 Q
 ;
EXPORT ; COLLECT TITLES AND CREATE XML EXPORT
 ;
 ; BLD(85,69,3)=""
 ; TITLE, DOCUMENT CLASS, CLASS
 ;
 N TL,FLG,DC,CL,ERR,BLD,PATH,FILE,POP,CT
 F  Q:'$$GET
 Q:'$D(BLD)
 S PATH=$$ASKPATH S:PATH="" PATH=$$DEFDIR^%ZISH
 S CT=$NA(BLD) F  S CT=$Q(@CT) Q:CT=""  D
 .S FILE="DSIO_TIU_TITLE_"_$TR($$GET1^DIQ(8925.1,$QS(CT,1)_",",.01)," ","_")_".TXT"
 .;S FILE=FILE_"_EXPORT_"_$TR($$NOW^XLFDT,".","")_".TXT"
 .D OPEN^%ZISH("TIU",PATH,FILE,"W") Q:POP
 .U IO
 .; CLASS
 .W "<TIU_CLASS>",!
 .D GETFLDS(8925.1,$QS(CT,3),"LP",$O(^TIU(8925.1,$QS(CT,3),10,"B",$QS(CT,2),"")))
 .W "</TIU_CLASS>",!
 .; DOCUMENT CLASS
 .W "<TIU_DOCUMENT_CLASS>",!
 .D GETFLDS(8925.1,$QS(CT,2),"L",$O(^TIU(8925.1,$QS(CT,2),10,"B",$QS(CT,1),"")))
 .W "</TIU_DOCUMENT_CLASS>",!
 .; TITLE
 .W "<TIU_TITLE>",!
 .D GETFLDS(8925.1,$QS(CT,1))
 .W "</TIU_TITLE>",!
 .D CLOSE^%ZISH("TIU")
 Q
 ;
GETFLDS(FLE,IEN,TYP,CHILD) ; GET FIELDS AND VALUES
 N OUT,ERR,CT,FLG,SFLE,FLD
 D GETS^DIQ(FLE,IEN_",","**","E","OUT","ERR") Q:'$D(OUT)
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .I $G(TYP)["L",$QS(CT,1)=8925.14,$P($QS(CT,2),",")'=CHILD Q
 .I $G(TYP)["P",$QS(CT,1)'=FLE Q
 .I $G(TYP)["P",$QS(CT,1)=FLE,$QS(CT,3)'=.01 Q
 .I $QS(CT,1)'=FLE,$D(FLG) D
 ..; DIFFERENT SUB FILE OR DIFFERENT SUB FILE ENTRY
 ..I $QS(CT,1)'=$P(FLG,U)!($QS(CT,2)'=$P(FLG,U,2)) K FLG D  Q
 ...W "</"_$TR($O(^DD(SFLE,0,"NM",""))," ","_")_">",!
 .I $QS(CT,1)'=FLE,'$D(FLG) D
 ..S SFLE=$QS(CT,1),FLG=$QS(CT,1)_U_$QS(CT,2)_U_$QS(CT,3)
 ..W "<"_$TR($O(^DD(SFLE,0,"NM",""))," ","_")_" NAME='"
 ..W $O(^DD(SFLE,0,"NM",""))_"' SUB-FILE='"_SFLE_"'>",!
 .S FLD=$$GET1^DID($QS(CT,1),$QS(CT,3),,"LABEL") I @CT'="" D
 ..Q:$$GET1^DID($QS(CT,1),FLD,,"TYPE")="COMPUTED"
 ..W "<"_$TR(FLD," ","_")_" NAME='"_FLD_"' NUMBER='"_$QS(CT,3)_"'>",@CT
 ..W "</"_$TR(FLD," ","_")_">",!
 I $D(FLG) W "</"_$TR($O(^DD(SFLE,0,"NM",""))," ","_")_">",!
 Q
 ;
GET() ; GET TITLES
 N DIC,DA,X,Y W !
 S:$D(TL) DIC("A")="ANOTHER: "
 S DIC("S")="I $P(^(0),U,4)=""DOC""" ;ONLY TITLES
 S DIC="^TIU(8925.1,",DIC(0)="AEOQ" D ^DIC
 I +Y>0 D DCLGET(+Y) Q 1
 ;I $$GET1^DIQ(8925.1,+Y_",",3.02,"I") D DCLGET(+Y) Q
 ;W !!,"**Not okay to distribute!**"
 Q 0
 ;
SETUP(VAR) ; SET UP THE DC AND CL DIRECTORY TO FIND OWNERS
 N LIST,CT,SUB,EX
 D LIST^DIC(8925.1,,"@","P",,,,,"I $P(^(0),U,4)="""_VAR_"""",,"LIST","ERR")
 K LIST("DILIST",0) Q:'$D(LIST)
 S CT=$NA(LIST) F  S CT=$Q(@CT) Q:CT=""  D
 .K SUB D LIST^DIC(8925.14,","_@CT_",","@;.01I","P",,,,,,,"SUB","ERR")
 .K SUB("DILIST",0) I $D(SUB) S EX="M "_VAR_"(@CT)=SUB" X EX
 Q
 ;
DCLGET(IEN) ; GET DOCUMENT CLASS(DC) OF TITLE(TL) OR CLASS(CL) OF DC
 I '$D(FLG) N I F I="DC","CL" D SETUP(I) S FLG=1
 N DCT,CCT,DCIEN,CLIEN
 S DCT=$NA(DC) F  S DCT=$Q(@DCT) Q:DCT=""  D
 .I $P(@DCT,U,2)=IEN S DCIEN=$QS(DCT,1) D
 ..S CCT=$NA(CL) F  S CCT=$Q(@CCT) Q:CCT=""  D
 ...I $P(@CCT,U,2)=DCIEN S BLD(IEN,DCIEN,$QS(CCT,1))=""
 Q
 ;
IMPORT(FNAME) ; IMPORT XML LIKE TIU TITLE TRANSPORT
 Q:$G(FNAME)=""
 N DIR,X,Y,PATH,SPC,LIST,FILE,POP,RI,LN,TST,TYP,SUB,FLGB,FLGS
 N BLD,BSUB,IPT,CL,DCL,TITLE
 ;S DIR(0)="Y",DIR("A")="Delete the MATERNITY TRACKER Document class"
 ;S DIR("B")="YES" D ^DIR I +Y>0 D DELETE
 ;S (POP,RI)=0,PATH=$$ASKPATH S:PATH="" PATH=$$DEFDIR^%ZISH
 S PATH=$$DEFDIR^%ZISH_"TITLES\"
 ;S FILE=$$ASKFILE Q:FILE=""
 S SPC(FNAME)="" Q:'$$LIST^%ZISH(PATH,"SPC","LIST")
 S FILE="" F  S FILE=$O(LIST(FILE)) Q:FILE=""  D
 .K SUB,FLGB,FLGS,BSUB,CL,DCL
 .D OPEN^%ZISH("DSIO",PATH,FILE,"R") Q:POP
 .D USE^%ZISUTL("DSIO") F RI=+$G(RI):1 R LN:DTIME Q:$$STATUS^%ZISH  D
 ..;D DOTS
 ..F TST="<TIU_TITLE>","<TIU_DOCUMENT_CLASS>","<TIU_CLASS>" I TST=LN D  Q
 ...S TYP=$TR(LN,"<>","") K SUB,FLGB,FLGS,BSUB
 ..F TST="</TIU_TITLE>","</TIU_DOCUMENT_CLASS>","</TIU_CLASS>" I TST=LN D  Q
 ...D SBLD
 ..D PARSE
 .D CLOSE^%ZISH("DSIO")
 Q
 ;
PARSE ;
 Q:'$D(TYP)
 N FLD I LN["</"&(LN["NUMBER='") D
 .S FLD=$P($P(LN,"NUMBER=",2),"'",2)
 .I $D(SUB),$$VFIELD^DILFD($P(SUB,U),FLD) D  S FLGS=1 Q
 ..S BSUB=$S('$D(FLGS):"S ",1:$G(BSUB)_",")_"IPT("_$P(SUB,U)_","_"""?+1,"""_"_IEN_"","""_","_FLD_")="""_$P($P(LN,">",2),"<")_""""
 .Q:'$$VFIELD^DILFD(8925.1,FLD)
 .I TYP="TIU_CLASS"&(FLD=.01)&('$D(CL)) D
 ..S CL=$$FIND1^DIC(8925.1,,"X",$P($P(LN,">",2),"<"))_U_$P($P(LN,">",2),"<")
 .I TYP="TIU_DOCUMENT_CLASS"&(FLD=.01)&('$D(DCL)) D
 ..S DCL=$$FIND1^DIC(8925.1,,"X",$P($P(LN,">",2),"<"))_U_$P($P(LN,">",2),"<")
 .Q:TYP="TIU_CLASS"
 .Q:TYP="TIU_DOCUMENT_CLASS"&(+$G(DCL)>0)
 .I TYP="TIU_TITLE"&(FLD=.01) S TITLE=$P($P(LN,">",2),"<")
 .S BLD=$S('$D(FLGB):"S ",1:$G(BLD)_",")_"IPT(8925.1,IEN_"","""_","_FLD_")="""_$P($P(LN,">",2),"<")_""""
 .S FLGB=1
 I LN["SUB-FILE='" S SUB=$P($P(LN,"SUB-FILE=",2),"'",2)_U_$P($P(LN,"=",2),"'",2) Q
 I $D(SUB) I LN="</"_$P(SUB,U,2)_">" K SUB
 Q
 ;
SBLD ;
 N TYPE,OWNER,VAR,I,NAME,STAT,OUSER
 Q:TYP="TIU_CLASS"&(+$G(CL)>0)
 Q:TYP="TIU_DOCUMENT_CLASS"&(+$G(DCL)>0)
 S TYPE=$S(TYP="TIU_DOCUMENT_CLASS":"DC",TYP="TIU_TITLE":"DOC",1:"") Q:TYPE=""
 S OWNER=$S(TYP="TIU_DOCUMENT_CLASS":+$G(CL),TYP="TIU_TITLE":+$G(DCL),1:"") Q:OWNER=""
 I OWNER=0&(TYP'="TIU_CLASS") D
 .S VAR=$S(TYP="TIU_DOCUMENT_CLASS":"CL",TYP="TIU_TITLE":"DCL")
 .S $P(@VAR,U)=$$FIND1^DIC(8925.1,,"X",$P(@VAR,U,2))
 I $D(BLD) F I=1:1:$L(BLD,",") I $P(BLD,",",I)[")=" D
 .I $P($P(BLD,",",I),")=")=.01 S NAME=$TR($P($P(BLD,",",I),")=",2),"""","")
 .I $P($P(BLD,",",I),")=")=.07 S STAT=$TR($P($P(BLD,",",I),")=",2),"""","")
 .I $P($P(BLD,",",I),")=")=.06 S OUSER=$TR($P($P(BLD,",",I),")=",2),"""","")
 D BUILD(NAME,TYPE,STAT,OUSER,OWNER)
 Q
 ;
BUILD(NAME,TYPE,STAT,OUSER,OWNER) ; BUILD TIU ENTITY AND ADD TO OWNER
 ;
 ; INTERNAL
 ; TYPE : DOC (TITLE), DC (DOCUMENT CLASS) (ALL OTHERS REJECTED)
 ;
 N IIPT,IEN,IENS Q:"DOC,DC"'[$G(TYPE)!($G(NAME)="")
 ; ADD TITLE (CREATE ENTRY), DO EXTERNAL VALIDATION
 S IIPT(8925.1,"?+1,",.01)=NAME
 S IIPT(8925.1,"?+1,",.06)=$G(OUSER,"CLINICAL COORDINATOR")
 D UPDATE^DIE("E","IIPT","IEN") K IIPT Q:$G(IEN(1))=""
 I TYP="TIU_CLASS"&(+$G(CL)<1) S $P(CL,U)=IEN(1)
 I TYP="TIU_DOCUMENT_CLASS"&(+$G(DCL)<1) S $P(DCL,U)=IEN(1)
 S IIPT(8925.1,IEN(1)_",",.03)=NAME                    ; .03 = PRINT NAME
 S IIPT(8925.1,IEN(1)_",",.04)=TYPE
 S IIPT(8925.1,IEN(1)_",",.07)=$$FIND1^DIC(8925.6,,"X","INACTIVE")
 D UPDATE^DIE(,"IIPT") K IIPT
 ; ADD ENTRY TO OWNER MULTIPLE
 S IIPT(8925.14,"?+1,"_OWNER_",",.01)=IEN(1)
 S IIPT(8925.14,"?+1,"_OWNER_",",4)=$$MIXED^TIULS(NAME)
 D UPDATE^DIE(,"IIPT","IENS") K IIPT
 S STAT=$$FIND1^DIC(8925.6,,"X",STAT)
 S:+STAT>0 IIPT(8925.1,IEN(1)_",",.07)=STAT
 D:$D(IIPT) UPDATE^DIE(,"IIPT")
 Q
 ;
DELETE ; NOT REUSABLE - SETUP FOR PROGRESS NOTES - MATERNITY TRACKER - TITLES
 Q  ; DON'T DO THIS UNLESS THERE ARE NO ENTRIES ASSOCIATED WITH THE OLD TITLES
 ;    AND IF THERE ARE THEN PREPARE TO REFERENCE THEM
 N IEN,OUT,ERR,IENS,TITLE,IPT,CLIEN,DCL
 S IEN=$$FIND1^DIC(8925.1,,"X","MATERNITY TRACKER") Q:IEN<1
 S IENS=0 F  S IENS=$O(^TIU(8925.1,IEN,10,IENS)) Q:'IENS  D
 .S TITLE=+^TIU(8925.1,IEN,10,IENS,0) Q:TITLE<1
 .S IPT(8925.1,TITLE_",",.01)="" D UPDATE^DIE(,"IPT","ERR") K IPT
 .I '$D(ERR) S IPT(8925.14,IENS_","_IEN_",",.01)="" D UPDATE^DIE(,"IPT") K IPT
 .K ERR
 S CLIEN=$$FIND1^DIC(8925.1,,"X","PROGRESS NOTES") Q:CLIEN<1
 S IENS=0 F  S IENS=$O(^TIU(8925.1,CLIEN,10,IENS)) Q:'IENS  D
 .S DCL=+^TIU(8925.1,CLIEN,10,IENS,0) Q:DCL<1
 .I '$D(^TIU(8925.1,DCL)) D
 ..S IPT(8925.14,IENS_","_CLIEN_",",.01)="" D UPDATE^DIE(,"IPT") K IPT
 S IPT(8925.1,IEN_",",.01)="" D UPDATE^DIE(,"IPT")
 Q
 ;
 ; --------------------------------- UTILITIES --------------------------------
 ;
DOTS(MD) ; WRITE DOTS TO THE SCREEN
 D SWITCH("|TNT|") W $S($D(MD):MD,1:".") D SWITCH("DSIO")
 Q
 ;
SWITCH(TAG) ; SWITCH THE DEVICE AND RETURN THE CURRENT
 N IEN,NEW
 I TAG["|TNT|" S NEW=$P(IO("HOME"),U,2) D HOME^%ZIS Q NEW
 K IEN S IEN=$O(^TMP("XUDEVICE",$J,"B",TAG,"")) Q:IEN=""
 S IO=$G(^TMP("XUDEVICE",$J,IEN,"IO")) D USE^%ZISUTL(TAG)
 Q
 ;
ASKPATH() ; FUNCTION TO GET THE HFS PATH
 N DIR,DIRUT,X,Y,SL
 S DIR("A")="Set PATH",DIR(0)="FOr^1:255" D ^DIR
 I $D(DIRUT) Q ""
 F SL="/","\" I Y[SL&($E(Y,$L(Y))'[SL) S Y=Y_SL
 Q Y
 ;
ASKFILE() ; FM DIC TO GET THE FILENAME FROM THE USER
 N DIR,DIRUT,X,Y
 W ! S DIR("A")="Filename"
 S DIR(0)="FrO^1:100" D ^DIR
 I $D(DIRUT) Q ""
 Q Y
