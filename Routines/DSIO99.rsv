Routine DSIO99 saved using VFDXTRS routine on Oct 09, 2015 10:03
DSIO99^INT^63722,50356^Jun 19, 2015@13:59
DSIO99 ;DSS/TFF - DSIO IMPORT/EXPORT TIU TITLES;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**1**;Feb 06, 2015;Build 3
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ; $$MIXED^TIULS
 ;
 ;
 ;
 ; CLINICAL DOCUMENTS (CL)
 ;  +PROGRESS NOTES (CL)
 ;   +MATERNITY TRACKER (DC) - OR OTHER DOCUMENT CLASS
 ;    TITLE <- SELECT AT THIS LEVEL
 ;
 D UPDATE,FIX
 Q
 ;
UPDATE ; Start Auto Configuration
 N PATH,I,STR,TITLE,CAT,FILE,IEN,DIK
 S PATH=$$PATH($$DEFDIR^%ZISH)
 F I=1:1 S STR=$P($T(TITLE+I),";;",2) Q:STR=""  D
 . S TITLE=$P(STR,U),CAT=$P(STR,U,2),FILE=$P(STR,U,3)
 . S IEN=$$IMPORT(TITLE,PATH,FILE) Q:'IEN
 . D CONFIG(IEN,CAT),CONTROL(IEN)
 S DIK="^DSIO(19641.4," D IXALL2^DIK   ; REINDEX KILL
 S DIK="^DSIO(19641.4," D IXALL^DIK    ; REINDEX SET
 Q
 ;
TITLE ; DSIO TIU TITLES
 ;;DASHBOARD CDA INCOMING^E^DSIO_TIU_TITLE_DASHBOARD_CDA_INCOMING.TXT
 ;;MCC DASHBOARD NOTE^E^DSIO_TIU_TITLE_MCC_DASHBOARD_NOTE.TXT
 ;;MCC PROVIDER CONTACT^T^DSIO_TIU_TITLE_MCC_PROVIDER_CONTACT.TXT
 ;;MD POSTPARTUM FOLLOWUP VISIT^E^DSIO_TIU_TITLE_MD_POSTPARTUM_FOLLOWUP.TXT
 ;;NURSE POSTPARTUM NOTE^E^DSIO_TIU_TITLE_NURSE_POSTPARTUM_NOTE.TXT
 ;;OB FOLLOWUP NOTE^E^DSIO_TIU_TITLE_OB_FOLLOWUP_NOTE.TXT
 ;;OB H&P INITIAL^E^DSIO_TIU_TITLE_OB_HP_INITIAL.TXT
 ;;PHONE CALL #1 (FIRST CONTACT)^T^DSIO_TIU_TITLE_PHONE_CALL_1.TXT
 ;;PHONE CALL #2 (12 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_2.TXT
 ;;PHONE CALL #3 (20 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_3.TXT
 ;;PHONE CALL #4 (28 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_4.TXT
 ;;PHONE CALL #5 (36 WEEKS)^T^DSIO_TIU_TITLE_PHONE_CALL_5.TXT
 ;;PHONE CALL #6A (41 WEEKS NOT DELIVERED)^T^DSIO_TIU_TITLE_PHONE_CALL_6A.TXT
 ;;PHONE CALL #6B (41 WEEKS DELIVERED) TOPICS^T^DSIO_TIU_TITLE_PHONE_CALL_6B.TXT
 ;;PHONE CALL #7 (6 WEEKS POSTPARTUM) TOPICS^T^DSIO_TIU_TITLE_PHONE_CALL_7.TXT
 ;;PHONE CALL - ADDITIONAL^T^DSIO_TIU_TITLE_PHONE_CALL_ADDITIONAL.TXT
 ;;
 Q
 ;
IMPORT(TITLE,PATH,FILE) ; Import XML TIU TITLE TRANSPORT
 N IEN,POP,RI,LN,TST,TYP,SUB,FLGB,FLGS,BLD,BSUB,IPT,CL,DCL
 S IEN=$O(^TIU(8925.1,"B",TITLE,"")) Q:IEN IEN
 Q:$G(PATH)="" ""  D OPEN^%ZISH("DSIO",PATH,FILE,"R")
 I POP W !,"*** Failed to open file - "_FILE,! Q ""
 D USE^%ZISUTL("DSIO")
 F RI=1:1 R LN:DTIME Q:$$STATUS^%ZISH  D
 . D DOTS(,"DSIO")
 . I "^<TIU_TITLE>^<TIU_DOCUMENT_CLASS>^<TIU_CLASS>^"[(U_LN_U) D  Q
 . . S TYP=$TR(LN,"<>") K SUB,FLGB,FLGS,BSUB
 . I "^</TIU_TITLE>^</TIU_DOCUMENT_CLASS>^</TIU_CLASS>^"[(U_LN_U) D  Q
 . . D SBLD
 . D PARSE
 D CLOSE^%ZISH("DSIO")
 Q $O(^TIU(8925.1,"B",TITLE,""))
 ;
SBLD ; Continue
 N TYPE,OWNER,VAR,I,NAME,STAT,OUSER
 Q:TYP="TIU_CLASS"&(+$G(CL)>0)
 Q:TYP="TIU_DOCUMENT_CLASS"&(+$G(DCL)>0)
 S TYPE=$S(TYP="TIU_DOCUMENT_CLASS":"DC",TYP="TIU_TITLE":"DOC",1:"") Q:TYPE=""
 S OWNER=$S(TYP="TIU_DOCUMENT_CLASS":+$G(CL),TYP="TIU_TITLE":+$G(DCL),1:"") Q:OWNER=""
 I OWNER=0&(TYP'="TIU_CLASS") D
 . S VAR=$S(TYP="TIU_DOCUMENT_CLASS":"CL",TYP="TIU_TITLE":"DCL")
 . S $P(@VAR,U)=$$FIND1^DIC(8925.1,,"X",$P(@VAR,U,2))
 I $D(BLD) F I=1:1:$L(BLD,",") I $P(BLD,",",I)[")=" D
 . I $P($P(BLD,",",I),")=")=.01 S NAME=$TR($P($P(BLD,",",I),")=",2),"""","")
 . I $P($P(BLD,",",I),")=")=.07 S STAT=$TR($P($P(BLD,",",I),")=",2),"""","")
 . I $P($P(BLD,",",I),")=")=.06 S OUSER=$TR($P($P(BLD,",",I),")=",2),"""","")
 D BUILD(NAME,TYPE,STAT,OUSER,OWNER)
 Q
 ;
BUILD(NAME,TYPE,STAT,OUSER,OWNER) ; Build TIU Entity and add to Owner
 ;
 ; INTERNAL
 ; TYPE : DOC (TITLE), DC (DOCUMENT CLASS) (ALL OTHERS REJECTED)
 ;
 N IPT,IEN,IENS
 Q:"^DOC^DC^"'[(U_$G(TYPE)_U)!($G(NAME)="")
 ; *** ADD TITLE (CREATE ENTRY), DO EXTERNAL VALIDATION
 S IPT(8925.1,"?+1,",.01)=NAME
 S IPT(8925.1,"?+1,",.06)=$G(OUSER,"CLINICAL COORDINATOR")
 D UPDATE^DIE("E","IPT","IEN") K IPT Q:'$G(IEN(1))
 I TYP="TIU_CLASS"&(+$G(CL)<1) S $P(CL,U)=IEN(1)
 I TYP="TIU_DOCUMENT_CLASS"&(+$G(DCL)<1) S $P(DCL,U)=IEN(1)
 S IPT(8925.1,IEN(1)_",",.03)=NAME                    ; .03 = PRINT NAME
 S IPT(8925.1,IEN(1)_",",.04)=TYPE
 S IPT(8925.1,IEN(1)_",",.07)=$$FIND1^DIC(8925.6,,"X","INACTIVE")
 D UPDATE^DIE(,"IPT") K IPT
 ; ADD ENTRY TO OWNER MULTIPLE
 S IPT(8925.14,"?+1,"_OWNER_",",.01)=IEN(1)
 S IPT(8925.14,"?+1,"_OWNER_",",4)=$$MIXED^TIULS(NAME)
 D UPDATE^DIE(,"IPT","IENS") K IPT
 S STAT=$$FIND1^DIC(8925.6,,"X",STAT)
 I STAT S IPT(8925.1,IEN(1)_",",.07)=STAT D UPDATE^DIE(,"IPT")
 Q
 ;
PARSE ; Continue
 Q:'$D(TYP)
 N FLD I LN["</"&(LN["NUMBER='") D
 . S FLD=$P($P(LN,"NUMBER=",2),"'",2)
 . I $D(SUB),$$VFIELD^DILFD($P(SUB,U),FLD) D  S FLGS=1 Q
 . . S BSUB=$S('$D(FLGS):"S ",1:$G(BSUB)_",")_"IPT("_$P(SUB,U)_","_"""?+1,"""_"_IEN_"","""_","_FLD_")="""_$P($P(LN,">",2),"<")_""""
 . Q:'$$VFIELD^DILFD(8925.1,FLD)
 . I TYP="TIU_CLASS"&(FLD=.01)&('$D(CL)) D
 . . S CL=$$FIND1^DIC(8925.1,,"X",$P($P(LN,">",2),"<"))_U_$P($P(LN,">",2),"<")
 . I TYP="TIU_DOCUMENT_CLASS"&(FLD=.01)&('$D(DCL)) D
 . . S DCL=$$FIND1^DIC(8925.1,,"X",$P($P(LN,">",2),"<"))_U_$P($P(LN,">",2),"<")
 . Q:TYP="TIU_CLASS"
 . Q:TYP="TIU_DOCUMENT_CLASS"&(+$G(DCL)>0)
 . I TYP="TIU_TITLE"&(FLD=.01) S TITLE=$P($P(LN,">",2),"<")
 . S BLD=$S('$D(FLGB):"S ",1:$G(BLD)_",")_"IPT(8925.1,IEN_"","""_","_FLD_")="""_$P($P(LN,">",2),"<")_""""
 . S FLGB=1
 I LN["SUB-FILE='" S SUB=$P($P(LN,"SUB-FILE=",2),"'",2)_U_$P($P(LN,"=",2),"'",2) Q
 I $D(SUB) I LN="</"_$P(SUB,U,2)_">" K SUB
 Q
 ;
CONFIG(IEN,CAT) ; Add/Edit DSIO TITLE CONFIGURATION (19641.5)
 N DIC,DIE,DA,X,Y,DIRUT,LOC
 S LOC=$$FIND1^DIC(44,,"X","MATERNITY TRACKER") S:'LOC LOC=$$ADD44 Q:'LOC
 S (DIC,DIE)="^DSIO(19641.5,",DIC(0)="XL",X="`"_IEN D ^DIC Q:+Y<1
 S DA=IEN,DR=".02////"_LOC_";.03////"_CAT D ^DIE
 Q
 ;
ADD44 ; Add MATERNITY TRACKER as a HOSIPITAL LOCATION
 N FDA,IEN
 S FDA(44,"?+1,",.01)="MATERNITY TRACKER"
 S FDA(44,"?+1,",2.1)="CLINIC"
 S FDA(44,"?+1,",9.5)="TREATING SPECIALTY"
 S FDA(44,"?+1,",2)="CLINIC"
 S FDA(44,"?+1,",8)="TELEPHONE CASE MANAGEMENT"
 S FDA(44,"?+1,",9)="MEDICINE"
 D UPDATE^DIE("E","FDA","IEN")
 Q $G(IEN(1))
 ;
CONTROL(IEN) ; Populate the DSIO DDCS CONTROL file (19641.4)
 N FDA
 S FDA(19641.4,"?+1,",.01)=IEN_";TIU(8925.1,"
 D UPDATE^DIE(,"FDA")
 Q
 ;
 ; --------------------------------- MANUAL -----------------------------------
 ;
FIX ; Correct the oCNT Configuration (19641.48)
 N IEN,ND,FLG,OUT,CT,XND,IPT
 S ^DSIO(19641.48,3,1,29,5,0)="^19641.4815IP^2^2"
 S ^DSIO(19641.48,3,1,29,5,1,0)="1^MEMOPRENATAL^"_$$FIND1^DIC(8925.1,,"X","OB H&P INITIAL")_"^^1"
 S ^DSIO(19641.48,3,1,29,5,2,0)="3^MEMOPRENATAL^"_$$FIND1^DIC(8925.1,,"X","OB FOLLOWUP NOTE")_"^^0"
 S ^DSIO(19641.48,3,1,29,5,"AB",1,1)=""
 S ^DSIO(19641.48,3,1,29,5,"AB",3,2)=""
 S ^DSIO(19641.48,3,1,29,5,"B",1,1)=""
 S ^DSIO(19641.48,3,1,29,5,"B",3,2)=""
 S ^DSIO(19641.48,3,1,29,5,"P",0,2)=""
 S ^DSIO(19641.48,3,1,29,5,"P",1,1)=""
 S ^DSIO(19641.48,3,1,31,5,0)="^19641.4815IP^2^2"
 S ^DSIO(19641.48,3,1,31,5,1,0)="1^CKLSTPROBLEMS^"_$$FIND1^DIC(8925.1,,"X","OB H&P INITIAL")_"^^1"
 S ^DSIO(19641.48,3,1,31,5,2,0)="3^CKLSTPROBLEMS^"_$$FIND1^DIC(8925.1,,"X","OB FOLLOWUP NOTE")_"^^0"
 S ^DSIO(19641.48,3,1,31,5,"AB",1,1)=""
 S ^DSIO(19641.48,3,1,31,5,"AB",3,2)=""
 S ^DSIO(19641.48,3,1,31,5,"B",1,1)=""
 S ^DSIO(19641.48,3,1,31,5,"B",3,2)=""
 S ^DSIO(19641.48,3,1,31,5,"P",0,2)=""
 S ^DSIO(19641.48,3,1,31,5,"P",1,1)=""
 S ^DSIO(19641.48,3,1,32,5,0)="^19641.4815IP^2^2"
 S ^DSIO(19641.48,3,1,32,5,1,0)="1^CKLSTEDUCATION^"_$$FIND1^DIC(8925.1,,"X","OB H&P INITIAL")_"^^1"
 S ^DSIO(19641.48,3,1,32,5,2,0)="3^CKLSTEDUCATION^"_$$FIND1^DIC(8925.1,,"X","OB FOLLOWUP NOTE")_"^^0"
 S ^DSIO(19641.48,3,1,32,5,"AB",1,1)=""
 S ^DSIO(19641.48,3,1,32,5,"AB",3,2)=""
 S ^DSIO(19641.48,3,1,32,5,"B",1,1)=""
 S ^DSIO(19641.48,3,1,32,5,"B",3,2)=""
 S ^DSIO(19641.48,3,1,32,5,"P",0,2)=""
 S ^DSIO(19641.48,3,1,32,5,"P",1,1)=""
 ; *** UPDATE 'GET DATA FROM'
 S IEN=$$FIND1^DIC(19641.48,,"X","oCNT_OBFollowUp.dll") Q:'IEN
 S ND="$S(FLG=""OCNT_OBHP.DLL"":$$FIND1^DIC(8925.1,,""X"",""OB H&P INITIAL""),"
 S ND=ND_"FLG=""OCNT_OBFOLLOWUP.DLL"":$$FIND1^DIC(8925.1,,""X"",""OB FOLLOWUP NOTE""),1:"""")"
 D GETS^DIQ(19641.48,IEN_",","1*","EN","OUT")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 . I $QS(CT,1)=19641.4815 D
 . . I $QS(CT,3)=.01 S FLG=$$UP^XLFSTR(@CT) Q
 . . I $QS(CT,3)=.03,$S(FLG="OCNT_OBHP.DLL"&(@CT="OB H&P INITIAL"):0,FLG="OCNT_OBFOLLOWUP.DLL"&(@CT="OB FOLLOWUP NOTE"):0,1:1) D
 . . . S XND="S IPT(19641.4815,"""_$QS(CT,2)_""",.03)="_ND X XND
 . . . D FILE^DIE(,"IPT") K IPT
 Q
 ;
 ; -------------------------------- UTILITIES ---------------------------------
 ;
PATH(PATH) ; Default Path
 N DIR,X,Y,DIRUT
 S:$G(PATH)="" PATH=$S($$OS^%ZOSV["NT":"C:\HFS\",$$OS^%ZOSV["UNIX":"/hfs/")
 S DIR(0)="F^1:255",DIR("A")="Enter directory name or path",DIR("B")=PATH
 D ^DIR Q:$D(DIRUT) ""
 Q Y
 ;
DOTS(MSG,TAG) ; Write Dots to the Screen
 N WR D SWITCH("|TNT|") S WR="W "_$S($D(MSG):"!,MSG,!",1:""".""")
 X WR D SWITCH($G(TAG,"PARSE"))
 Q
 ;
SWITCH(TAG,FLG) ; Switch the Device and Return the Current
 N IEN,NEW
 I '$G(FLG),TAG["|TNT|" S NEW=$P(IO("HOME"),U,2) D HOME^%ZIS Q
 K IEN S IEN=$O(^TMP("XUDEVICE",$J,"B",TAG,"")) Q:IEN=""
 S IO=$G(^TMP("XUDEVICE",$J,IEN,"IO")) D USE^%ZISUTL(TAG)
 Q
 ;
 ; ---------------------------------- EXPORT ----------------------------------
 ;
EXPORT ; Collect TITLES and create XML EXPORT
 ;
 ; BLD(85,69,3)=""
 ; TITLE, DOCUMENT CLASS, CLASS
 ;
 N TL,FLG,DC,CL,ERR,BLD,PATH,FILE,POP,CT
 F  Q:'$$GET
 Q:'$D(BLD)
 S PATH=$$PATH($$DEFDIR^%ZISH)
 S CT=$NA(BLD) F  S CT=$Q(@CT) Q:CT=""  D
 .S FILE="DSIO_TIU_TITLE_"_$TR($$GET1^DIQ(8925.1,$QS(CT,1)_",",.01)," ","_")_".TXT"
 .;S FILE=FILE_"_EXPORT_"_$TR($$NOW^XLFDT,".","")_".TXT"
 .D OPEN^%ZISH("TIU",PATH,FILE,"W") Q:POP
 .U IO
 .; CLASS
 .W "<TIU_CLASS>",!
 .D GETFLDS(8925.1,$QS(CT,3),"LP",$O(^TIU(8925.1,$QS(CT,3),10,"B",$QS(CT,2),"")))
 .W "</TIU_CLASS>",!
 .; DOCUMENT CLASS
 .W "<TIU_DOCUMENT_CLASS>",!
 .D GETFLDS(8925.1,$QS(CT,2),"L",$O(^TIU(8925.1,$QS(CT,2),10,"B",$QS(CT,1),"")))
 .W "</TIU_DOCUMENT_CLASS>",!
 .; TITLE
 .W "<TIU_TITLE>",!
 .D GETFLDS(8925.1,$QS(CT,1))
 .W "</TIU_TITLE>",!
 .D CLOSE^%ZISH("TIU")
 Q
 ;
GETFLDS(FLE,IEN,TYP,CHILD) ; GET FIELDS AND VALUES
 N OUT,ERR,CT,FLG,SFLE,FLD
 D GETS^DIQ(FLE,IEN_",","**","E","OUT","ERR") Q:'$D(OUT)
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .I $G(TYP)["L",$QS(CT,1)=8925.14,$P($QS(CT,2),",")'=CHILD Q
 .I $G(TYP)["P",$QS(CT,1)'=FLE Q
 .I $G(TYP)["P",$QS(CT,1)=FLE,$QS(CT,3)'=.01 Q
 .I $QS(CT,1)'=FLE,$D(FLG) D
 ..; DIFFERENT SUB FILE OR DIFFERENT SUB FILE ENTRY
 ..I $QS(CT,1)'=$P(FLG,U)!($QS(CT,2)'=$P(FLG,U,2)) K FLG D  Q
 ...W "</"_$TR($O(^DD(SFLE,0,"NM",""))," ","_")_">",!
 .I $QS(CT,1)'=FLE,'$D(FLG) D
 ..S SFLE=$QS(CT,1),FLG=$QS(CT,1)_U_$QS(CT,2)_U_$QS(CT,3)
 ..W "<"_$TR($O(^DD(SFLE,0,"NM",""))," ","_")_" NAME='"
 ..W $O(^DD(SFLE,0,"NM",""))_"' SUB-FILE='"_SFLE_"'>",!
 .S FLD=$$GET1^DID($QS(CT,1),$QS(CT,3),,"LABEL") I @CT'="" D
 ..Q:$$GET1^DID($QS(CT,1),FLD,,"TYPE")="COMPUTED"
 ..W "<"_$TR(FLD," ","_")_" NAME='"_FLD_"' NUMBER='"_$QS(CT,3)_"'>",@CT
 ..W "</"_$TR(FLD," ","_")_">",!
 I $D(FLG) W "</"_$TR($O(^DD(SFLE,0,"NM",""))," ","_")_">",!
 Q
 ;
GET() ; GET TITLES
 N DIC,DA,X,Y W !
 S:$D(TL) DIC("A")="ANOTHER: "
 S DIC("S")="I $P(^(0),U,4)=""DOC""" ;ONLY TITLES
 S DIC="^TIU(8925.1,",DIC(0)="AEOQ" D ^DIC
 I +Y>0 D DCLGET(+Y) Q 1
 ;I $$GET1^DIQ(8925.1,+Y_",",3.02,"I") D DCLGET(+Y) Q
 ;W !!,"**Not okay to distribute!**"
 Q 0
 ;
SETUP(VAR) ; SET UP THE DC AND CL DIRECTORY TO FIND OWNERS
 N LIST,CT,SUB,EX
 D LIST^DIC(8925.1,,"@","P",,,,,"I $P(^(0),U,4)="""_VAR_"""",,"LIST","ERR")
 K LIST("DILIST",0) Q:'$D(LIST)
 S CT=$NA(LIST) F  S CT=$Q(@CT) Q:CT=""  D
 .K SUB D LIST^DIC(8925.14,","_@CT_",","@;.01I","P",,,,,,,"SUB","ERR")
 .K SUB("DILIST",0) I $D(SUB) S EX="M "_VAR_"(@CT)=SUB" X EX
 Q
 ;
DCLGET(IEN) ; GET DOCUMENT CLASS(DC) OF TITLE(TL) OR CLASS(CL) OF DC
 I '$D(FLG) N I F I="DC","CL" D SETUP(I) S FLG=1
 N DCT,CCT,DCIEN,CLIEN
 S DCT=$NA(DC) F  S DCT=$Q(@DCT) Q:DCT=""  D
 .I $P(@DCT,U,2)=IEN S DCIEN=$QS(DCT,1) D
 ..S CCT=$NA(CL) F  S CCT=$Q(@CCT) Q:CCT=""  D
 ...I $P(@CCT,U,2)=DCIEN S BLD(IEN,DCIEN,$QS(CCT,1))=""
 Q
