Routine DSIO98 saved using VFDXTRS routine on May 20, 2015 09:21
DSIO98^INT^63692,31238^May 20, 2015@08:40
DSIO98 ;DSS/TFF - DSIO PUSH AND LOG;06/28/2013 15:19
 ;;1.0;DSIO 1.0;;Feb 06, 2015;Build 23
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
HISTORY(HIS,FLE,DFN,FLD,HVAL,VAL,RET) ; HISTORY LOG
 ;
 ;  HIS = 19641.124 : DSIO OBSERVATIONS PUSH HISTORY
 ;  FLE = DESTINATION FILE
 ;  FLD = DESTINATION FIELD
 ; HVAL = HISTORY LOG TRIGGER
 ;         FOR OBSERVATIONS = CODE
 ;                 FOR DDCS = TIU DOCUMENT IEN
 ;  DFN = PATIENT
 ;  VAL = NEW VALUE (GLOBAL REFERENCE)
 ;
 Q:$G(DFN)=""!($G(FLE)="")!($G(FLD)="")!($G(HVAL)="")!($G(VAL)="")
 N IPT,IEN,DLAYGO S DLAYGO=HIS
 S IPT(HIS,"+1,",.01)=$$NOW^XLFDT
 S IPT(HIS,"+1,",.02)=FLE
 S IPT(HIS,"+1,",.03)=FLD
 S IPT(HIS,"+1,",.04)=HVAL
 S IPT(HIS,"+1,",.05)=DUZ
 S IPT(HIS,"+1,",.06)=DFN
 D UPDATE^DIE(,"IPT","IEN") K IPT
 ; GET OLD VALUE, SAVE OLD VALUE TO HISTORY, AND PUSH
 I $$GET1^DID(FLE,FLD,,"MULTIPLE-VALUED") D
 .I $$GET1^DID(FLE,FLD,,"TYPE")="WORD-PROCESSING" D  Q
 ..D HISTWP(HIS,$G(IEN(1)),FLE,DFN,FLD,VAL)
 .D HISTM(HIS,$G(IEN(1)),FLE,DFN,FLD,VAL) ; MULTI
 E  D HISTS(HIS,$G(IEN(1)),FLE,DFN,FLD,VAL) ; SINGLE
 S RET=$G(IEN(1))
 Q
 ;
HISTWP(HIS,HIEN,FLE,DFN,FLD,VAL) ; WORD PROCESSING
 N LOC,ERR,ND,ND1,DLAYGO S DLAYGO=FLE
 S LOC=$NA(^TMP($J,"DSIO HPUSH")) K @LOC
 ; GET OLD VALUE FROM STORAGE
 D GETS^DIQ(FLE,DFN_",",FLD,"E",LOC,"ERR") K @LOC@(FLE,DFN_",",FLD,"E")
 S ND=$NA(@LOC@(FLE,DFN_",",FLD)) M ND1=@ND K @LOC M @LOC=ND1
 ; SET OLD VALUE AS HISTORY OLD VALUE
 S DLAYGO=HIS D WP^DIE(HIS,HIEN_",",1,"K",LOC,"ERR") K @LOC
 ; SET NEW VALUE INTO STORAGE
 K DLAYGO I $P(FLE,".")=19641 Q:'$$CHECK^DSIO2($G(DFN))
 D WP^DIE(FLE,DFN_",",FLD,"K",VAL,"ERR")
 ; SET NEW VALUE AS HISTORY NEW VALUE
 S DLAYGO=HIS D WP^DIE(HIS,HIEN_",",2,"K",VAL,"ERR")
 Q
 ;
HISTM(HIS,HIEN,FLE,DFN,FLD,VAL) ; MULTIPLE NOT AS WORD PROCESSING
 ;
 ; CAN ONLY HANDLE .01
 ;
 N LOC,LOC1,I,CT,OVAL,ERR,IPT,DLAYGO S DLAYGO=FLE
 S LOC=$NA(^TMP($J,"DSIO HPUSH")) K @LOC
 S LOC1=$NA(^TMP($J,"DSIO HPUSH1")) K @LOC1
 ; GET OLD VALUES FROM STORAGE
 D GETS^DIQ(FLE,DFN_",",FLD_"*","E",LOC,"ERR")
 S I=1,CT=$NA(@LOC)
 F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="DSIO HPUSH")  D
 .I $QS(CT,5)=.01 S @LOC1@(I)=@CT,I=I+1
 K @LOC
 ; SET OLD VALUE AS HISTORY OLD VALUE
 S DLAYGO=HIS D WP^DIE(HIS,HIEN_",",1,"K",LOC1,"ERR") K @LOC1
 ; SET NEW VALUE INTO STORAGE
 K DLAYGO I $P(FLE,".")=19641 Q:'$$CHECK^DSIO2($G(DFN))
 S CT=$NA(@VAL)
 F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,1)'=$J!($QS(CT,2)'="SETDATA VALUE")  D
 .S IPT(FLE,"?+1,"_DFN_",",FLD)=$P(@CT,U,2,9999999999) D UPDATE^DIE("E","IPT") K IPT
 ; SET NEW VALUE AS HISTORY NEW VALUE
 S DLAYGO=HIS D WP^DIE(HIS,HIEN_",",2,"K",VAL,"ERR")
 Q
 ;
HISTS(HIS,HIEN,FLE,DFN,FLD,VAL) ; SINGLE
 N LOC,OVAL,ERR,IPT,DLAYGO S DLAYGO=FLE
 S LOC=$NA(^TMP($J,"DSIO HPUSH")) K @LOC
 ; GET OLD VALUE FROM STORAGE
 S OVAL=$$GET1^DIQ(FLE,DFN_",",FLD)
 ; SET OLD VALUE AS HISTORY OLD VALUE
 K @LOC S @LOC@(1)=OVAL
 S DLAYGO=HIS D WP^DIE(HIS,HIEN_",",1,"K",LOC,"ERR") K @LOC
 ; SET NEW VALUE INTO STORAGE
 I $G(@VAL@(1))'="" D
 .K DLAYGO I $P(FLE,".")=19641 Q:'$$CHECK^DSIO2($G(DFN))
 .S IPT(FLE,DFN_",",FLD)=@VAL@(1) D UPDATE^DIE("E","IPT") K IPT
 ; SET NEW VALUE AS HISTORY NEW VALUE
 S DLAYGO=HIS D WP^DIE(HIS,HIEN_",",2,"K",VAL,"ERR")
 Q
