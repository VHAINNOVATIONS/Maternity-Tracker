DSIO12 ;DSS/CMW - DSIO CLINICAL RPCS;06/06/2014 15:19
 ;;1.0;DSIO 1.0;**1**;Feb 06, 2015;Build 19
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ; REMLIST^ORQORB
 ; REMNONOR^ORQORB
 ; URGLIST^ORQORB
 ; REMIND^ORQQPX
 ; REMDET^PXRMRPCA
 ; GETUSER1^XQALDATA
 ;
 Q
 ;
PROV(RET,LIST) ; RPC: DSIO GET PROVIDER
 ;
 ; LIST - LIST(#)= PIEN (Provider IEN List)
 ;
 ; RETURN:
 ;  IEN^NAME^ADDRESS1^ADDRESS2^ADDRESS3^CITY^STATE^ZIP^PH OFFICE^
 ;  TITLE^SERVICE^NPI
 ;
 N CT,I
 S RET=$NA(^TMP($J,"DSIO PROV")) K @RET S @RET@(0)="-1^Entrie(s) not found."
 S I=0,CT="" F  S CT=$O(LIST(CT)) Q:CT=""  S @RET@(I)=$$PF(+LIST(CT)),I=I+1
 Q
 ;
PF(IEN) ; Provider Fields
 N FLD,OUT,LINE,FLD,STR
 S FLD=".01;.111;.112;.113;.114;.115;.116;.132;8;29;41.99"
 D GETS^DIQ(200,IEN_",",FLD,"IE","OUT") Q:'$D(OUT) IEN_"^Provider Not Found."
 S LINE=$NA(OUT) F  S LINE=$Q(@LINE) Q:LINE=""  S FLD($QS(LINE,3),$QS(LINE,4))=@LINE
 S STR=IEN_U
 S STR=STR_$$NAME^DSIO2(FLD(.01,"I"))_U              ; NAME
 S STR=STR_FLD(.111,"I")_U                           ; STREET ADDRESS 1
 S STR=STR_FLD(.112,"I")_U                           ; STREET ADDRESS 2
 S STR=STR_FLD(.113,"I")_U                           ; STREET ADDRESS 3
 S STR=STR_FLD(.114,"I")_U                           ; CITY
 S STR=STR_$$GET1^DIQ(5,FLD(.115,"I")_",",1)_U       ; STATE
 S STR=STR_FLD(.116,"I")_U                           ; ZIP
 S STR=STR_FLD(.132,"I")_U                           ; OFFICE PHONE
 S STR=STR_FLD(8,"E")_U                              ; TITLE
 S STR=STR_FLD(29,"E")_U                             ; SERVICE
 S STR=STR_FLD(41.99,"I")                            ; NPI
 Q STR
 ;
REM(RET,DFN,SORT) ; RPC: DSIO GET REMINDER LIST
 ;                       ORQQPX REMINDERS LIST
 ;
 ; Return patient's currently due PCE clinical reminders.
 ;
 ; RETURN:
 ; -----------------------------
 ; ^TMP("DSIO REMIND",$J,0)=COUNT
 ; ^TMP("DSIO REMIND",$J,1)=IEN^REMINDER PRINT NAME^DATE DUE
 ;
 N LST,RCT,TS,CT,STRT,END D SORT($G(SORT))
 S RET=$NA(^TMP($J,"DSIO12 REM")) K @RET S @RET@(0)="0^Nothing found."
 I '$G(DFN) S @RET@(0)="-1^Patient entry not found." Q
 D REMIND^ORQQPX(.LST,DFN)
 S (RCT,TS,CT)=0
 F  S CT=$O(LST(CT)) Q:'CT  D
 . S TS=TS+1 I STRT'="",TS'>STRT Q
 . S RCT=RCT+1 I END'="",RCT>END Q
 . S @RET@(RCT)=$P(LST(CT),"^",1,3)
 S:$G(TS) @RET@(0)=TS
 Q
 ;
DETAIL(RET,DFN,RIEN) ; RPC: DSIO GET REMINDER DETAIL
 ; DBIA 3078                PXRM REMINDER DETAIL
 ;
 ;   RIEN - clinical reminder (811.9 IEN)
 ;
 ; RETURN:
 ;  RET(1)="The reminder ZZ Breast Exam was inactivated 04/13/2005@21:55:15"
 ;
 I $G(DFN),$G(RIEN) D REMDET^PXRMRPCA(.RET,DFN,RIEN)
 S:'$D(RET) RET(0)="0^Entry not found."
 Q
 ;
ALERT(RET,SORT) ; RPC: DSIO GET NOTIFICATIONS/ALERTS
 ;
 ; Return current user's notifications across all patients
 ;N STRTDATE,STOPDATE,ORTOT,I,ORURG,URG,ORN,SORT,ORN0
 ;N ALRT,ALRTDT,ALRTPT,ALRTMSG,ALRTI,ALRTLOC,ALRTXQA,J,FWDBY,PRE,ALRTDFN
 N URGLIST,REMLIST,REM,NONORLST,NONOR
 K ^TMP("ORBG",$J)
 I $G(SORT)'?.N1",".N S SORT=""
 S STRTDATE="",STOPDATE="",FWDBY="Forwarded by: "
 D GETUSER1^XQALDATA("^TMP(""ORB"",$J)",DUZ,STRTDATE,STOPDATE)
 S ORTOT=^TMP("ORB",$J)
 D URGLIST^ORQORB(.URGLIST)
 D REMLIST^ORQORB(.REMLIST)
 D REMNONOR^ORQORB(.NONORLST)
 S J=0
 F I=1:1:ORTOT D
 .S ALRTDFN=""
 .S ALRT=^TMP("ORB",$J,I)
 .S PRE=$E(ALRT,1,1)
 .S ALRTXQA=$P(ALRT,U,2)  ;XQAID
 .S NONOR="" F  S NONOR=$O(NONORLST(NONOR)) Q:NONOR=""  D
 ..I ALRTXQA[NONOR S REM=1  ;allow this type of alert to be Removed
 .S ALRTMSG=$P($P(ALRT,U),PRE_"  ",2)
 .I $E(ALRT,4,8)'="-----" D  ;not forwarded alert info/comment
 ..S ORURG="n/a"
 ..S ALRTI=$P(ALRT,"  ")
 ..S ALRTPT=""
 ..S ALRTLOC=""
 ..I $E($P(ALRTXQA,";"),1,3)="TIU" S ORURG="Moderate"
 ..I $P(ALRTXQA,",")="OR" D
 ...S ORN=$P($P(ALRTXQA,";"),",",3)
 ...S URG=$G(URGLIST(ORN))
 ...S ORURG=$S(URG=1:"HIGH",URG=2:"Moderate",1:"low")
 ...S REM=$G(REMLIST(ORN))
 ...S ORN0=^ORD(100.9,ORN,0)
 ...S ALRTI=$S($P(ORN0,U,6)="INFODEL":"I",1:"")
 ...S ALRTDFN=$P(ALRTXQA,",",2)
 ...S ALRTLOC=$G(^DPT(+$G(ALRTDFN),.1))
 ..S ALRTI=$S(ALRTI="I":"I",1:"")
 ..I (ALRT["): ")!($G(ORN)=27&(ALRT[") CV")) D  ;WAT
 ...S ALRTPT=$P(ALRT,": ")
 ...S ALRTPT=$E(ALRTPT,4,$L(ALRTPT))
 ...I $G(ORN)=27&(ALRT[") CV") S ALRTMSG=$P($P(ALRT,U),": ",2) ;WAT
 ...E  S ALRTMSG=$P($P(ALRT,U),"): ",2) ;WAT
 ...I $E(ALRTMSG,1,1)="[" D
 ....S:'$L(ALRTLOC) ALRTLOC=$P($P(ALRTMSG,"]"),"[",2)
 ....S ALRTMSG=$P(ALRTMSG,"] ",2)
 ..I '$L($G(ALRTPT)) S ALRTPT="no patient"
 ..S ALRTDT=$P(ALRTXQA,";",3)
 ..S ALRTDT=$P(ALRTDT,".")_"."_$E($P(ALRTDT,".",2)_"0000",1,4)
 ..S ALRTDT=$E(ALRTDT,4,5)_"/"_$E(ALRTDT,6,7)_"/"_($E(ALRTDT,1,3)+1700)_"@"_$E($P(ALRTDT,".",2),1,2)_":"_$E($P(ALRTDT,".",2),3,4)
 ..;S ALRTDT=($E(ALRTDT,1,3)+1700)_"/"_$E(ALRTDT,4,5)_"/"_$E(ALRTDT,6,7)_"@"_$E($P(ALRTDT,".",2),1,2)_":"_$E($P(ALRTDT,".",2),3,4)
 ..S J=J+1,^TMP("ORBG",$J,J)=ALRTI_U_ALRTPT_U_ALRTLOC_U_ORURG_U_ALRTDT_U
 ..S ^TMP("ORBG",$J,J)=^TMP("ORBG",$J,J)_ALRTMSG_U_U_ALRTXQA_U_$G(REM)_U
 .;
 .;if alert forward info/comment:
 .I $E(ALRTMSG,1,5)="-----" D
 ..S ALRTMSG=$P(ALRTMSG,"-----",2)
 ..I $E(ALRTMSG,1,14)=FWDBY D
 ...S J=J+1,^TMP("ORBG",$J,J)=FWDBY_U_$P($P(ALRTMSG,FWDBY,2),"Generated: ")_$P($P(ALRTMSG,FWDBY,2),"Generated: ",2)
 ..E  S ^TMP("ORBG",$J,J)=^TMP("ORBG",$J,J)_U_""""_ALRTMSG_""""
 S ^TMP("ORBG",$J)=""
 ; DSIO SORT
 S REF=$NA(^TMP("ORBG",$J))
 M LST=@REF
 S RET=$NA(^TMP("DSIO ALERT",$J)) K @RET
 I $G(ORTOT)=0 S @RET@(0)="-1^No entries found." Q
 I $G(SORT) D
 . D SORTA^DSIO2(.LST,.RETLST)
 . K @RET M @RET=RETLST
 I '$G(SORT) M @RET=LST
 S @RET@(0)=ORTOT
 K LST,RETLST,REF
 K ^TMP("ORB",$J),^TMP("ORBG",$J)
 Q
 ;
ALERT2(RET,SORT,EXP) ; RPC: DSIO GET NOTIFICATIONS/ALERTS
 ;                          ORWORB FASTUSER
 ;
 ; Get user alerts for patients that are being tracked. Current this only works for
 ; TIU related alerts to determine patient.
 ;
 N ND,RCT,TS,CT,STRT,END D SORT($G(SORT))
 S RET=$NA(^TMP($J,"DSIO12 ALERT")),ND=$NA(^TMP($J,"DSIO ALERT")) K @RET,@ND
 K @RET S @RET@(0)="0^No tracked patient alerts."
 D GETUSER1^XQALDATA(ND,DUZ)
 S (RCT,TS,CT)=0 F  S CT=$O(@ND@(CT)) Q:'CT  D
 . S TST=$P($P(@ND@(CT),U,2),";") I '$G(EXP) Q:$E(TST,1,3)'="TIU"
 . I '$G(EXP) Q:'$$TRACK($$GET1^DIQ(8925,$E(TST,4,($L(TST)))_",",.02,"I"))
 . S TS=TS+1 I STRT'="",TS'>STRT Q
 . S RCT=RCT+1 I END'="",RCT>END Q
 . S @RET@(RCT)=@ND@(CT)
 S:$G(TS) @RET@(0)=TS
 Q
 ;
TRACK(DFN) ; Tracked Yes or No
 Q $S($$TRACK^DSIO4(DFN,1)=1:1,1:0)
 ;
 ; ---------------------------------- COMMON ----------------------------------
 ;
SORT(SORT) ; Set Start and End
 D S^DSIO2(SORT)
 Q
