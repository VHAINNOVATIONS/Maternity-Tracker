Routine DSIO64 saved using VFDXTRS routine on Oct 02, 2015 16:29
DSIO64^INT^63809,48351^Sep 14, 2015@13:25
DSIO64 ;DSS/TFF - DSIO DDCS DATA RETURN;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**2**;Feb 06, 2015;Build 18
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
ELE(RET,OBJECT,INFACE,SIEN,ELE) ; RPC: DSIO DDCS GET CONTROL VALUE
 ;
 ; INPUT PARAMETERS:
 ; -----------------
 ;  OBJECT = OBJECT of CONTROL
 ;           Example: 500000042;PXRMD(801.41,
 ;                    Which is the Reminder Dialog of IEN 500000042
 ;         = O=### (ORDER IEN)
 ;         = N=### ( NOTE IEN)
 ;
 ; INFACE = IEN;DSIO(19641.42, (or NAME) 
 ;          or IEN;DSIO(19641.49,
 ;
 ;    SIEN = Destination IEN - Record of Control
 ;
 ;     ELE = Full Name of ELEMENT
 ;
 ; RETURN: 0^MESSAGE
 ;         Example:
 ;           RET(0)="101;VFD(21661,^MO_CLINICALCOMMENTS^03^^THESE "_$c(13,10)_"ARE VALUES."
 ;
 S RET(0)="0^Nothing found."
 N NUM,CONTROL,DFLE,DFN,SHARE,DATA S NUM=1
 Q:$G(OBJECT)=""  S:OBJECT["=" SIEN=$P(OBJECT,"=",2)
 S CONTROL=$$OBJECT^DSIO65(OBJECT) Q:'CONTROL
 I $G(SIEN) D
 . S DFLE=$$GET1^DIQ(19641.4,CONTROL_",",.03,"I") Q:'DFLE
 . S DFN=$$DFN^DSIO6(CONTROL,SIEN)
 I $G(INFACE)'="" D  K:$G(RET(1))'="" RET(0) Q
 . S INFACE=$$INFACE^DSIO65($G(INFACE)),SHARE=$$SHARE(CONTROL,INFACE)
 . D D1
 Q:'$G(SIEN)&('$G(DFLE))
 S DATA=$O(^DSIO(19641.41,"B",SIEN_";"_$TR(^DIC(DFLE,0,"GL"),U),"")) Q:'DATA
 S INFACE="" F  S INFACE=$O(^DSIO(19641.41,DATA,1,"B",INFACE)) Q:INFACE=""  D
 . S SHARE=$$SHARE(CONTROL,INFACE)
 . D D1
 K:$G(RET(1))'="" RET(0)
 Q
 ;
D1 ; Continue
 N IEN
 I $G(SHARE),$G(DFN) D  Q
 . S IEN=$O(^DSIO(19641.4941,DFN,1,"B",INFACE,"")) Q:'IEN
 . I $G(ELE)'="" D D2(19641.4941) Q
 . S ELE="" F  S ELE=$O(^DSIO(19641.4941,DFN,1,IEN,1,"C",ELE)) Q:ELE=""  D
 . . D D2(19641.4941)
 Q:'$G(SIEN)&('$G(DFLE))
 S:'$D(DATA) DATA=$O(^DSIO(19641.41,"B",SIEN_";"_$TR(^DIC(DFLE,0,"GL"),U),"")) Q:'DATA
 S IEN=$O(^DSIO(19641.41,DATA,1,"B",INFACE,"")) Q:'IEN
 I $G(ELE)'="" D D2(19641.41) Q
 S ELE="" F  S ELE=$O(^DSIO(19641.41,DATA,1,IEN,1,"C",ELE)) Q:ELE=""  D
 . D D2(19641.41)
 Q
 ;
D2(FLE) ; Continue
 N IENS,IELE
 I FLE=19641.41 D
 . S IENS=$O(^DSIO(19641.41,DATA,1,IEN,1,"C",ELE,"")) Q:'IENS
 . S IELE=^DSIO(19641.41,DATA,1,IEN,1,IENS,0)
 I FLE=19641.4941 Q:'$G(DFN)  D
 . S IENS=$O(^DSIO(19641.4941,DFN,1,IEN,1,"C",ELE,"")) Q:'IENS
 . S IELE=^DSIO(19641.4941,DFN,1,IEN,1,IENS,0)
 Q:'$G(IELE)
 S RET(NUM)=INFACE_U_ELE_U_$$ID(ELE,INFACE)_U_$$VAL(IELE),NUM=NUM+1
 Q
 ;
SHARE(CONTROL,INFACE) ; Determine if the INTERFACE/FORM should be SHARED
 N SHARE
 S SHARE=$$GET1^DIQ(19641.4,$G(CONTROL)_",",.06,"I")
 ; *** LIMITED or not set - Allow the INTERFACE/FORM decide
 I $G(INFACE)'=""&(SHARE=2!(SHARE="")) Q $$SE
 Q $S(SHARE:1,1:0)
 ;
SE() ; Continue
 Q:$G(INFACE)[";DSIO(19641.49," $S($$GET1^DIQ(19641.49,+INFACE_",",.04,"I"):1,1:0)
 Q $S($$GET1^DIQ(19641.42,+INFACE_",",.03,"I"):1,1:0)
 ;
ID(CONTROL,INFACE) ; Get assigned ID
 N IEN,PG,IENS
 Q:$G(CONTROL)=""!($G(INFACE)="") ""
 S IEN=$O(^DSIO(19641.42,"B",INFACE,"")) S:'IEN IEN=$O(^DSIO(19641.42,"P",INFACE,"")) Q:'IEN ""
 S PG=$O(^DSIO(19641.42,IEN,1,"CONTROL",CONTROL,"")) Q:'PG ""
 S IENS=$O(^DSIO(19641.42,IEN,1,"CONTROL",CONTROL,PG,""))
 Q $P(^DSIO(19641.42,IEN,1,PG,1,IENS,0),U,4)
 ;
VAL(IEN) ; Get Value
 ;
 ; There's an extra caret in the value wp for the storage of the index value.
 ;
 ; Example:
 ;   RET(10)="MO_CLINICALCOMMENTS^03^^THIS IS THE CONFIGURATION VALUES"_$c(13,10)_
 ;           "FOR A WP TYPE CONTROL."_$c(13,10)
 ;
 N OUT,VAL,CT
 D GETS^DIQ(19641.45,IEN_",",1,,"OUT")
 S VAL="",CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 . Q:$QL(CT)<4
 . S VAL=$S(VAL'="":VAL_$c(13,10),1:"")_@CT
 Q VAL
 ;
IMPORT(RET,INFACE,ELES) ; RPC: DSIO DDCS IMPORT FORM
 ;
 ; INFACE = IEN;DSIO(19641.42, (or NAME)
 ;          or IEN;DSIO(19641.49,
 ;
 ;   ELES = (ARRAY)
 ;          PAGE#^CONTROL_NAME^CLASS
 ;
 N IEN,CT,IPT,PG,ERR S RET=0
 S INFACE=$$INFACE^DSIO65($G(INFACE)) Q:INFACE'[";DSIO(19641.42,"
 S IEN=+INFACE,CT=$NA(ELES) F  S CT=$Q(@CT) Q:CT=""  K PG D
 . Q:'@CT
 . S IPT(19641.421,"?+1,"_IEN_",",.01)=+@CT
 . D UPDATE^DIE("E","IPT","PG") K IPT Q:'$G(PG(1))
 . S IPT(19641.4211,"?+1,"_PG(1)_","_IEN_",",.01)=$TR($$UP^XLFSTR($P(@CT,U,2))," ","_")
 . S IPT(19641.4211,"?+1,"_PG(1)_","_IEN_",",.02)=$P(@CT,U,3)
 . D UPDATE^DIE("E","IPT",,"ERR") K IPT
 S:'$D(ERR) RET=1
 Q
 ;
 ; ----------------------------------------------------------------------------
 ;
 ;   STRT = The node of the return array that the control data begins
 ;
 ;    CON = The name of the CONTROL ELEMENT
 ;
 ; OBJECT = OBJECT of CONTROL
 ;          Example: 500000042;PXRMD(801.41,
 ;                   Which is the Reminder Dialog of IEN 500000042
 ;         = O=### (ORDER IEN)
 ;         = N=### ( NOTE IEN)
 ;
 ; INFACE = IEN;DSIO(19641.42, (or NAME)
 ;          or IEN;DSIO(19641.49,
 ;
 ;   SIEN = Destination IEN - Record of Control
 ;
 ; RETURN
 ;     CONTROL: CV^NAME^F^(INDEXED^VALUE)
 ;
RESTF(RET,STRT,IEN,PG,CON,OBJECT,INFACE,SIEN) ; Restore Saved Data (FORMS)
 N OUT,I,VAL,CT
 D ELE(.OUT,OBJECT,$S($D(SINFACE):SINFACE,1:INFACE),SIEN,CON) Q:$D(OUT(0))
 F I=STRT:1:$O(@RET@(PG,IEN,0),-1) K @RET@(PG,IEN,I)
 S VAL=$P(OUT(1),U,4,999)
 S CT=STRT F I=1:1:$L(VAL,$C(13,10)) S @RET@(PG,IEN,CT)="CV^"_CON_"^F^"_$P(VAL,$C(13,10),I),CT=CT+1
 Q
 ;
RESTD(RET,STRT,CON,OBJECT,INFACE,SIEN) ; Restore Saved Data (DIALOGS)
 N OUT,I,VAL
 D ELE(.OUT,OBJECT,INFACE,SIEN,CON) Q:$D(OUT(0))
 F I=STRT:1:$O(@RET@(""),-1) K @RET@(I)
 S VAL=$P(OUT(1),U,4,999)
 S DDCSCT=STRT F I=1:1:$L(VAL,$C(13,10)) D
 . S @RET@(DDCSCT)="CV^"_CON_"^F^"_$P(VAL,$C(13,10),I),DDCSCT=DDCSCT+1
 Q
