Routine DSIO1 saved using VFDXTRS routine on May 20, 2015 17:01
DSIO1^INT^63692,31310^May 20, 2015@08:41
DSIO1 ;DSS/TFF - DSIO GENERAL RPCS;06/28/2013 15:19
 ;;1.0;DSIO 1.0;;Feb 06, 2015;Build 1
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ; $$DOB^DPTLK1
 ; $$SCREEN^DPTLK1
 ; $$SSN^DPTLK1
 ;
 Q
 ;
 ; ------------------------------ PATIENT SEARCH ------------------------------
 ;
PATIENT(RET,KEY,SORT) ; RPC: DSIO PATIENT LIST
 ;
 ;  IX : KEY
 ; ----------------------------------------------
 ;  SSN: SSN
 ;   BS: LAST 4
 ;  BS5: FIRST LETTER OF LAST NAME + LAST 4 OF SSN
 ;
 ;  KEY: PARTIAL NAME - TO - FULL NAME
 ; SORT: PAGE#,CT
 ;
 ; RETURN FORMAT
 ; -------------
 ; DFN^LASTNAME,FIRSTNAME^SSN(LAST 4)^DATE OF BIRTH^VETERAN STATUS^
 ; LOCATION(WARD)^ROOM/BED^SERVICE CONNECTED^CURRENTLY TRACKING^
 ; SSN^CITY^STATE^ZIP^SENSITIVE STATUS (1,0)
 ;
 ;  CURRENTLY TRACKING: 0:NO,1:YES,2:FLAG
 ; IF NOTHING IS FOUND: RET(0)="0^Patient(s) not found."
 ;
 S RET=$NA(^TMP($J,"DSIO PATIENT")) K @RET S @RET@(0)="0^Patient(s) not found."
 S KEY=$G(KEY) D PAR
 ; *** LIST BY FULL SSN
 I KEY?9N!(KEY?3N1"-"2N1"-"4N) D LIST($TR(KEY,"-",""),"SSN") Q
 ; *** LIST BY LAST 4
 I KEY?4N D LIST(KEY,"BS") Q
 ; *** LIST BY FIRST LETTER OF LAST NAME + LAST 4 OF SSN
 I KEY?1A4N D LIST(KEY,"BS5") Q
 ; *** ALL PATIENTS, ASSUME NAME OR PARTIAL NAME
 D LIST(KEY,"B")
 Q
 ;
LIST(KEY,IX) ; Get Patient List
 N RCT,TS,CT,STRT,END D SORT($G(SORT))
 S (RCT,TS)=0,CT=$E(KEY,1,($L(KEY)-1))_$$M($E(KEY,$L(KEY)))
 F  S CT=$O(^DPT(IX,CT)) Q:CT=""  Q:$$KEY(CT,KEY)  D
 . S IEN=$O(^DPT(IX,CT,""))
 . Q:$$GET1^DIQ(2,IEN_",",.02,"I")'="F"
 . S TS=TS+1 I STRT'="",TS'>STRT Q
 . S RCT=RCT+1 I END'="",RCT>END Q
 . S @RET@(RCT)=IEN_U_$$FLDS(IEN)
 S:$G(TS) @RET@(0)=TS
 Q
 ;
FLDS(IEN) ; Return Transformed Fields
 ;
 ; LASTNAME,FIRSTNAME^SSN(LAST 4)^DATE OF BIRTH^VETERAN STATUS^
 ; LOCATION(WARD)^ROOM/BED^SERVICE CONNECTED^CURRENTLY TRACKING^
 ; SSN^CITY^STATE^ZIP^SENSITIVE STATUS (1,0)
 ;
 N SSN,DOB,TRK,OUT,CT,FLD,STR
 S SSN=$$SSN^DPTLK1(IEN) I SSN?.N S SSN=$E(SSN,6,9)
 S DOB=$$FMTE^XLFDT($$DT^DSIO2($$DOB^DPTLK1(IEN,0)),"5Z")
 S TRK=$$TRACK^DSIO4(IEN,1)
 D GETS^DIQ(2,IEN_",",".01;1901;.1;.101;.301;.09;.114;.115;.116",,"OUT")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  S FLD($QS(CT,3))=@CT
 S STR=$$NAME^DSIO2(FLD(.01))_U_SSN_U_DOB_U_FLD(1901)_U_FLD(.1)_U_FLD(.101)_U_FLD(.301)_U
 S STR=STR_TRK_U_FLD(.09)_U_FLD(.114)_U_FLD(.115)_U_FLD(.116)_U_$$SCREEN^DPTLK1(IEN)
 Q STR
 ;
 ; ------------------------------ PATIENT FIELDS ------------------------------
 ;
INFO(RET,DFN,LIST) ; RPC: DSIO GET PATIENT INFORMATION
 ;
 ; Return a list of patient information by field name or number
 ;
 ; RETURN:
 ; INPUT (FIELD #/NAME)^VALUE
 ; -1^MESSAGE
 ;
 I '$$CHECK^DSIO2($G(DFN)) S RET(0)="-1^Patient entry not found." Q
 N FLD,NM,FLG
 S FLD=$NA(LIST) F  S FLD=$Q(@FLD) Q:FLD=""  K NM D
 . I @FLD?.N S NM=$$FLDNUM^DILFD(19641,$$UP^XLFSTR($P(@FLD,U)))
 . E  I $$VFIELD^DILFD(19641,$P(@FLD,U)) S NM=$P(@FLD,U)
 . I $G(NM),$P(^DD(19641,NM,0),U,2)["D" D  Q
 . . S $P(LIST($QS(FLD,1)),U,2)=$$FMTE^XLFDT($$GET1^DIQ(19641,DFN_",",NM,"I"),"5Z")
 . S $P(LIST($QS(FLD,1)),U,2)=$$GET1^DIQ(19641,DFN_",",NM)
 M RET=LIST S:$D(RET)<10 RET(0)="-1^Nothing found."
 Q
 ;
PATSET(RET,DFN,FLD,VAL) ; RPC: DSIO SET PATIENT INFORMATION
 I '$$CHECK^DSIO2($G(DFN)) S RET="-1^Patient entry not found." Q
 N TRVAL,OUT,IPT,ERR
 I $G(FLD)=.01 S RET="-1^You cannot change the PATIENT NAME." Q
 I '$$VFIELD^DILFD(19641,$G(FLD)) S RET="-1^Not a valid field." Q
 I $$GET1^DID(19641,FLD,,"TYPE")="COMPUTED" S RET="-1^You cannot set a computed field." Q
 I "^.04^1.2^"[(U_FLD_U) S RET="-1^This field cannot be set directly." Q
 S TRVAL=$S($$GET1^DID(19641,FLD,,"TYPE")="DATE/TIME":$$DT^DSIO2($G(VAL)),1:$G(VAL))
 D VAL^DIE(19641,DFN,FLD,,TRVAL,.OUT) I TRVAL'=OUT S RET="-1^The value is not valid." Q
 S IPT(19641,DFN_",",FLD)=TRVAL
 D FILE^DIE("E","IPT","ERR") I '$D(ERR) S RET=1
 Q
 ;
 ; ----------------------------- PATIENT TRACKING -----------------------------
 ;
TRCLIST(RET,TYP,SORT) ; RPC: DSIO GET TRACKING
 ;
 ; RETURN MATERNITY TRACKING LISTS
 ;
 ; INPUT PARAMETERS:
 ; -----------------
 ; ALL: ALL LOGS
 ;   T: ALL CURRENTLY TRACKED PATIENTS
 ;   F: ALL CURRENTLY FLAGGED PATIENTS
 ; DFN: ALL LOGS FOR A PATIENT
 ;  F#: ALL CURRENTLY FLAGGED LOGS FOR A PATIENT
 ;
 ; SORT: PAGE#,CT
 ;
 S RET=$NA(^TMP($J,"DSIO TRCLIST")) K @RET S @RET@(0)="0^No entries found."
 D PAR S TYP=$G(TYP)
 ;
 ; *** GET ALL CURRENTLY TRACKED PATIENTS
 ;     P^DFN^LASTNAME,FIRSTNAME^LAST 4^DOB^PHONE^EDD^OB^PLANNED DELV LOC^
 ;     CURRENTLY PREGNANT^CURRENTLY LACATING
 ;     ^DSIO(19641.2,"TRACKING",PATIENT NAME,DFN,DA)=""
 I TYP="T" D T1("TRACKING",4) Q
 ;
 ; *** GET ALL CURRENTLY FLAGGED PATIENTS
 ;     P^DFN^LASTNAME,FIRSTNAME^LAST 4^DOB^PHONE^REASON
 ;     ^DSIO(19641.2,"FLAGGED",PATIENT NAME,DFN,DA)=""
 I TYP="F" D T1("FLAGGED",5) Q
 ;
 ; *** GET ALL LOGS FOR A PATIENT
 ;     L^IEN^TRACKING DT(E)^DFN^LASTNAME,FIRSTNAME^USER^ACTION^SOURCE^REASON|REASON
 ;     C^IEN^COMMENT
 ;     ^DSIO(19641.2,DFN,1,"B",LOG DATE,DA)=""
 I TYP D T1("B",2,TYP) Q
 ; *** GET CURRENTLY FLAGGED ENTRIES FOR A PATIENT
 ;     ^DSIO(19641.2,DFN,1,"FLAG",LOG DATE,DA)
 I TYP?1"F"1.N D T1("FLAG",3,$E(TYP,2,$L(TYP))) Q
 ; *** GET ALL LOGS
 ;     ^DSIO(19641.2,"C",LOG DATE,DFN,DA)=""
 I TYP="" D T1("C",1)
 Q
 ;
T1(IX,FLG,KEY) ; Get Tracking List
 N COUNT,TS,CT,IEN,STRT,END D SORT($G(SORT))
 S (COUNT,TS)=0
 I FLG=1!(FLG=4)!(FLG=5) D
 . ; IX: C, TRACKING, FLAGGED
 . ; ^DSIO(19641.2,IX(C),LOG DATE,DFN,DA)=""
 . ; ^DSIO(19641.2,IX,PATIENT NAME,DFN,DA)=""
 . S CT=$NA(^DSIO(19641.2,IX)) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,2)'=IX  D
 . . S DFN=$QS(CT,4),IEN=$QS(CT,5) D T2
 I FLG=2!(FLG=3) S DFN=KEY D
 . ; IX: B, FLAG
 . ; ^DSIO(19641.2,DFN,1,IX,LOG DATE,DA)=""
 . S CT=$NA(^DSIO(19641.2,DFN,1,IX)) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,4)'=IX  D
 . . S IEN=$QS(CT,6) D T2
 S:$G(TS) @RET@(0)=TS
 Q
 ;
T2 ; Continue
 S TS=TS+1 I STRT'="",TS'>STRT Q
 S COUNT=COUNT+1 I END'="",COUNT>END Q
 D TGET(FLG)
 Q
 ;
TGET(F) ; Return Unique String of Data
 ;
 ; L^IEN^TRACKING DT(E)^DFN^LASTNAME,FIRSTNAME^USER^ACTION^SOURCE^REASON|REASON
 ; C^IEN^COMMENT
 N STR,IFLD,EFLD
 D TFLD
 I F=1 D TG0    ; ALL
 I F=2 D TG0    ; ALL LOGS FOR A PATIENT
 I F=3 D TG0    ; ALL FLAGGED LOGS FOR A PATIENT
 ;
 ; P^DFN^LASTNAME,FIRSTNAME^LAST 4^DOB^PHONE^EDD^OB^PLANNED DELV LOC^
 ; CURRENTLY PREGNANT^CURRENTLY LACATING
 I F=4 D TG4    ; TRACKING
 ;
 ; P^DFN^LASTNAME,FIRSTNAME^LAST 4^DOB^PHONE^REASON
 I F=5 D TG5    ; FLAGGED
 Q
 ;
TG0 ; 1,2,3
 S STR="L^"_IEN_U_$$FMTE^XLFDT(IFLD(.01),"5ZS")_U_DFN_U_$$NAME^DSIO2($$GET1^DIQ(2,DFN,.01))_U
 S STR=STR_$$NAME^DSIO2(EFLD(.03))_U_EFLD(.02)_U_EFLD(.03)_U_$$RES
 S @RET@(COUNT,0)=STR D COM
 Q
TG4 ; 4
 N SSN,DOB,PHONE,EDD,OB,LOC,PREG,LAC D TGV
 S @RET@(COUNT)="P^"_DFN_U_$$NAME^DSIO2(EFLD(.03))_U_SSN_U_DOB_U_PHONE_U_EDD_U_OB_U_LOC_U_PREG_U_LAC
 Q
TG5 ; 5
 N SSN,DOB,PHONE,EDD,OB,LOC,PREG,LAC D TGV
 S @RET@(COUNT)="P^"_DFN_U_$$NAME^DSIO2(EFLD(.03))_U_SSN_U_DOB_U_PHONE_U_$$RES
 Q
 ;
TGV ; Get Common Variables
 S SSN=$$SSN^DPTLK1(DFN) I SSN?.N S SSN=$E(SSN,6,9)
 S DOB=$$FMTE^XLFDT($$DT^DSIO2($$DOB^DPTLK1(DFN,0)),"5Z")
 S PHONE=$$GET1^DIQ(2,DFN,.131)
 S EDD=$$FMTE^XLFDT($$GET1^DIQ(19641.03,$$GET1^DIQ(19641.13,$$PG^DSIO4(DFN)_",",.06,"I")_",",.01,"I"),"5Z")
 S OB=$$TITLE^XLFSTR($$GET1^DIQ(19641.13,$$PG^DSIO4(DFN)_",",.08))
 S LOC=$$TITLE^XLFSTR($$GET1^DIQ(19641.13,$$PG^DSIO4(DFN)_",",.09))
 S PREG=$$PSTAT^DSIO4(DFN),LAC=$$LACT^DSIO4(DFN)
 Q
 ;
TFLD ; Get Tracking Fields
 N IN,EN
 D GETS^DIQ(19641.24,IEN_","_DFN_",",".01;.02;.03;.04","I","IN")
 D GETS^DIQ(19641.24,IEN_","_DFN_",",".01;.02;.03;.04","I","EN")
 D TFLD1($NA(IN),.IFLD),TFLD1($NA(EN),.EFLD)
 Q
 ;
TFLD1(CT,ARY) ; Continue
 F  S CT=$Q(@CT) Q:CT=""  S ARY($QS(CT,3))=@CT
 Q
 ;
RES() ; Reason Multiple
 N RES,R,STR
 D GETS^DIQ(19641.24,IEN_","_DFN_",","1*","I","RES")
 S R=$NA(RES) F  S R=$Q(@R) Q:R=""  D
 . S STR=$G(STR)_$$TITLE^XLFSTR(@R)_"|"
 Q $E($G(STR),1,($L($G(STR))-1))
 ;
COM ; Comment Multiple
 N COM,C,CT
 D GETS^DIQ(19641.24,IEN_","_DFN_",",2,,"COM")
 S CT=1,C=$NA(COM) F  S C=$Q(@C) Q:C=""  D
 . Q:$QL(C)<4
 . S @RET@(COUNT,CT)="C^"_IEN_U_@C,CT=CT+1
 Q
 ;
REC(RET,DFN,ACT,RES,COM,SOR) ; RPC: DSIO CREATE TRACKING LOG
 ;
 ; MCC DASHBOARD PURPOSE -  TO CHANGE THE TRACKING STATUS OF A PATIENT
 ;
 ; ACT: ACTION    INPUT PARAMETER:
 ;   0: STOP           DFN : PATIENT NUMBER
 ;   1: START          ACT : ACTION (0:STOP,1:START,2:FLAG,3:REJECT,4:ACCEPT)
 ;   2: FLAG           RES : REASON  - #^#^#
 ;   3: REJECT         COM : COMMENT - ARRAY
 ;   4: ACCEPT         SOR : SOURCE  - DEFAULT IS DASHBOARD
 ;
 ; RETURN: -1^ERROR OR IEN
 ;
 N DSIOSILENT,DIC,DA,X,Y,DLAYGO,IPT,IEN,I,%X,%Y
 S DSIOSILENT=1,RET=-1
 I '$$CHECK^DSIO2($G(DFN)) S RET="-1^Patient entry not found." Q
 S DLAYGO=19641.2,DIC="^DSIO(19641.2,",DIC(0)="XL",X="`"_DFN
 D ^DIC I +Y<1 S RET="-1^Failed to create log." Q
 ; *** TRACKING STATUS/EVENT
 S ACT=$E($G(ACT),1) S ACT=$S(ACT?1N:ACT,1:"")
 I ACT="" S RET="-1^EVENT TYPE could not be identified." Q
 I ACT<0!(ACT>4) S RET="-1^EVENT TYPE reference number is invalid." Q
 S SOR=$$UP^XLFSTR($G(SOR,"DASHBOARD"))
 S IPT(19641.22,"?+1,",.01)=SOR
 S:SOR="DASHBOARD"!(SOR="FILEMAN") IPT(19641.22,"?+1,",.02)=1
 S DLAYGO=19641.22 D UPDATE^DIE(,"IPT") K IPT
 S IPT(19641.24,"+1,"_DFN_",",.01)=$$NOW^XLFDT
 S IPT(19641.24,"+1,"_DFN_",",.02)=ACT
 S IPT(19641.24,"+1,"_DFN_",",.03)=DUZ
 S IPT(19641.24,"+1,"_DFN_",",.04)=SOR
 S DLAYGO=19641.24 D UPDATE^DIE(,"IPT","IEN") K IPT
 I '$G(IEN(1)) S RET="-1^Failed to create log." Q
 ; *** UPDATE THE DSIO PATIENT FILE
 S IPT(19641,DFN_",",.02)=$S(ACT=3:0,ACT=4:1,1:ACT)
 D FILE^DIE(,"IPT") K IPT
 ; *** BUILD THE REASON MULTIPLE
 S RES=$G(RES) F I=1:1:$L(RES,U) D
 . S IPT(19641.241,"?+1,"_IEN(1)_","_DFN_",",.01)=$P(RES,U,I)
 . D UPDATE^DIE(,"IPT") K IPT
 ; *** SET THE COMMENT WORD PROCESSING FIELD
 ;     WE CAN'T PROCESS THE ZERO NODE WITH WP^DIE SO INSTEAD OF MERGING
 ;     WE CAN USE %XY^%RCR TO CONCATENATE A SUBSCRIPT
 I $D(COM)>9 K ^TMP($J,"DSIO REC") D
 . S %X="COM(",%Y="^TMP($J,""DSIO REC"",1" D %XY^%RCR
 . D WP^DIE(19641.24,IEN(1)_","_DFN_",",2,"K","^TMP($J,""DSIO REC"")")
 . K ^TMP($J,"DSIO REC")
 S RET=$S(SOR="DASHBOARD":1,1:IEN(1))
 Q
 ;
 ; -------------------------------- SELECTION ---------------------------------
 ;
SELECT(RET,TYP) ; RPC: DSIO SELECT LIST
 ;
 ; RETURN DROP DOWN LIST
 ;
 ; INPUT PARAMETER:
 ;  TYP : 'S' FOR FILE 19641.22
 ;  TYP : 'R' FOR FILE 19641.23
 ;
 ;  LETTER^ENTRY^A : ADD AN ENTRY TO ONE OF THE ABOVE FILES
 ;  LETTER^ENTRY^D : DELETE AN ENTRY FROM ONE OF THE ABOVE FILES
 ;
 ;  RETURN LIST FOR SUCCESS OR RET(0)="-1^MESSAGE" FOR FAILURE
 ;
 N FILE,FLD,DIK,DA,X,Y,IPT,I,CT
 S RET(0)="0^No entries found.",TYP=$$UP^XLFSTR($G(TYP)) Q:TYP=""
 S FILE=$S($P(TYP,U)="S":19641.22,$P(TYP,U)="R":19641.23,1:"") Q:FILE=""
 S FLD=$S(FILE=19641.22:"@;.01;.02I",1:"@;.01")
 I $P(TYP,U,3)="D" D
 . S DIK="^DSIO("_FILE_","
 . S DA=$$FIND1^DIC(FILE,,"X",$$UP^XLFSTR($P(TYP,U,2))) D:DA ^DIK
 I $P(TYP,U,3)="A" D
 . S IPT(FILE,"?+1,",.01)=$$UP^XLFSTR($P(TYP,U,2))
 . D UPDATE^DIE(,"IPT")
 S I=0,CT="" F  S CT=$O(^DSIO(FILE,"B",CT)) Q:CT=""  D
 . Q:$$GET1^DIQ(FILE,$O(^DSIO(FILE,"B",CT,""))_",",.02,"I")
 . S RET(I)=$$TITLE^XLFSTR(CT),I=I+1
 Q
 ;
 ; ----------------------------- EXTERNAL ENTITY ------------------------------
 ;
SENT(RET,IEN,NAME,TYP,ACT,PCON,ADDR,PHONE,AB) ; RPC: DSIO SAVE EXTERNAL ENTITY
 ;
 ;  ADDR = ARRAY OF LABELS: 1,2,3,CITY,STATE,ZIP
 ; PHONE = ARRAY OF LABLES: H,MC,WP
 ;
 ; RETURN:
 ; IEN OR -1^MESSAGE
 ;
 D:'$G(AB) AB^DSIO2("NAME,TYP,ACT,PCON,ADDR,PHONE")
 N IPT,CT,LOC,VAL
 S IEN=$S($G(IEN):IEN,1:"?+1")_",",NAME=$$UP^XLFSTR($G(NAME))
 I IEN,$$FIND1^DIC(19641.1,,"X",NAME)'=IEN S RET="-1^Duplicate name not allowed." Q
 S:NAME'="" IPT(19641.1,IEN,.01)=NAME                   ; NAME
 S:$G(TYP)'="" IPT(19641.1,IEN,.02)=TYP                 ; TYPE
 S:$G(ACT)'="" IPT(19641.1,IEN,.03)=ACT                 ; INACTIVE
 I $G(PCON)'="" D                                       ; PRIMARY CONTACT
 . I PCON S IPT(19641.1,IEN,.04)=$$GET1^DIQ(19641.1,PCON_",",.01) Q
 . S IPT(19641.1,IEN,.04)=$S(PCON="@":"@",1:$$FIND1^DIQ(19641.1,,"X",PCON))
 I $D(ADDR) D                                           ; ADDRESS
 . S CT=$NA(ADDR) F  S CT=$Q(@CT) Q:CT=""  D
 . . S LOC=$$UP^XLFSTR($P(@CT,U)),VAL=$P(@CT,U,2)
 . . I LOC[1 S IPT(19641.1,IEN,1.1)=VAL Q                 ; STREET ADDRESS 1
 . . I LOC[2 S IPT(19641.1,IEN,1.2)=VAL Q                 ; STREET ADDRESS 2
 . . I LOC[3 S IPT(19641.1,IEN,1.3)=VAL Q                 ; STREET ADDRESS 3
 . . I LOC="CITY" S IPT(19641.1,IEN,1.4)=VAL Q            ; CITY
 . . I LOC="STATE",$$FIND1^DIC(5,,"XM",VAL) D
 . . . S IPT(19641.1,IEN,1.5)=$$FIND1^DIC(5,,"XM",VAL) Q  ; STATE
 . . I LOC="ZIP" S IPT(19641.1,IEN,1.6)=VAL               ; ZIP CODE
 D UPDATE^DIE(,"IPT",$S($G(IEN):"",1:"IEN")) K IPT
 S (RET,IEN)=$S($G(IEN):+IEN,$G(IEN(1)):IEN(1),1:"") I 'IEN S RET="-1^Failed to Update." Q
 I $D(PHONE) D                                          ; PHONE
 . S CT=$NA(PHONE) F  S CT=$Q(@CT) Q:CT=""  D
 . . S LOC=$$UP^XLFSTR($P(@CT,U)) Q:"^H^MC^WP^FAX^O^"'[(U_LOC_U)
 . . S VAL=$TR($P(@CT,U,2),"()- ") Q:VAL=""!(VAL'?.N)
 . . S IPT(19641.15,"?+1,"_IEN_",",.01)=LOC               ; PHONE TYPE
 . . S IPT(19641.15,"?+1,"_IEN_",",.02)=VAL               ; NUMBER
 . . D UPDATE^DIE(,"IPT") K IPT
 Q
 ;
RENT(RET,TYP,SORT) ; RPC: DSIO GET EXTERNAL ENTITY
 ;
 ; SORT: PAGE#,CT
 ;
 ; RETURN: GLOBAL ARRAY
 ;
 ; IEN^NAME^TYPE^INACTIVE^PRIMARY PROVIDER NAME^STREET ADDRESS 1
 ; ^STREET ADDRESS 2^STREET ADDRESS 3^CITY^STATE^ZIP CODE
 ; ^PHONE NUMBER (HOME)^PHONE NUMBER (OFFICE)^PHONE NUMBER (CELL)^FAX NUMBER
 ;
 ; TYP - F        (FACILITIES)
 ;       P        (PROVIDERS)
 ;       NULL     (ALL ENTITIES)
 ;       IEN/NAME (SINGLE ENTITY)
 ;
 S RET=$NA(^TMP($J,"DSIO RENT")) K @RET S @RET@(0)="0^No entries found."
 S TYP=$$UP^XLFSTR($G(TYP))
 ; *** SINGLE (IEN)
 I TYP,$D(^DSIO(19641.1,TYP)) D R1("B",TYP) Q
 ; *** FACILITIES/PROVIDERS
 I "^F^P^"[(U_TYP_U) D R1(TYP) Q
 ; *** SINGLE (NAME)
 I TYP'="",$D(^DSIO(19641.1,"B",TYP)) D R1("B",$O(^DSIO(19641.1,"B",TYP,""))) Q
 ; *** ALL
 I TYP="" D R1("B")
 Q
 ;
R1(IX,KEY) ; Get Entity List
 N RCT,TS,CT,IEN,STRT,END D SORT($G(SORT))
 S KEY=$$GET1^DIQ(19641.1,$G(KEY)_",",.01)
 S (RCT,TS)=0,CT=$E(KEY,1,($L(KEY)-1))_$$M($E(KEY,$L(KEY)))
 F  S CT=$O(^DSIO(19641.1,IX,CT)) Q:CT=""  Q:$$KEY(CT,KEY)  D
 . S TS=TS+1 I STRT'="",TS'>STRT Q
 . S RCT=RCT+1 I END'="",RCT>END Q
 . S IEN=$O(^DSIO(19641.1,IX,CT,""))
 . S @RET@(RCT)=IEN_U_$$NAME^DSIO2(CT)_U_IX_U_$$RFLDS(IEN)
 S:$G(TS) @RET@(0)=TS
 Q
 ;
RFLDS(IEN) ; Return Transformed Fields
 ;
 ; INACTIVE^PRIMARY PROVIDER NAME^STREET ADDRESS 1^STREET ADDRESS 2^
 ; STREET ADDRESS 3^CITY^STATE^ZIP CODE^PHONE NUMBER (HOME)^
 ; PHONE NUMBER (OFFICE)^PHONE NUMBER (CELL)^FAX NUMBER
 ;
 N ST,PC,OUT,CT,FLD,STR
 S ST=$$GET1^DIQ(5,$$GET1^DIQ(19641.1,IEN_",",1.5,"I")_",",1)
 S PC=$$GET1^DIQ(19641.1,IEN_",",.04)
 D GETS^DIQ(19641.1,IEN_",",".03;1.1;1.2;1.3;1.4;1.6",,"OUT")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  S FLD($QS(CT,3))=@CT
 S STR=FLD(.03)_U_PC_U_FLD(1.1)_U_FLD(1.2)_U_FLD(1.3)_U_FLD(1.4)_U_ST_U
 S STR=STR_U_FLD(1.6)_U_$$PHONE(19641.1,IEN)
 Q STR
 ;
PHONE(FLE,IEN) ; Return Phone List
 ;           H ^ WP ^ MC ^ FAX
 N STR,OUT,CT,F,P
 S STR="^^^" D GETS^DIQ(FLE,IEN_",","2*","I","OUT")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 . I $QS(CT,3)=.01 S F=@CT Q
 . I $D(F) D
 . . S P=$S(F="H":1,F="WP":2,F="MC":3,F="FAX":4,1:"") K F
 . . S:P $P(STR,U,P)=$$FORMAT^DSIO2($TR(@CT,"()- "))
 Q STR
 ;
 ; ---------------------------------- COMMON ----------------------------------
 ;
SORT(SORT) ; Set Start and End
 D S^DSIO2(SORT)
 Q
 ;
M(VAL) ; Minus One
 I VAL?.N S VAL=VAL-1 Q VAL
 I VAL?.A Q $$ALPHA(VAL)
 Q VAL
 ;
ALPHA(AL) ; Return the Prior Alpha
 N VAL,I,X
 S VAL="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
 F I=1:1:26 I $E(VAL,I)=AL S X=I-1 Q
 Q $E(VAL,$G(X))
 ;
KEY(VAL,KEY) ; Value to Key
 I KEY="" Q 0
 I $E(VAL,1,$L(KEY))'=KEY Q 1
 Q 0
 ;
PAR ; Check Parameters
 I $$GET^XPAR("SYS","DSIO EVAL LABS NOW") D LAB^DSIO5
 I $$GET^XPAR("SYS","DSIO EVAL PROBLEMS NOW") D PROBLEM^DSIO5
 I $$GET^XPAR("SYS","DSIO EVAL CONSULTS NOW") D CONSULT^DSIO5
 Q
