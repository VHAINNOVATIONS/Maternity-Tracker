Routine DSIO0 saved using VFDXTRS routine on May 20, 2015 17:01
DSIO0^INT^63692,31237^May 20, 2015@08:40
DSIO0 ;DSS/TFF - DSIO OCNT SUPPORT;06/28/2013 15:19
 ;;1.0;DSIO 1.0;;Feb 06, 2015;Build 23
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ; LIST^ORQQAL
 ; LIST^ORQQPL
 ; LIST^ORQQPS
 ; FASTVIT^ORQQVI
 ;
 Q
 ;
PARAM(RET) ; DSIO OCNT GET LOCATION
 N OUT,ERR,SYS S RET=""
 D ENVAL^XPAR(.OUT,"DSIO OCNT LOCATION",,"ERR") Q:$D(ERR)
 I $D(OUT) D
 .S SYS=+$$GET1^DIQ(8989.3,"1,",.01,"I") D
 ..I $G(OUT(SYS_";DIC(4.2,",1))'="" S RET=OUT(SYS_";DIC(4.2,",1)
 .I $G(OUT(+$G(DUZ(2))_";DIC(4,",1))'="" S RET=OUT(+$G(DUZ(2))_";DIC(4,",1)
 Q
 ;
CANCEL(RET,TASKS) ; RPC: DSIO OCNT CANCEL AUTOSAVE
 ; THE OCNT IS CLOSED AND SAVED SO CANCEL PENDING AUTO SAVE TASKS
 ; TASKS =  ARRAY OF AUTO SAVE TASKS TO CANCEL
 S RET=-1 Q:'$D(TASKS)
 N CT,FLG S CT=$NA(TASKS) F  S CT=$Q(@CT) Q:CT=""  D
 .I '$$ASKSTOP^%ZTLOAD(@CT) S FLG=1
 S:'$D(FLG) RET=1
 Q
 ;
 ; --------------------------------ALLERGIES-----------------------------------
ORQQAL(RET,DFN) ; DSIO ORQQAL LIST
 ; GET AND FORMAT ALLERGIES
 ; allergy ien^allergen/reactant^severity^reaction/symptom
 N OUT,I,ND,NAME,P,CT S RET(0)="An unknown error occured."
 D LIST^ORQQAL(.OUT,DFN) Q:'$D(OUT)
 S RET(0)="ALLERGIES: "
 I "No Allergy Assessment,No Known Allergies,No allergies found."[$P(OUT($O(OUT(""),-1)),U,2) D  Q
 .S RET(0)=RET(0)_$P(OUT($O(OUT(""),-1)),U,2)
 S RET(1)="Allergen/Reactant"_$$REPEAT^XLFSTR(" ",25)_"| Severity   | Reactions/Symptoms"
 S RET(2)=$$REPEAT^XLFSTR("=",77)
 S I=3,ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .S NAME=$P(@ND,U,2) ; NAME UP TO 41 CHAR LENGTH
 .S RET(I)=$E(NAME,1,41)_$$REPEAT^XLFSTR(" ",42-$L($E(NAME,1,41)))
 .S RET(I)=RET(I)_"| "_$P(@ND,U,3)
 .S RET(I)=RET(I)_$$REPEAT^XLFSTR(" ",55-$L(RET(I)))
 .S RET(I)=RET(I)_"| "_$P($P(@ND,U,4),";"),P=2,I=I+1
 .I $L(NAME)>41 F CT=42:41:$L(NAME) D  S I=I+1
 ..S RET(I)=$E(NAME,CT,CT+41)_$$REPEAT^XLFSTR(" ",42-$L($E(NAME,CT,CT+41)))
 ..S RET(I)=RET(I)_"| "_$$REPEAT^XLFSTR(" ",53-$L(RET(I)))_"| "
 ..I $P(@ND,U,4)[";" S RET(I)=RET(I)_$P($P(@ND,U,4),";",P),P=P+1
 .I $L($P(@ND,U,4),";")>=P F CT=P:1:$L($P(@ND,U,4),";") D  S I=I+1
 ..S RET(I)=$G(RET(I))_$$REPEAT^XLFSTR(" ",42-$L($G(RET(I))))_"| "
 ..S RET(I)=RET(I)_$$REPEAT^XLFSTR(" ",55-$L(RET(I)))_"| "
 ..S RET(I)=RET(I)_$P($P(@ND,U,4),";",P),P=P+1
 .S I=I+1
 .; ROW LINES
 .;S RET(I)=$$REPEAT^XLFSTR("-",77),I=I+1
 Q
 ;
 ; --------------------------------ACTIVE MEDS---------------------------------
ORQQPS(RET,DFN) ; DSIO ORQQPS LIST
 ; GET AND FORMAT ACTIVE MEDICATIONS
 ; id^nameform^stop date^route^schedule/infusion rate^refills remaining
 N OUT,I,ND,NAME,P,CT S RET(0)="An unknown error occured."
 D LIST^ORQQPS(.OUT,DFN,"","") Q:'$D(OUT)
 S RET(0)="ACTIVE MEDICATIONS: "
 I $P(OUT($O(OUT(""),-1)),U,2)["No medications found." D  Q
 .S RET(0)=RET(0)_$P(OUT($O(OUT(""),-1)),U,2)
 S RET(1)="Medication"_$$REPEAT^XLFSTR(" ",20)_"| Stop Date  | Route  "
 S RET(1)=RET(1)_"| Schedule   | Refills"
 S RET(2)=$$REPEAT^XLFSTR("=",77)
 S I=3,ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .S NAME=$P(@ND,U,2) ; NAME UP TO 29 CHAR LENGTH
 .S RET(I)=$E(NAME,1,29)_$$REPEAT^XLFSTR(" ",30-$L($E(NAME,1,29)))
 .S RET(I)=RET(I)_"| "_$$FMTE^XLFDT($P(@ND,U,3),"5ZD")
 .S RET(I)=RET(I)_$$REPEAT^XLFSTR(" ",43-$L(RET(I)))
 .S RET(I)=RET(I)_"| "_$P(@ND,U,4)
 .S RET(I)=RET(I)_$$REPEAT^XLFSTR(" ",52-$L(RET(I)))
 .S RET(I)=RET(I)_"| "_$P(@ND,U,5)
 .S RET(I)=RET(I)_$$REPEAT^XLFSTR(" ",65-$L(RET(I)))
 .S RET(I)=RET(I)_"| "_$P(@ND,U,6),I=I+1
 .I $L(NAME)>29 F CT=30:29:$L(NAME) D  S I=I+1
 ..S RET(I)=$E(NAME,CT,CT+29)_$$REPEAT^XLFSTR(" ",30-$L($E(NAME,CT,CT+29)))
 ..S RET(I)=RET(I)_"| "_$$REPEAT^XLFSTR(" ",41-$L(RET(I)))
 ..S RET(I)=RET(I)_"| "_$$REPEAT^XLFSTR(" ",50-$L(RET(I)))
 ..S RET(I)=RET(I)_"| "_$$REPEAT^XLFSTR(" ",63-$L(RET(I)))_"|"
 .S I=I+1
 .; ROW LINES
 .;S RET(I)=$$REPEAT^XLFSTR("-",77),I=I+1
 Q
 ;
 ; -------------------------------ACTIVE PROBS---------------------------------
ORQQPL(RET,DFN) ; DSIO ORQQPL LIST
 ; GET AND FORMAT ACTIVE PROBLEMS
 ; ien^description^ICD^onset^last modified^SC^SpExp
 N OUT,I,ND,NAME,CT S RET(0)="An unknown error occured."
 D LIST^ORQQPL(.OUT,DFN,"A") Q:'$D(OUT)
 S RET(0)="ACTIVE PROBLEMS: "
 I $P(OUT($O(OUT(""),-1)),U,2)["No problems found." D  Q
 .S RET(0)=RET(0)_$P(OUT($O(OUT(""),-1)),U,2)
 S RET(1)="Problem"_$$REPEAT^XLFSTR(" ",45)_"| ICD        | SCT"
 S RET(2)=$$REPEAT^XLFSTR("=",77)
 S I=3,ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .S NAME=$P(@ND,U,2) ; NAME UP TO 51 CHAR LENGTH
 .S RET(I)=$E(NAME,1,51)_$$REPEAT^XLFSTR(" ",52-$L($E(NAME,1,51)))
 .S RET(I)=RET(I)_"| "_$P(@ND,U,4)
 .S RET(I)=RET(I)_$$REPEAT^XLFSTR(" ",65-$L(RET(I)))
 .S RET(I)=RET(I)_"| "_$P(@ND,U,14),I=I+1
 .I $L(NAME)>51 F CT=52:51:$L(NAME) D  S I=I+1
 ..S RET(I)=$E(NAME,CT,CT+51)_$$REPEAT^XLFSTR(" ",52-$L($E(NAME,CT,CT+51)))
 ..S RET(I)=RET(I)_"| "_$$REPEAT^XLFSTR(" ",63-$L(RET(I)))_"| "
 .S I=I+1
 .; ROW LINES
 .;S RET(I)=$$REPEAT^XLFSTR("-",77),I=I+1
 Q
 ;
 ; -----------------------------------VITALS-----------------------------------
ORQQVI(RET,DFN) ; DSIO ORQQVI VITALS
 N EDD,LMP,SEX
 I +$G(DFN)<1 S RET(0)=-1
 D FASTVIT^ORQQVI(.RET,DFN)
 S RET($O(RET(""),-1)+1)="^AGE^"_$$GET1^DIQ(2,DFN_",",.033)
 S EDD=$$FMTE^XLFDT($$GET1^DIQ(19641.03,$$GET1^DIQ(19641.13,$$PG^DSIO4(DFN)_",",.06,"I")_",",.01,"I"),"5Z")
 I EDD'="" S RET($O(RET(""),-1)+1)="^EDD^"_EDD
 S LMP=$$FMTE^XLFDT($$GET1^DIQ(19641.01,$$GET1^DIQ(19641,DFN_",",1.2,"I")_",",.01),"5Z")
 I LMP'="" S RET($O(RET(""),-1)+1)="^LMP^"_LMP
 S SEX=$$GET1^DIQ(2,DFN_",",.02) I SEX'="" S RET($O(RET(""),-1)+1)="^SEX^"_SEX
 N CT S CT=$NA(RET) F  S CT=$Q(@CT) Q:CT=""  D
 .S $P(@CT,U,4)=$$FMTE^XLFDT($P(@CT,U,4),"5Z")
 Q
 ;
 ; ------------------------------------BASE------------------------------------
 ;
POP(RET,FILENAME,CONTROL,DFN) ; DSIO OCNT POPULATE
 ;
 ; LOOK IN THE OCNT CONFIGURATION FILE FOR THE CONTROL AND RETURN
 ; THE CONFIGURATION/STARTUP VALUES
 ; THESE VALUES WILL OVERRIDE THE EXISTING REPORTITEM OR BE USED TO CREATE
 ; ON IF ONE DOES NOT ALREADY EXIST
 ;
 ; 0: DISPLAY TEXT^ORDER^VISTA LABEL^PREFIX^SUFFIX^TITLE^DIALOG RETURN^REQUIRED^
 ;    DO NOT SAVE^HIDE FROM NOTE
 ; #: <ITEM DISPLAY>^UNIT NAME (IF A DIALOG)
 ;
 N IEN,EN,ROU,SHARE,OUT,I,ITM,FLG
 S RET=$NA(^TMP($J,"OCNT POP")) K @RET S @RET@(0)=-1
 S CONTROL=$$UP^XLFSTR($G(CONTROL)) Q:CONTROL=""
 S IEN=$$FIND1^DIC(19641.48,,"X",$G(FILENAME)) Q:IEN<1
 S EN=$$FIND1^DIC(19641.481,","_IEN_",","X",CONTROL) Q:EN<1
 S ROU=$$GET1^DIQ(19641.481,EN_","_IEN_",",4)
 I ROU'=""  X ROU I @RET@(0)'=-1 Q
 D GETS^DIQ(19641.481,EN_","_IEN_",","**","IE","OUT") Q:'$D(OUT)
 S I=1,ITM=$NA(OUT) F  S ITM=$Q(@ITM) Q:ITM=""  D
 .; REPORT ITEM
 .I $QS(ITM,1)=19641.481 D
 ..I $QS(ITM,3)=.02 S FLG=@ITM Q
 ..Q:$QS(ITM,3)=.01!($QS(ITM,4)="I")!($QS(ITM,3)=.03)
 ..; SINGLE VALUE DISPLAY TEXT
 ..I $QS(ITM,3)=2,$QS(ITM,4)=1 S @RET@(0)=$S($G(FLG)="S":@ITM,1:"") Q
 ..I $QS(ITM,3)["." S @RET@(0)=$S($G(@RET@(0))=-1:"",1:$G(@RET@(0)))_U_@ITM
 .; MULTIPLE
 .I $QS(ITM,3)=2,"S,DL"'[$G(FLG),$QS(ITM,4)?.N S @RET@(I)=@ITM,I=I+1 Q
 .; DIALOG LIST/PAGE (DIALOG PAGE)
 .Q:"DL,P"'[$G(FLG)
 .I $QS(ITM,1)=19641.4811 D
 ..I $QS(ITM,4)="E" S @RET@(I)=@ITM,I=I+1 Q
 ..I $QS(ITM,4)="I" S @RET@(I-1)=$G(@RET@(I-1))_U_$$GET1^DIQ(19641.49,@ITM_",",.03)
 S SHARE=$$GETDATA(EN,IEN) I +$G(SHARE)>0 D POPOTHR(+SHARE,$P(SHARE,U,2),CONTROL)
 Q
 ;
POPOTHR(NIEN,FILENAME,COM) ; POPULATE FROM OTHER CONTROL
 N OUT,CT,ND,SRET,I
 D RESTORE(.OUT,NIEN,FILENAME) Q:'$D(OUT)
 I FLG="WP" S SRET=@RET@(0) K @RET S @RET@(0)=SRET,I=1
 S CT=$NA(@OUT) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,2)'="OCNT RESTORE"  D
 .I $P(@CT,U)=$G(COM) D
 ..I FLG="S" S $P(@RET@(0),U)=@CT Q
 ..I FLG="WP" S @RET@(I)=$P(@CT,U,3,9999),I=I+1 Q
 ..S ND=$$FINDND($P(@CT,U,4,9999)) I +ND>0 D
 ...S @RET@(ND)=$P(@CT,U,3,9999)
 ...I $P(@CT,U,2) S $P(@RET@(0),U,11)=1 ; ADD INDEXING
 Q
 ;
FINDND(RPV) ; FIND THE NODE TO REPLACE CONFIGRATION DATA WITH RESTORATION DATA
 N CT,FLG
 S CT=$NA(@RET) F  S CT=$Q(@CT) Q:CT=""  Q:$QS(CT,2)'="OCNT POP"  D  Q:$D(FLG)
 .I @CT=RPV S FLG=$QS(CT,3)
 Q $G(FLG)
 ;
GETDATA(IENS,IEN) ; GET SHARED COMPONENT DATA
 ;
 ; IN THIS CASE CONFIGUATION DATA BECOMES RESTORE DATA FROM ANOTHER NOTE
 ; IS THIS INTERFACE,CONTROL TRYING TO GET DATA FROM SOMETHING ELSE?
 ;
 ; SO WE HAVE A INTERFACE AND A COMPONENT AND IDENTIFY THAT ANOTHER INTERFACE
 ; COMPONENT THAT WE CAN GET DATA FROM BUT WE STILL HAVE TO DETERMINE WHAT
 ; NOTE HAS THAT DATA
 ;
 ; ^DSIO(19641.41,"C","oCNT_OBHP.dll",89,5134,1)=""
 ;  = INTERFACE,TITLE IEN,NOTE IEN
 ; ^DSIO(19641.41,"D","oCNT_OBHP.dll",10000000034,5119,1)=""
 ;  = INTERFACE,DUZ,NOTE IEN
 ; ^DSIO(19641.41,"CD","oCNT_OBHP.dll",10000000034,89,5119,1)=""
 ;  = INTERFACE,DUZ,TITLE IEN, NOTE IEN
 ; ^DSIO(19641.41,"L","oCNT_OBHP.dll",5119,1)=""
 ;  = INTERFACE,NOTE IEN
 ;
 ;     IEN = FILENAME(INTERFACE) IEN
 ; CONTROL = CURRENT CONTROL NAME
 ;
 ;       GET - 1,29 (INTERFACE,COMPONENT)
 ;           FROM - GET TITLE AND/OR DUZ STATUS
 ;                  IF TITLE THEN USE "C"
 ;                  IF DUZ THEN USE "D"
 ;                  IF BOTH THEN USE "CD"
 ;                  IF NONE THEN USE "L" - LAST NOTE BY INTERFACE
 ; DETERMINE - FROM THE C,D,CD,L X-REF WHICH NOTE TO GET FROM
 ;
 N SHARE,CT,OUT,FNAME,COM,TITLE,USER,NEW
 D LIST^DIC(19641.4815,","_IENS_","_IEN_",","@;.05;.01;.02;.03I;.04I","P",,,,"P",,,"SHARE")
 S CT=$NA(SHARE) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,2)<1
 .; OUT(PRIORITY)=FNAME^CONTROL^TITLE^USER
 .S OUT(+$P(@CT,U,2))=$P(@CT,U,3)_U_$P(@CT,U,4)_U_$P(@CT,U,5)_U_$P(@CT,U,6)
 Q:'$D(OUT) ""
 S CT="" F  S CT=$O(OUT(CT),-1) Q:CT=""  D
 .S FNAME=$P(OUT(CT),U),COM=$P(OUT(CT),U,2)
 .S TITLE=$P(OUT(CT),U,3),USER=$P(OUT(CT),U,4)
 .Q:FNAME=""
 .I $G(TITLE),$G(USER) S NIEN=$O(^DSIO(19641.41,"CD",FNAME,USER,TITLE,""),-1)
 .E  I '$G(TITLE),$G(USER) S NIEN=$O(^DSIO(19641.41,"D",FNAME,USER,""),-1)
 .E  I '$G(USER),$G(TITLE) S NIEN=$O(^DSIO(19641.41,"C",FNAME,TITLE,""),-1)
 .E  S NIEN=$O(^DSIO(19641.41,"L",FNAME,""),-1)
 .S NEW(CT)=$G(NIEN)_U_FNAME_U_$$GET1^DIQ(8925,$G(NIEN)_",",.05)
 K NIEN S CT="" F  S CT=$O(NEW(CT)) Q:CT=""  D  Q:$D(NIEN)
 .I $P(NEW(CT),U,3)="COMPLETED" S NIEN=+NEW(CT)_U_$P(NEW(CT),U,2) Q
 Q $G(NIEN)
 ;
RESTORE(RET,NIEN,FILENAME,DIEN,DFN) ; DSIO OCNT RESTORE
 ;
 ; RESTORE THE DISCREET VALUES OF A LAST USED OCNT
 ; CONTROL ^ INDEXED (TRUE(1) OR FALSE(0)) ^ VALUE (CAN HAVE ^ IF INDEXED THEN
 ; THE FIRST PIECE IS THE INDEX)
 ;
 N DATALST,FLE,MIENS,OUT,TMP,I,ND,IENS,CON,CK,INDEX,MULT,VAL
 S RET=$NA(^TMP($J,"OCNT RESTORE")) K @RET S @RET@(0)=""
 I $G(DIEN) D
 .I $$GET1^DIQ(19641.49,+DIEN_",",.05,"I")="DATA" D
 ..S DATALST=$O(^DSIO(19641.49,DIEN,1,"E",""))
 .I $$GET1^DIQ(19641.49,DIEN_",",.04,"I") D  Q
 ..S FLE=19641.494121,MIENS=","_DIEN_","_DFN_","
 .S FLE=19641.4121,MIENS=","_DIEN_","_NIEN_","
 E  I '$G(DIEN) D
 .S FLE=19641.4111
 .S MIENS=","_$$FIND1^DIC(19641.411,","_NIEN_",","X",$G(FILENAME))_","_NIEN_","
 D LIST^DIC(FLE,MIENS,,"P",,,,,,,"OUT") Q:'$D(OUT)
 S ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .Q:$QS(ND,2)<1
 .S TMP($P(@ND,U,2))=$$GET1^DIQ(FLE,+@ND_MIENS,.01,"I")
 S I=0,ND="" F  S ND=$O(TMP(ND)) Q:ND=""  D
 .K IENS S IENS=TMP(ND),CON=$$GET1^DIQ(19641.45,IENS_",",2.4) Q:CON=""
 .I $G(DIEN) Q:'$$ACTION^DSIO6(DIEN,$G(NIEN),"RESTORE",$$GET1^DIQ(19641.45,IENS_",",2.1,"I"))
 .Q:$D(CK(CON))  S CK(CON)=""
 .I $D(DATALST),CON'=DATALST Q
 .S INDEX=$$GET1^DIQ(19641.45,IENS_",",.02,"I")
 .I $$GET1^DIQ(19641.45,IENS_",",2.5,"I")="M" S INDEX=1
 .K MULT D GETS^DIQ(19641.45,IENS_",",1,"E","MULT") Q:'$D(MULT)
 .K MULT(19641.45,IENS_",",1,"E")
 .S VAL=$NA(MULT) F  S VAL=$Q(@VAL) Q:VAL=""  D
 ..S @RET@(I)=CON_U_INDEX_U_$TR(@VAL,$C(13,10),"|"),I=I+1
 Q
 ;
CIPT(RET,FILENAME,CONTROLS) ; DSIO OCNT IMPORT
 N IPT,IEN,CTR S RET=-1 Q:$G(FILENAME)=""
 S IPT(19641.48,"?+1,",.01)=FILENAME
 D UPDATE^DIE("E","IPT","IEN") K IPT Q:$G(IEN(1))=""!('$D(CONTROLS))
 S CTR="" F  S CTR=$O(CONTROLS(CTR)) Q:CTR=""  D
 .S IPT(19641.481,"?+1,"_IEN(1)_",",.01)=$$UP^XLFSTR($P(CONTROLS(CTR),U))
 .S IPT(19641.481,"?+1,"_IEN(1)_",",.02)=$P(CONTROLS(CTR),U,2)
 .D UPDATE^DIE("E","IPT") K IPT
 S RET=1
 Q
 ;
CLEAR(RET,NIEN,FILENAME) ; DSIO OCNT CLEAR NOTE DATA
 N FLE,DIK,INTF,OUT,ND,DA,IPT S RET=1
 S FLE=19641.4111,DIK="^DSIO(19641.45,"
 S INTF=$$FIND1^DIC(19641.411,","_NIEN_",","X",$G(FILENAME))
 D LIST^DIC(FLE,","_INTF_","_NIEN_",",,"P",,,,,,,"OUT")
 S ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .Q:$QS(ND,2)<1
 .S DA=@ND D ^DIK
 S IPT(19641.411,INTF_","_NIEN_",",.01)="" D FILE^DIE(,"IPT") K IPT
 K OUT D GETS^DIQ(19641.41,NIEN_",","2*","I","OUT")
 S ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .I $QS(ND,1)=19641.412 S IPT(19641.412,$QS(ND,2),.01)="" Q
 .S DA=@ND D ^DIK
 D:$D(IPT) FILE^DIE(,"IPT")
 Q
 ;
DELETE(NIEN) ; DSIO OCNT DELETE
 N FLE,DIK,OUT,ND,DA
 S FLE=19641.4111,DIK="^DSIO(19641.45,"
 D GETS^DIQ(19641.41,NIEN_",","1*","I","OUT")
 S ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .I $QS(ND,1)=19641.411 D  Q
 .S DA=@ND D ^DIK
 K OUT D GETS^DIQ(19641.41,NIEN_",","2*","I","OUT")
 S ND=$NA(OUT) F  S ND=$Q(@ND) Q:ND=""  D
 .I $QS(ND,1)=19641.412 D  Q
 .S DA=@ND D ^DIK
 S DIK="^DSIO(19641.41,",DA=NIEN D ^DIK
 Q
 ;
 ; -----------------------------------DIALOG-----------------------------------
 ;
DIALK(RET,CLASS,IEN) ; DSIO OCNT DIALOG LOOKUP
 ; DIALOG LOOKUP BY CLASSNAME
 ; RET: IEN ^ SHARE STATUS ^ ACTION
 S RET=$$FIND1^DIC(19641.49,,"X",$G(CLASS),"C") I +RET<1 S RET=+$G(IEN)
 S RET=RET_U_+$$GET1^DIQ(19641.49,+RET_",",.04,"I")
 S RET=RET_U_$$GET1^DIQ(19641.49,+RET_",",.05,"I")
 Q
 ;
POPD(RET,IEN,CONTROL,DFN) ; DSIO OCNT DIALOG POPULATE
 ;
 ; LOOK IN THE OCNT DIALOGS FILE FOR THE CONTROL AND RETURN
 ; THE CONFIGURATION/STARTUP VALUES
 ;
 ; 0: DISPLAY TEXT
 ; #: <ITEM DISPLAY>
 ;
 N EN,ROU,OUT,I,ITM,FLG,TYP
 S RET=$NA(^TMP($J,"OCNT POPD")) K @RET S @RET@(0)=-1 Q:+$G(IEN)<1
 S CONTROL=$$UP^XLFSTR($G(CONTROL)) Q:CONTROL=""
 S EN=$$FIND1^DIC(19641.491,","_IEN_",","X",CONTROL) Q:EN<1
 S ROU=$$GET1^DIQ(19641.491,EN_","_IEN_",",4)
 I ROU'="" X ROU I $G(@RET@(0))'=-1 Q
 I $$GET1^DIQ(19641.491,EN_","_IEN_",",.02,"I")="DL" D  Q
 .S @RET@(0)="" D GETS^DIQ(19641.491,EN_","_IEN_",","1*","I","OUT") Q:'$D(OUT)
 .S I=1,ITM=$NA(OUT) F  S ITM=$Q(@ITM) Q:ITM=""  D
 ..S @RET@(I)=@ITM_U_$$GET1^DIQ(19641.49,@ITM_",",.01)
 ..S @RET@(I)=@RET@(I)_U_$$GET1^DIQ(19641.49,@ITM_",",.03),I=I+1
 D GETS^DIQ(19641.491,EN_","_IEN_",","*","E","OUT") Q:'$D(OUT)
 K OUT(19641.491,EN_","_IEN_",",2,"E")
 S I=1,ITM=$NA(OUT) F  S ITM=$Q(@ITM) Q:ITM=""  D  Q:$D(FLG)
 .I $QS(ITM,3)=.02 S TYP=@ITM D  Q
 ..I @ITM="SINGLE" S @RET@(0)=$G(OUT(19641.491,EN_","_IEN_",",2,1)),FLG=1
 .Q:$QS(ITM,3)<1  Q:$G(TYP)=""
 .I TYP="LIST VIEW" S @RET@(I-1)=@ITM,I=I+1 Q
 .S @RET@(0)=""
 .S @RET@(I)=@ITM,I=I+1 Q
 Q
 ;
DIPT(RET,UNIT,CLASS,CONTROLS) ; DSIO OCNT DIALOG IMPORT
 N VAL,IPT,OUT,CT,CTR S RET=-1 Q:$G(UNIT)=""!($G(CLASS)="")
 S VAL(1)=UNIT,VAL(2)=CLASS
 I '$$FIND1^DIC(19641.49,,"X",.VAL,"D") D
 .S IPT(19641.49,"?+1,",.01)="<"_$E(UNIT,5,$L(UNIT))_">"
 .S IPT(19641.49,"?+1,",.02)=UNIT
 .S IPT(19641.49,"?+1,",.03)=CLASS
 .D UPDATE^DIE("E","IPT") K IPT
 ; SINCE DIALOGS CAN BE REUSED WITH DIFFERENT DISPLAY NAMES
 ; FIND ALL DIALOGS OF THIS UNIT/CLASS AND ADD THEIR CONTROLS
 Q:'$D(CONTROLS)
 D FIND^DIC(19641.49,,"@","P",.VAL,,"D",,,"OUT")
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,2)<1
 .S CTR="" F  S CTR=$O(CONTROLS(CTR)) Q:CTR=""  D
 ..S IPT(19641.491,"?+1,"_@CT_",",.01)=$$UP^XLFSTR($P(CONTROLS(CTR),U))
 ..S IPT(19641.491,"?+1,"_@CT_",",.02)=$P(CONTROLS(CTR),U,2)
 ..D UPDATE^DIE("E","IPT") K IPT
 S RET=1
 Q
