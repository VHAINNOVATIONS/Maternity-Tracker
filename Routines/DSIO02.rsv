DSIO02 ;DSS/TFF - DSIO OCNT PUSH UTILITIES;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**1**;Feb 06, 2015;Build 19
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
 ; ----------------------- RUN ROUTINE FOR CONFIGURATION ----------------------
 ;
VIT(VITAL) ; GET VITAL FOR CONTROL
 ; DFN, IEN = DIALOG IEN
 N OUT,CT S @RET@(0)=-1 Q:'$G(DFN)
 D FASTVIT^ORQQVI(.OUT,DFN)
 N CT S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$G(@RET@(0))'=-1
 . I $P(@CT,U,2)=VITAL S @RET@(0)=$P(@CT,U,3) Q
 Q
 ;
GETOBS(CAT,CD) ; GET OBSERVATIONS
 D OBG^DSIO10(.RET,,DFN,,,,,,$G(CAT),,$G(CD))
 K:+$G(@RET@(0))<1 @RET
 S @RET@(0)="IEN^DATE^PATIENT^DFN^BABY^BABY IEN^PREGNANCY^PREGNANCY IEN^CATEGORY^"
 S @RET@(0)=@RET@(0)_"CODE^SYSTEM^UPDATED BY^UPDATEDBY IEN^DESCRIPTION^VALUE"
 Q
 ;
PERSON ; GET LIST OF PEOPLE ASSOCIATED WITH THE PATIENT - DSIO PERSON
 N OUT,I,CT,FLG
 D PERSON^DSIO9(.OUT,DFN,,1)
 S @RET@(0)="IEN^NAME^SEX^DOB^EDUCATION^STREET1^STREET2^STREET3^CITY^STATE^ZIP^PHONE HOME^"
 S @RET@(0)=@RET@(0)_"PHONE CELL^PHONE WORK^PATIENT^DFN^RELATIONSHIP^STATUS^DIAGNOSIS"
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$D(FLG)
 . I $$UP^XLFSTR(@CT)["NOTHING FOUND" S FLG=1 Q
 . I @CT[U S @RET@(I)=@CT D
 . . S $P(@RET@(I),U,10)=$$GET1^DIQ(19641.11,+@RET@(I)_",",1.5),I=I+1
 Q
 ;
PREG ; GET LIST OF HISTORICAL PREGNANCIES
 N OUT,I,CT,FLG,J,FLD,STR,T,STR1
 D PREGG^DSIO15(.OUT,,DFN)
 S @RET@(0)="IEN^STRT^PATIENT^DFN^TYP^FOF^FOF IEN^EDD^END^OBP^OBP IEN^PFAC^PFAC IEN^"
 S @RET@(0)=@RET@(0)_"UPDATE BY^UPDATE BY IEN^BABY;IEN;GENDER;WEIGTH;STILLBORN|^"
 S @RET@(0)=@RET@(0)_"GESTATIONAL AGE AT DELIVERY^LENGTH OF LABOR^TYPE OF DELIVERY^"
 S @RET@(0)=@RET@(0)_"ANESTHESIA^PLACE OF DELIVERY^PRETERM LABOR^BIRTH TYPE^COMMENTS"
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$D(FLG)
 . I $$UP^XLFSTR(@CT)["NOTHING FOUND" S FLG=1 Q
 . Q:$P(@CT,U,5)="C"
 . S @RET@(I)=@CT
 . S FLD=3.1 F J=17:1:22 S $P(@RET@(I),U,J)=$$GET1^DIQ(19641.13,+@CT_",",FLD),FLD=FLD+.1
 . S $P(@RET@(I),U,23)=$$GET1^DIQ(19641.13,+@CT_",",999.1)
 . S $P(@RET@(I),U,24)=$$GET1^DIQ(19641.13,+@CT_",",4)
 . ; UPDATE THE BABIES (BABY;IEN;GENDER;WEIGTH;STILLBORN|)
 . S STR=$P(@RET@(I),U,16) F J=1:1:$L(STR,"|") S STR1=$P(STR,"|",J) D  S $P(STR,"|",J)=STR1
 . . S FLD=.05 F T=3:1:5 D  S FLD=FLD+.01
 . . . S $P(STR1,";",T)=$$GET1^DIQ(19641.112,$P(STR1,";",2)_",",FLD,$S(T=3:"I",1:""))
 . S $P(@RET@(I),U,16)=STR,I=I+1
 Q
 ;
BAY ; GET BABIES FOR CURENT PREGNANCY
 Q:'$$PG^DSIO4(DFN)
 N OUT,CT,I,BIEN,BW,BW1,L,BW2
 S @RET@(0)="IEN^NUMBER^GENDER^WEIGHT^STILLBORN^DEMISE^APGAR^NICU^COMMENTS"
 D LIST^DIC(19641.132,","_$$PG^DSIO4(DFN)_",","@;.01I","P",,,,,,,"OUT")
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 . Q:$QS(CT,2)<1
 . S BIEN=$P(@CT,U,2)
 . S @RET@(I)=BIEN_U_$$GET1^DIQ(19641.112,BIEN_",",.01)_U_$$GET1^DIQ(19641.112,BIEN_",",.05,"I")
 . S BW=$$UP^XLFSTR($$GET1^DIQ(19641.112,BIEN_",",.06)) I BW'?.N1"|".N1"|".N D
 . . S BW1=$$TRIM^XLFSTR($P(BW,"LB"))_"|"_$$TRIM^XLFSTR($P(BW,"OZS"))_"|"_$$TRIM^XLFSTR($P(BW,"GRAMS"))
 . . F L=$L(BW1):-1:1 D
 . . . I $E(BW1,L)="|" S BW2=$E(BW1,L)_$G(BW2) K FLG Q
 . . . I '$D(FLG),$E(BW1,L)?.N S BW2=$E(BW1,L)_$G(BW2) Q
 . . . S FLG=1
 . S @RET@(I)=@RET@(I)_U_$G(BW2,BW)_U_$$GET1^DIQ(19641.112,BIEN_",",.07,"I")_"^^^^"
 . S I=I+1
 Q
 ;
PAT(FLD) ; GET PATIENT INFORMATION
 S RET=$NA(^TMP($J,"OCNT PAT")) K @RET S @RET@(0)="" Q:$G(FLD)=""
 N R D INFO^DSIO1(.R,$G(DFN)) S:+$P($G(R(FLD)),U)>0 @RET@(0)=$P(R(FLD),U,2)
 Q
 ;
MH(FLD) ; GET CURRENT MENSTRUAL HISTORY FIELD
 ;
 ; THE FOLLOWING ARE CHECKBOXES AND ONLY ONE SHOULD BE RETURNED WITH A VALUE
 ; 1.1, 1.3, 1.4 (N(ORMAL)/A(BNORMAL))
 ; 1.5(Y(ES)/N(O))
 ;
 N IEN S RET=$NA(^TMP($J,"OCNT MH")) K @RET
 S IEN=$$GET1^DIQ(19641,DFN_",",1.2,"I") Q:+IEN<1
 I $$GET1^DID(19641.01,FLD,,"TYPE")="WORD-PROCESSING" D  Q
 . K @RET D GETS^DIQ(19641.01,IEN_",",2,"E",RET)
 . K @RET@(19641.01,IEN_",",2,"E")
 I $$GET1^DID(19641.01,FLD,,"TYPE")="DATE/TIME" D  Q
 . S @RET@(0)=$$FMTE^XLFDT($$GET1^DIQ(19641.01,IEN_",",FLD,"I"),"5Z")
 S @RET@(0)=$$GET1^DIQ(19641.01,IEN_",",FLD)
 I CONTROL="CKMENSESYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKMENSESNO",@RET@(0)="NORMAL" S @RET@(0)=""
 I CONTROL="CKAMOUNTYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKAMOUNTNO",@RET@(0)="NORMAL" S @RET@(0)=""
 I CONTROL="CKDURATIONYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKDURATIONNO",@RET@(0)="NORMAL" S @RET@(0)=""
 I CONTROL="CKCONTRACEPTIONYES",@RET@(0)="ABNORMAL" S @RET@(0)=""
 I CONTROL="CKCONTRACEPTIONNO",@RET@(0)="NORMAL" S @RET@(0)=""
 Q
 ;
EDU ; GET EDUCATION
 N OUT,I,CT
 D LIST^DIC(19641.8,,"@;.01","P",,,,,,,"OUT") Q:'$D(OUT)
 S I=1,CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 . Q:$QS(CT,2)<1
 . S @RET@(I)=$P(@CT,U,2),I=I+1
 Q
 ;
 ; ------------------------------ UTILITIES -----------------------------------
 ;
VAL(CTR) ; GET VALUE BY CONTROL NAME
 N CT,NVAL,ND,ND1
 S NVAL=$NA(^TMP($J,"DSIO VAL")) K @NVAL
 S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 .Q:$QS(CT,1)=19641.49412
 .I $$UP^XLFSTR($$GET1^DIQ(19641.45,@CT_",",2.4))=CTR D
 ..D GETS^DIQ(19641.45,@CT_",",1,"E",NVAL)
 ..K @NVAL@(19641.45,@CT_",",1,"E")
 ..S ND=$NA(@NVAL@(19641.45,@CT_",",1)) M ND1=@ND K @NVAL M @NVAL=ND1
 Q $G(@NVAL@(+$O(@NVAL@(""))))
 ;
CIEN(CTR) ; GET CONTROL IEN
 N CT,FLG S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D  Q:$D(FLG)
 .Q:$QS(CT,1)=19641.49412
 .I $$UP^XLFSTR($$GET1^DIQ(19641.45,@CT_",",2.4))=CTR S FLG=@CT
 Q $G(FLG)
 ;
VALWP(CTR,FLE,FLD,XIEN) ; TRANSFER A WORD PROCESSING FIELD
 N CONIEN,NVAL,ND,ND1
 S CONIEN=$$CIEN(CTR),NVAL=$NA(^TMP($J,"DSIO VAL")) K @NVAL
 D GETS^DIQ(19641.45,CONIEN_",",1,"E",NVAL)
 K @NVAL@(19641.45,CONIEN_",",1,"E")
 S ND=$NA(@NVAL@(19641.45,CONIEN_",",1)) M ND1=@ND K @NVAL M @NVAL=ND1
 D WP^DIE(FLE,XIEN_",",FLD,"K",NVAL)
 Q
