Routine DSIO02 saved using VFDXTRS routine on Oct 09, 2015 10:03
DSIO02^INT^63830,47942^Oct 05, 2015@13:19
DSIO02 ;DSS/TFF - DSIO FORM CONFIGURATION;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**1,2**;Feb 06, 2015;Build 18
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ; External References      DBIA#
 ; -------------------      -----
 ;
 ;
 ;
 Q
 ;
SUM ; Get Summary for Overview
 ; *19641.42 : OB H&P CONSULT, OB H&P INITIAL, OB FOLLOWUP NOTE : MEMSUMMARY
 ;
 ; Summary statement sample: 29 yo G4P3003 at 35 5/7 weeks here for a return OB visit.
 ;  So, include: patient age, GA value, weeks and days (as fraction) of fetus age (35 5/7),
 ;  and reason (initial or return visit).
 ;
 N STR,PREG
 S STR=$$GET(2,.01)_" is a "_$$GET(2,.033)_"yr old "_$$GET(2,.02)_" patient with a "_$$GET(19641,.04)
 S PREG=$$PG^DSIO4(DFN)
 I 'PREG S STR=STR_" and is not currently pregnant but is here for a return OB visit."
 E  S STR=STR_" and is at "_$$GA_" here for a return OB visit."
 S @RET@(DDCSCT)="CV^"_DDCSCON_"^F^^"_STR
 Q
 ;
GET(FLE,FLD) ; GET1^DIQ for 2 and 19641 only
 N ANS
 S ANS=$$GET1^DIQ(FLE,DFN_",",FLD) Q:ANS'="" ANS
 Q "<NO DATA>"
 ;
GA() ; Get patient's GA
 N EDD,IEN,GA,DATE
 S EDD=$$GET1^DIQ(19641.13,PREG_",",.06,"I") Q:'EDD "<NO DATA>"
 S EDD=$$GET1^DIQ(19641.03,EDD_",",.01,"I")
 S IEN(1)=$O(^DSIO(19641.4941,"EDD",EDD,DFN,""),-1) Q:'IEN(1) "<NO DATA>"
 S IEN(2)=$O(^DSIO(19641.4941,"EDD",EDD,DFN,IEN(1),""),-1) Q:'IEN(2) "<NO DATA>"
 S GA=$P($G(^DSIO(19641.4941,DFN,2,IEN(1),1,IEN(2),0)),U,3) Q:GA="" "<NO DATA>"
 S DATE=$$FMTE^XLFDT($$GET1^DIQ(8925,+$G(^DSIO(19641.4941,DFN,2,IEN(1),0))_",",1201,"I"),"5Z")
 Q GA_" as of "_DATE
 ;
PREG ; Get list of Historical Pregnancies
 ; *19641.49 : PREGNANCY HISTORY : PREGLISTVIEW
 ;
 ;  L^IEN^DATE RECORDED^EDC^DFN|PATIENT^STATUS^FOF|(IEN OR IDENTIFIER)^
 ;    EDD^PREGNANCY END^OB IEN|OB^FACILITY IEN|FACILITY^
 ;    UPDATED BY IEN|UPDATED BY^GESTATIONAL AGE AT DELIVERY^LENGTH OF DELIVERY^
 ;    TYPE OF DELIVERY^EPIDERAL/SPINAL^PRETERM LABOR^BIRTH TYPE^IEN;BABY#|IEN;BABY#^
 ;    COMPLICATION(E,AI,AS)^HIGH RISK FLAG(0,1)
 ;  C^IEN^COMMENT
 ;
 N OUT,CT,LN,I,STR,CTC
 S @RET@(DDCSCT)="CV^"_DDCSCON_"^F^0^ID^IEN^DATE RECORDED^EDC^DFN|PATIENT^STATUS^FOF|(IEN OR IDENTIFIER)^"
 S @RET@(DDCSCT)=@RET@(DDCSCT)_"EDD^PREGNANCY END^OB IEN|OB^FACILITY IEN|FACILITY^UPDATED BY IEN|UPDATED BY^"
 S @RET@(DDCSCT)=@RET@(DDCSCT)_"GESTATIONAL AGE AT DELIVERY^LENGTH OF DELIVERY^TYPE OF DELIVERY^EPIDERAL/SPINAL^"
 S @RET@(DDCSCT)=@RET@(DDCSCT)_"PRETERM LABOR^BIRTH TYPE^IEN;BABY#;SEX;WEIGHT;STILLBORN|^COMPLICATION^HIGH RISK FLAG"
 D PREGG^DSIO15(.OUT,,DFN) Q:'$G(@OUT@(0))
 S DDCSCT=DDCSCT+1,CT=0 F  S CT=$O(@OUT@(CT)) Q:'CT  S LN=@OUT@(CT) D
 . Q:$P(LN,U,6)="CURRENT"!($P(LN,U)'="L")
 . I $P(LN,U,19)'="" D  S $P(LN,U,19)=STR
 . . S STR=$P(LN,U,19) F I=1:1:$L(STR,"|") I $P(STR,"|",I) D
 . . . S $P(STR,"|",I)=$P(STR,"|",I)_";"_$TR($P($G(^DSIO(19641.112,+$P(STR,"|",I),0)),U,5,7),U,";")
 . F I=16,17 S $P(LN,U,I)=$S($P(LN,U,I)="YES":1,1:0)
 . S @RET@(DDCSCT)="CV^"_DDCSCON_"^F^^"_LN,DDCSCT=DDCSCT+1
 . I $O(@OUT@(CT,"")) S CTC=0 F  S CTC=$O(@OUT@(CT,CTC)) Q:'CTC  D
 . . I $P(@OUT@(CT,CTC),U)="C" S @RET@(DDCSCT)="CV^"_DDCSCON_"^F^^"_@OUT@(CT,CTC),DDCSCT=DDCSCT+1
 K @OUT
 Q
 ;
PERSON ; Get list of people associated with the patient
 ; *19641.49 : FAMILY HISTORY : LVPERSONLIST
 ;
 ;  IEN^NAME^SEX^DOB^EDUCATION^STREET1^STREET2^STREET3^CITY^STATE^
 ;  ZIP^PHONE HOME^PHONE WORK^PHONE CELL^PHONE FAX^PATIENT^DFN
 ;
 N OUT,CT,LN
 S @RET@(DDCSCT)="CV^"_DDCSCON_"^F^0^IEN^NAME^SEX^DOB^EDUCATION^STREET1^STREET2^STREET3^CITY^"
 S @RET@(DDCSCT)=@RET@(DDCSCT)_"STATE^ZIP^PHONE HOME^PHONE WORK^PHONE CELL^PHONE FAX^PATIENT^"
 S @RET@(DDCSCT)=@RET@(DDCSCT)_"DFN^RELATIONSHIP^STATUS^DIAGNOSIS|^COMMENTS"
 D PERSON^DSIO9(.OUT,DFN,,1) Q:'$G(OUT(0))
 S DDCSCT=DDCSCT+1,CT=0 F  S CT=$O(OUT(CT)) Q:'CT  S LN=OUT(CT) D
 . S @RET@(DDCSCT)="CV^"_DDCSCON_"^F^^"_LN
 . S $P(@RET@(DDCSCT),U,14)=$$GET1^DIQ(19641.11,+LN_",",1.5)
 . S $P(@RET@(DDCSCT),U,25)=$$GET1^DIQ(19641.11,+LN_",",4)
 . S DDCSCT=DDCSCT+1
 Q
 ;
VIT(VITAL) ; Get vital
 ; *19641.49 : OB FLOW SHEET : LBLWT,LBLBP
 ;
 N OUT,CT Q:$G(VITAL)=""
 D FASTVIT^ORQQVI(.OUT,DFN)
 N CT S CT=$NA(OUT) F  S CT=$Q(@CT) Q:CT=""  D
 . I $P(@CT,U,2)=VITAL S @RET@(DDCSCT)="CV^"_DDCSCON_"^F^^"_$P(@CT,U,3),DDCSCT=DDCSCT+1
 Q
 ;
BABY ; Get babies for current pregnancy
 ; *19641.42 : NURSE POSTPARTUM-DELIVERY : LSTDELIVERY
 ;
 N PG,CT,STR,WP
 S PG=$$PG^DSIO4(DFN) Q:'PG
 S @RET@(DDCSPG,DDCSCI,DDCSCT)="CV^"_DDCSCON_"^F^0^ID^IEN^NUMBER^NAME^GENDER^BIRTH WEIGHT^STILLBORN^APGAR1^APGAR2^STATUS^NICU"
 S DDCSCT=DDCSCT+1,CT=0 F  S CT=$O(^DSIO(19641.112,"PG",PG,CT)) Q:'CT  D
 . S STR=$G(^DSIO(19641.112,CT,0)),$P(STR,U,6)=$$WT
 . S @RET@(DDCSPG,DDCSCI,DDCSCT)="CV^"_DDCSCON_"^F^^L^"_CT_U_$P(STR,U)_U_$P(STR,U,4,9)_U_$P(STR,U,11,12)
 . S DDCSCT=DDCSCT+1
 . I $P($G(^DSIO(19641.112,CT,1,0)),U,4) D
 . . S WP=0 F  S WP=$O(^DSIO(19641.112,CT,1,WP)) Q:'WP  D
 . . . S @RET@(DDCSPG,DDCSCI,DDCSCT)="CV^"_DDCSCON_"^F^^C^"_CT_U_^DSIO(19641.112,CT,1,WP,0)
 . . . S DDCSCT=DDCSCT+1
 Q
 ;
WT() ; Convert the stored #lbs #oz (#g) to #|#|#
 N VAL
 S VAL=$TR($$UP^XLFSTR($P(STR,U,6))," ()G")
 Q $TR($TR(VAL,"LBS","|"),"OZ","|")
