DSIO0PP ;DSS/TFF - POST INSTALL;06/28/2013 15:19
 ;;1.0;DSIO 1.0;**1**;Feb 06, 2015;Build 19
 ;Copyright 1995-2015,Document Storage Systems Inc. All Rights Reserved
 ;
 ;
 ;
 ;
 D REIX,CONVT
 N DIK
 K ^DSIO(19641.12,"B"),^DSIO(19641.12,"C"),^DSIO(19641.12,"D")
 K ^DSIO(19641.12,"P"),^DSIO(19641.12,"CHILD")
 S DIK="^DSIO(19641.12," D IXALL2^DIK,IXALL^DIK
 K ^DSIO(19641.13,"P")
 S DIK="^DSIO(19641.13," D IXALL2^DIK,IXALL^DIK
 D ^DSIO99
 Q
 ;
REIX ; ReIndex Files
 K ^DSIO(19641.2,"C")
 N DIK
 S DIK="^DSIO(19641.2," D IXALL2^DIK,IXALL^DIK
 S DIK="^DSIO(19641.83," D IXALL2^DIK,IXALL^DIK
 Q
 ;
CONVT ; Convert DATA from old stores
 N DIK
 I $D(^TMP($J,"DSIO THEO","E")) D
 . D CPHONE
 . S DIK="^DSIO(19641.1," D IXALL2^DIK,IXALL^DIK
 I $D(^TMP($J,"DSIO THEO","O")) D
 . S DIK="^DSIO(19641.12," D IXALL2^DIK,IXALL^DIK
 . D COBS
 I $D(^TMP($J,"DSIO THEO","P")) D
 . S DIK="^DSIO(19641.11," D IXALL2^DIK,IXALL^DIK
 . D PERSON
 K ^TMP($J,"DSIO THEO")
 Q
 ;
CPHONE ; Update 19641.1 Phone Multiple
 N NAM,IEN,STR,PHONE,RET
 S NAM="" F  S NAM=$O(^DSIO(19641.1,"B",NAM)) Q:NAM=""  D
 . K IEN,STR,PHONE,RET
 . S IEN=$O(^DSIO(19641.1,"B",NAM,""))
 . S STR=$G(^DSIO(19641.1,IEN,2)) Q:STR=""
 . K ^DSIO(19641.1,IEN,2)
 . S:$P(STR,U)'="" PHONE(0)="H^"_$P(STR,U)
 . S:$P(STR,U,2)'="" PHONE(1)="WP^"_$P(STR,U,2)
 . S:$P(STR,U,3)'="" PHONE(2)="MC^"_$P(STR,U,3)
 . S:$P(STR,U,4)'="" PHONE(3)="FAX^"_$P(STR,U,4)
 . I $D(PHONE) D SENT^DSIO1(.RET,IEN,,,,,,.PHONE,1)
 Q
 ;
PERSON ; *** Update 19641.11 Phone and Diagnosis Multiples
 N NAM,IEN,STR,PHONE,PROB,RET
 S NAM="" F  S NAM=$O(^DSIO(19641.11,"B",NAM)) Q:NAM=""  D
 . K IEN,STR,PHONE,RET
 . S IEN=$O(^DSIO(19641.11,"B",NAM,""))
 . S STR=$G(^DSIO(19641.11,IEN,1)) I STR'="" D
 . . K ^DSIO(19641.11,IEN,1)
 . . S:$P(STR,U,7)'="" PHONE(0)="H^"_$P(STR,U,7)
 . . S:$P(STR,U,9)'="" PHONE(1)="WP^"_$P(STR,U,9)
 . . S:$P(STR,U,8)'="" PHONE(2)="MC^"_$P(STR,U,8)
 . . S ^DSIO(19641.11,IEN,1)=$P(STR,U,1,6)
 . I $D(^DSIO(19641.11,IEN,2)) D DG(IEN)
 . Q:'$D(PHONE)&('$D(PROB))
 . D SAVE^DSIO9(.RET,IEN,,,,,.PHONE,,,,,.PROB,1)
 Q
 ;
DG(IEN) ; Add DIAGNOSIS to DSIO PERSON return
 N CT,I
 S CT="" F I=0:1 S CT=$O(^DSIO(19641.11,IEN,2,"B",CT)) Q:CT=""  D
 . S PROB(I)=CT
 K ^DSIO(19641.11,IEN,2)
 Q
 ;
COBS ; *** Update 19641.12 Whole File
 N DATE,IEN,STR,DFN,OBJ,CAT,CODE,USER,PUSH,IPT
 S DATE="" F  S DATE=$O(^DSIO(19641.12,"B",DATE)) Q:DATE=""  D
 . S IEN=0 F  S IEN=$O(^DSIO(19641.12,"B",DATE,IEN)) Q:'IEN  D
 . . K STR,DFN,OBJ,CAT,CODE,USER,PUSH,IPT
 . . S STR=$G(^DSIO(19641.12,IEN,0)) Q:STR=""
 . . S DFN=$P(STR,U,2)
 . . I $P(STR,U,3),$D(^DSIO(19641.112,$P(STR,U,3))) S OBJ(0)="FB."_$P(STR,U,3)
 . . I $P(STR,U,4),$D(^DSIO(19641.13,$P(STR,U,4))) S OBJ(1)="PG."_$P(STR,U,4)
 . . S CAT=$P(STR,U,5) S:CAT CAT=$$GET1^DIQ(19641.122,$P(STR,U,5)_",",.01)
 . . ; CODE = SYS NAME^SYS VALUE^CODE^DISPLAY NAME
 . . S:$P(STR,U,6)'="" $P(CODE,U,3)=$$DCODE^DSIO2($P(STR,U,6))
 . . S:$P(STR,U,7)'="" $P(CODE,U)=$$GET1^DIQ(757.03,$O(^LEX(757.03,"B",$P(STR,U,7),""))_",",1)
 . . S:$P($G(^DSIO(19641.12,IEN,1)),U)'="" $P(CODE,U,4)=^DSIO(19641.12,IEN,1)
 . . S USER=$P(STR,U,8)
 . . S VAL=$P(^DSIO(19641.12,IEN,2),U)
 . . S PUSH=$$GET1^DIQ(19641.124,$P($G(^DSIO(19641.12,IEN,4)),U)_",",.01,"I")
 . . K ^DSIO(19641.12,IEN)
 . . S ^DSIO(19641.12,IEN,0)=DATE_U_DFN_U_CAT_"^^^^"_USER_U_$$NOW^XLFDT_U_PUSH
 . . D TOBJ^DSIO10(IEN,DFN,.OBJ)
 . . ; *** SAVE CODE AND VALUE CODE
 . . I $D(CODE) D
 . . . S IPT(19641.17,"?+1,"_IEN_",",.01)="C"                    ; CODE TYPE
 . . . S IPT(19641.17,"?+1,"_IEN_",",1)=$P(CODE,U,3)             ; CODE
 . . . S IPT(19641.17,"?+1,"_IEN_",",2)=$P(CODE,U,4)             ; DISPLAY NAME
 . . . S IPT(19641.17,"?+1,"_IEN_",",3)=$P(CODE,U)               ; SYSTEM NAME
 . . S:VAL'="" IPT(19641.12,IEN_",",4.2)=VAL                     ; VALUE
 . . D UPDATE^DIE(,"IPT")
 Q
